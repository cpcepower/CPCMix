10 '
20 MEMORY &2FFF
30 BORDER 0:INK 0,0:PAPER 0:PEN 0:MODE 1:WINDOW 1,1,1,1
40 ON ERROR GOTO 1010
50 LOAD "9.BIN",&3000:LOAD "#.BIN",&A340:CALL &A340
60 ENV 1,60,-1,12:ENV 2,60,-1,8:ENT 1,25,25,2 
70 MODE 1:INK 0,0:BORDER 0:PEN 1:INK 1,26:LOCATE 1,19
80 M=PEEK(&A300):IF M=0 OR M>5 THEN CALL 0
90 ON M GOSUB 110,180,240,300,380
100 ON M GOTO 600,730,600,470,730
110 INK 0,0:INK 1,12:INK 2,18:INK 3,25:CALL &9001,64:PEN 3
120 PRINT "    PEU DE TEMPS APRES LE DECLENCHEMENT"
130 PRINT "DE L'ALARME,LA POLICE VOUS ARRETE POUR "
140 PRINT "INFRACTION.VOTRE PEINE SERA LEGERE,MAIS"
150 PRINT "ELLE VOUS EMPECHERA DE RETROUVER SOPHIE"
160 PRINT "A TOUT JAMAIS."
170 RETURN
180 INK 0,0:INK 1,3:INK 2,6:INK 3,24:CALL &9001,61:PEN 3
190 PRINT "     EN VOUS ENGAGEANT DANS LE GOUFFRE,"
200 PRINT " VOUS GLISSEZ."
210 PRINT " VOTRE  CHUTE  EST SI TERRIBLE QUE VOUS"
220 PRINT " VOUS ROMPEZ LES MEMBRES."
230 RETURN
240 INK 0,0:INK 1,1:INK 2,2:INK 3,14:CALL &9001,62:PEN 3
250 PRINT "     LE CABLE ETAIT SOUS HAUTE-TENSION "
260 PRINT "  EN LE COUPANT,VOUS VOUS ELECTROCUTEZ "
270 PRINT "     LE CHOC EST SI INTENSE QUE VOUS EN"
280 PRINT "  MOURREZ."
290 RETURN
300 INK 0,0:INK 1,2:INK 2,11:INK 3,23:CALL &9001,60:PEN 3
310 PRINT "  VOUS SENTEZ LE FROID D'UNE ARME A FEU"
320 PRINT " VOUS VOUS RETOURNEZ ,  UN COUP DE FEU "
330 PRINT " RETENTIT,IL VOUS EST DESTINE."
340 PRINT "   LA VISION  DE CETTE  JEUNE FILLE QUE"
350 PRINT " VOUS NE RETROUVEREZ JAMAIS, SERA VOTRE"
360 PRINT " DERNIERE PENSEE AVANT DE MOURIR."
370 RETURN
380 INK 0,0:INK 1,5:INK 2,10:INK 3,20:CALL &9001,63:PEN 3
390 PRINT "       LA PORTE S'OUVRE,VOUS CROYEZ "
400 PRINT "   APERCEVOIR SOPHIE,MAIS SOUDAIN UN"
410 PRINT "   VENT VOUS TRAVERSE, UNE MALADIE  "
420 PRINT "   HORS DU COMMUN VOUS RONGE A UNE  "
430 PRINT "   VITESSE INCROYABLE."
440 PRINT "     PEUT-ETRE AURAIT-IL FALLUT LIRE"
450 PRINT "   LA PANCARTE !"
460 RETURN
470 FOR I=0 TO 39
480 SA=PEEK(&9400+2*I)+256*PEEK(&9401+2*I)
490 SB=PEEK(&9450+2*I)+256*PEEK(&9451+2*I)
500 IF I MOD 4=0 THEN SOUND 2,SB/4,240,15,1 
510 SOUND 1,SB,29,15,1:SOUND 4,SA,60,15,1
520 SOUND 1,0,1
530 SOUND 1,SB,29,15,1
540 SOUND 1,0,1
550 GOSUB 890
560 IF F=1 THEN I=39
570 NEXT I
580 IF F=1 THEN GOTO 930
590 GOTO 470
600 FOR I=0 TO 51
610 EN=INT(I/4)
620 IF I MOD 4=0 THEN SA=PEEK(&94A0+2*EN)+256*PEEK(&94A1+2*EN)
630 SB=PEEK(&94BA+2*I)+256*PEEK(&94BB+2*I)
640 EC=8*(I/4-EN)
650 SOUND 1,SB,56,15,1
660 SOUND 2,SA,28,15:SOUND 2,SA/2,28,15
670 SOUND 4,SA/6,27,15-EC:SOUND 4,0,1:SOUND 4,SA/8,28,15-EC
680 GOSUB 890
690 IF F=1 THEN I=51
700 NEXT I
710 IF F=1 THEN GOTO 930
720 GOTO 600
730 L=-1
740 FOR I=0 TO 23
750 SA=PEEK(&9522+2*I)+256*PEEK(&9523+2*I)
760 SOUND 1,SA  ,56,15,1,1:SOUND 1,2*SA,56,15,1:SOUND 1,SA,56,15,1  
770 SOUND 1,2*SA,28,15,1  :SOUND 1,SA  ,27,15,1:SOUND 1,0,1
780 SOUND 2,SA  ,56,15,2  :SOUND 2,SA/2,56,15,2
790 SOUND 2,SA  ,56,15,2  :SOUND 2,SA/2,56,15,2
800 DUN=224
810 L=L+1
820 SB=PEEK(&9552+3*L)+256*PEEK(&9553+3*L):DU=PEEK(&9554+3*L)
830 SOUND 4,SB,DU,15      :DUN=DUN-DU:IF DUN<>0 THEN 810
840 GOSUB 890
850 IF F=1 THEN I=23
860 NEXT I
870 IF F=1 THEN GOTO 930
880 GOTO 730
890 IF INKEY(47)<>0 THEN F=0 ELSE F=1
900 IF INKEY(34)=0 THEN F=2
910 IF INKEY(46)=0 THEN F=3
920 RETURN
930 CALL &BCA7:MODE 2:INK 1,26:INK 0,0:PAPER 0:CLS:PEN 1:CALL &A380
940 LOCATE 21,10:PRINT "DESIREZ-VOUS FAIRE UNE AUTRE PARTIE ?"
950 LOCATE 21,12:PRINT "          [O]UI OU [N]ON"
960 WINDOW 1,1,1,1:PAPER 0:PEN 0
970 GOSUB 890
980 IF F=2 THEN RUN "4.BIN"
990 IF F=3 THEN CALL 0
1000 GOTO 970
1010 CALL 0
1020 '