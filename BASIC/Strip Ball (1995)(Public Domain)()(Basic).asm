10 ' STRIPBALL : CHEAT MODE : TOUCHE [R]
20 CALL &BB48
30 SPEED KEY 100,100
40 points=0:vie=3:cheatmode=0
50 MODE 1:INK 0,0:BORDER 0:INK 1,0:INK 2,0:INK 3,0:PAPER 0:CLS
60 IF PEEK(&A600)=0 THEN MEMORY &4E13:LOAD"STRIPBAL.N03":CALL &A180
70 IF PEEK(&A600)=0 THEN MEMORY &52F0:LOAD"STRIPBAL.N04":CALL &A180     
80 GOTO 650
90 INK 1,0:INK 2,0:INK 3,0:PAPER 0:CLS:kkl=5+pa:LOCATE 1,kkl
100 FOR i=1 TO 40:FOR t=1 TO 3
110 PEN (t):PRINT CHR$(233);
120 NEXT t,i:GOSUB 950:PLOT 287,0,1:DRAW 287,400,1
130 INK 1,11:INK 2,26:INK 3,20:PAPER 0:ballx=20:bally=20:PEN 3:LOCATE 30,1:PRINT"LEVEL:";lev
140 balli=1:ballj=-1
150 raqux=30
160 IF raqux=18 THEN raqux=19
170 IF raqux=40-1-larg THEN raqux=raqux-1
180 LOCATE raqux,25:PEN 2
190 PRINT " ";STRING$(larg,131);" "
200 LOCATE ballx,bally:PRINT"  "
210 ballx=ballx+balli
220 bally=bally+ballj
230 LOCATE ballx,bally:PEN 15
240 brik$=COPYCHR$(#0)
250 PRINT CHR$(231)
260 IF brik$=CHR$(233) THEN LOCATE 1,1:points=points+2:PRINT "SCORE:";points:ballj=1:SOUND 1,100,5,15,1,1:IF INT(RND*2)=0 THEN balli=-1 ELSE balli=1
270 IF points=40 THEN RETURN
280 IF ballx=19 THEN balli=1
290 IF ballx=39 THEN balli=-1
300 IF bally=2 THEN ballj=1
310 IF bally=24 THEN SOUND 1,500,5,15,1,1:GOTO 370  
320 a$=INKEY$:a$=LOWER$(a$)
330 IF a$="o" OR a$="O" THEN raqux=raqux-1
340 IF a$="r" OR a$="R" THEN GOTO 870
350 IF a$="p" OR a$="P" THEN raqux=raqux+1
360 GOTO 160
370 IF ballx>=raqux AND ballx<(raqux+larg+2) THEN ballj=-1:GOTO 160
380 vie=vie-1:LOCATE 20,15:IF vie=-1 THEN GOTO 860
390 PRINT"RESTE";vie;"VIES"
400 LOCATE 19,24:PRINT STRING$(21," ");:LOCATE 19,25:PRINT STRING$(21," ");
410 FOR r=100 TO 200:SOUND 1,r,4:NEXT r
420 :
430 CALL &BB06:LOCATE 19,15
440 PRINT STRING$(20," ")
450 GOTO 130
460 WINDOW 1,19,2,25:PAPER 1:PEN 2:CLS
470 PRINT:PRINT"STRIPBALL   (C)1995":PEN 3:PRINT:PRINT:PRINT"Touches:":PEN 0:PRINT"[O] et [P]":PRINT:PEN 3
480 PRINT"Jeu:":PEN 0
490 PRINT"Chaque fois que    vous atteignez le  score de 40, vous  passez au niveau   suivant."
500 PRINT"A chaque Level,Lynese deshabille.":PRINT
510 PRINT"La taille de votre raquette diminue   dans les niveaux 1 a 5. Puis les bri- ques se rapprochentdans les niveaux 6 a 9":CALL &BB06:PRINT:PRINT:PEN 3:PRINT"Restrictions:"
520 PEN 0:PRINT"Stripball ne fonc- tionne pas sur     CPC 464. Sorry...":CALL &BB06:RETURN
530 ENV 1, 10 ,-1 , 2 
540 ENT 1,5, 0 , 10
550 DIM rec( 89 ,3):OPENIN "stripbal.n02
560 FOR sss=1 TO  89 :FOR voix=1 TO 3:INPUT #9,rec(sss,voix):NEXT voix:NEXT sss
570 CLOSEIN
580 |IMG,&6000,&C000,0:BORDER 0:INK 1,11:INK 2,26:INK 3,20:d=20:sss=1
590 IF rec(sss,1)<>0 THEN SOUND 1,rec(sss,1), d ,15,1,1, 0
600 IF rec(sss,2)<>0 THEN SOUND 2,rec(sss,2), d ,15,1,1, 0
610 IF rec(sss,3)<>0 THEN SOUND 4,rec(sss,3), d ,15,1,1, 0 
620 a$=INKEY$:IF a$<>"" THEN ERASE rec:RETURN
630 sss=sss+1:IF sss=90 THEN sss=1
640 GOTO 590
650 SPEED KEY 100,100
660  MEMORY &643A:LOAD "stripbal.n01":CALL &A380:POKE &A600,1
670 SPEED KEY 100,100:GOSUB 530:GOSUB 460   
680 MODE 1:PAPER 0:CLS:pa=0:lev=1:SPEED KEY 1,1
690 points=cheatmode:larg=6:s$="1":GOSUB 90
700 lev=2
710 points=cheatmode:larg=5:s$="2":GOSUB 90
720 lev=3
730 points=cheatmode:larg=4:s$="3":GOSUB 90
740 lev=4
750 points=cheatmode:larg=3:s$="4":GOSUB 90
760 lev=5
770 points=cheatmode:larg=2:s$="5":GOSUB 90
780 lev=6
790 pa=2:points=cheatmode:s$="6":GOSUB 90 
800 lev=7
810 pa=3:points=cheatmode:s$="7":GOSUB 90
820 lev=8
830 pa=4:points=cheatmode:s$="8":GOSUB 90 
840 lev=9
850 pa=5:points=cheatmode:s$="9":GOSUB 90:WINDOW 20,40,1,22:CLS:CLEAR INPUT:PRINT"VOUS AVEZ GAGNE !!!":PRINT:PRINT"PRESSEZ UNE TOUCHE":SPEED KEY 100,100:cheatmode=0:CALL &BB06:MODE 1:yan=0:CLEAR:RUN
860 WINDOW 20,40,1,22:CLS:CLEAR INPUT:PRINT"VOUS AVEZ PERDU !!!":PRINT:PRINT"PRESSEZ UNE TOUCHE":SPEED KEY 100,100:cheatmode=0:CALL &BB06:MODE 1:yan=0:CLEAR:RUN 
870 CLEAR INPUT:CALL &BB00:CALL &BB03:SPEED KEY 100,100:CLEAR INPUT:LOCATE 1,1:INPUT"CHEATMODE LEVEL:",l:LOCATE 1,1:INPUT"           Lives:",vie
880 LOCATE 1,1:PRINT"AUTO-PASS     (Y/N)"
890 a$=INKEY$
900 IF UPPER$(a$)="Y" THEN cheatmode=38:GOTO 930
910 IF UPPER$(a$)="N" THEN cheatmode=0:GOTO 930
920 GOTO 890
930 LOCATE 1,1:PRINT"GREETINGS FROM ROM !!! ":CALL &BB06:SPEED KEY 1,1:ON l GOTO 680,700,720,740,760,780,800,820,840
940 SPEED KEY 100,100:GOTO 870
950 IF S$="1" THEN OUT &7F00,&C0:|IMG,&8570,&C000,0:OUT &7F00,&C0:RETURN
960 IF S$="2" THEN OUT &7F00,&C4:|IMG,&4000,&C000,0:OUT &7F00,&C0:RETURN
970 IF S$="3" THEN OUT &7F00,&C4:|IMG,&6000,&C000,0:OUT &7F00,&C0:RETURN   
980 IF S$="4" THEN OUT &7F00,&C5:|IMG,&4000,&C000,0:OUT &7F00,&C0:RETURN  
990 IF S$="5" THEN OUT &7F00,&C5:|IMG,&6000,&C000,0:OUT &7F00,&C0:RETURN   
1000 IF S$="6" THEN OUT &7F00,&C6:|IMG,&4000,&C000,0:OUT &7F00,&C0:RETURN  
1010 IF S$="7" THEN OUT &7F00,&C6:|IMG,&6000,&C000,0:OUT &7F00,&C0:RETURN 
1020 IF S$="8" THEN OUT &7F00,&C7:|IMG,&4000,&C000,0:OUT &7F00,&C0:RETURN  
1030 IF S$="9" THEN OUT &7F00,&C7:|IMG,&6000,&C000,0:OUT &7F00,&C0:RETURN  
1040 RETURN