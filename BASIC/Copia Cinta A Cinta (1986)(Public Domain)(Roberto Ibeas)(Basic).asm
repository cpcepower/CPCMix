10  '|TAPE.IN
20 ON BREAK GOSUB 710
30 MEMORY &1F3F
40 MODE 1:BORDER 0:PEN #2,3
50 WINDOW #1,1,40,1,5:PAPER #1,3
60 WINDOW #2,1,40,6,20:PAPER #2,10 
70 WINDOW #3,1,40,21,26:PAPER #3,8   
80 DIM TI$(6),B$(7)
90 FOR I=0 TO 6:READ TI$(I):NEXT I
100 FOR I=1 TO 7:READ B$(I):NEXT I
110 DATA BASIC,BASIC PROTEGIDO,BINARIO,BINARIO PROTEGIDO,SALTO PANTALLA,,ASCII
120 DATA NOMBRE DEL FICHERO...,BLOQUE NUMERO........,TIPO DE FICHERO.....,LONGITUD DEL BLOQUE.,LOCALIZACION DATOS..,LONGITUD TOTAL....,DIRECCION DE EJECUCION.
130 CLS #2:LOCATE #2,11,5:PRINT #2,"COPIA CINTA A CINTA"
140 LOCATE #2,6,8:PRINT #2,"PROGRAMAS EN BASIC Y BINARIOS"
150 GOSUB 910
160 GOSUB 690
170 GOSUB 930
180 T=0:H=&1F:A=&1FA4:P=A:FOR i=p+24 TO p+25:POKE i,0:NEXT
190 RESTORE 870:M=0:I=0:WHILE I<1
200 GOSUB 870:I$=INKEY$:IF I$="" THEN 200
210 I=1:WEND:GOSUB 940:GOSUB 730
220 CALL &BC6E
230 CALL &1F40
240 IF PEEK(P+24)+PEEK(P+25)*256=0 THEN 980
250 GOSUB 780
260 B=PEEK(A+19):C=PEEK(A+20)
270 POKE &1F4F,B:POKE &1F50,C
280 CALL &1F4C
290 IF PEEK(A+17)<>0 THEN CLS #3:GOTO 380
300 IF H=161 THEN 330
310 A=A+&A00:H=H+&A:P=A
320 POKE &1F47,H:POKE &1F53,H:GOTO 230
330 CLS #3:PRINT #3:PRINT #3,"      SE HA LLENANDO LA MEMORIA"
340 PRINT #3,"      GRABA LOS DATOS MEMORIZADOS"
350 PRINT #3,"      Y VUELVE A LA SUBRUTINA DE LECTURA"
360 PRINT #3,"      PARA COMPLETAR EL PROGRAMA"
370 FOR Z=1 TO 4000:NEXT Z
380 T=0:CALL &BC71:CLS #2
390 GOSUB 950
400 GOSUB 910
410 LOCATE #2,4,3:PRINT #2,"GRABACION: VELOCIDAD Y PROTECCION":PRINT #2
420 PRINT #2:PRINT #2,"    1 => (VEL. NORMAL / NO PROTEGIDO)"
430 PRINT #2:PRINT #2,"    2 => (VEL. RAPIDA / NO PROTEGIDO)"   
440 PRINT #2:PRINT #2,"    3 => (VEL. NORMAL / PROTEGIDO)"  
450 PRINT #2:PRINT #2,"    4 => (VEL. RAPIDA / PROTEGIDO)" 
460 LOCATE #2,3,15:RESTORE 870:M=0:I=0:WHILE I<1
470 GOSUB 870:S$=INKEY$:IF S$="" THEN 470 ELSE S=VAL(S$)
480 IF (S>4 OR S<1) THEN 470
490 IF (S MOD 2)=0 THEN SPEED WRITE 1:V$="RAPIDO" ELSE SPEED WRITE 0:V$="SEGURO"
500 I=1:WEND:GOSUB 970
510 J=&1FA4:IF S<3 THEN GOSUB 660 ELSE GOSUB 630
520 CALL &BC6E:CALL &BC71:P=&1FA4:N=&1F:CALL &BC6E
530 GOSUB 730
540 GOSUB 780:CALL &1F58
550 K=PEEK(P+19):L=PEEK(P+20)
560 POKE &1F67,K:POKE &1F68,L
570 CALL &1F64
580 IF PEEK(P+17)<>0 THEN 710
590 IF N=H THEN 710
600 P=P+&A00:N=N+&A
610 POKE &1F5F,N:POKE &1F6B,N
620 FOR X=1 TO 2200:NEXT:GOTO 540
630 IF PEEK(J+18)=0 THEN POKE (J+18),1
640 IF PEEK(J+18)=2 THEN POKE (J+18),3   
650 IF J=A THEN RETURN ELSE J=J+&A00:GOTO 630
660 IF PEEK(J+18)=1 THEN POKE (J+18),0   
670 IF PEEK(J+18)=3 THEN POKE (J+18),2    
680 IF J=A THEN RETURN ELSE J=J+&A00:GOTO 660   
690 RESTORE 700:FOR V=&1F40 TO &1F6F:READ D:POKE V,D:NEXT:RETURN
700 DATA 62,44,17,64,0,33,164,31,205,161,188,201,62,22,17,0,0,33,229,31,205,161,188,201,62,44,17,64,0,33,164,31,205,158,188,201,62,22,17,0,0,33,229,31,205,158,188,201
710 CALL &BC71:CLS #3:LOCATE #3,9,3:PRINT #3,"ESPERE.   INICIALIZANDO"
720 FOR G=1 TO 2000:NEXT:CALL &BB03:GOTO 130
730 CLS #1:LOCATE #1,8,3:PRINT #1,"ESPECIFICACION DE DATOS"
740 CLS #2:FOR I=1 TO 7
750 LOCATE #2,3,I*2:PRINT #2,B$(I)
760 NEXT I
770 RETURN
780 IF T=0 THEN LOCATE #2,24,2:IF PEEK(P)=0 THEN PRINT #2,"INNOMINADO" ELSE FOR F=P TO (P+15):IF PEEK(P)<>0 THEN PRINT #2,CHR$(PEEK(F));:NEXT
790 LOCATE #2,23,4:PRINT #2,PEEK(P+16);:IF PEEK(P+17)<>0 THEN PRINT #2,"ULTIMO BLOQUE"
800 IF T=0 THEN LOCATE #2,24,6:PRINT #2,TI$(PEEK(P+18))
810 LOCATE #2,23,8:PRINT #2,PEEK(P+19)+256*PEEK(P+20);:PRINT #2,"    "
820 LOCATE #2,23,10:PRINT #2,PEEK(P+21)+256*PEEK(P+22):IF T=1 THEN 860
830 LT=PEEK(P+24)+256*PEEK(P+25):BL=INT(LT/2048):IF BL<LT/2048 THEN BL=BL+1
840 LOCATE #2,23,12:PRINT #2,LT;BL;"BLOQUES"
850 LOCATE #2,23,14:IF PEEK(P+26)+256*PEEK(P+27)=0 THEN PRINT #2,"(NO CONSTA)" ELSE PRINT #2,PEEK(P+26)+256*PEEK(P+27)
860 T=1:RETURN
870 M=M+1:IF M>32 THEN M=1:RESTORE 890
880 READ M1:SOUND 1,M1,15,3:RETURN
890 DATA 60,47,40,47,60,47,40,47,60,47,40,45,40,36,40,40
900 DATA 63,53,45,53,63,53,45,53,63,60,53,45,47,53,60,60
910 CLS #3:LOCATE #3,15,2:PRINT #3,"BURGOS"
920 LOCATE #3,9,4:PRINT #3,"...ROBERTO IBEAS-1986...":RETURN  
930 CLS #1:PRINT #1:PRINT #1,"     INSERTAR CINTA PARA COPIAR":PRINT #1:PRINT #1,"   PULSA PLAY (Y UNA TECLA)":RETURN
940 CLS #3:LOCATE #3,11,3:PRINT #3,"LEYENDO EL PROGRAMA":RETURN
950 CLS #1:PRINT #1:PRINT #1,"  INSERTAR CINTA NUEVA.PULSA REC y PLAY":PRINT #1
960 PRINT #1,"   (SELECCIONA OPCION DEL MENU)":RETURN
970 CLS #1:CLS #3:LOCATE #3,7,3:PRINT #3,"GRABANDO EL PROGRAMA"V$:RETURN
980 CLS #3:LOCATE #3,13,3:PRINT #3,"ERROR DE LECTURA":FOR I=1 TO 3:PRINT #3,CHR$(7):NEXT:FOR I=1 TO 1000:NEXT:GOTO 710
990 |DISC:SAVE "COPION"