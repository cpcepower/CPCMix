1 ON BREAK GOSUB 16:ON ERROR GOTO 17:POKE &BDEE,201:KEY DEF 66,0,0,0,0:IF PEEK(&AC08)=201 THEN POKE-21503,195:POKE-21502,148:POKE-21501,202:POKE &B4E8,255 ELSE POKE &B632,255
10 MEMORY 32988:LOAD"mccrist":MODE 1:INK 0,0:INK 1,0:INK 2,0:INK 3,0:PEN 1:PAPER 0:BORDER 0:DEFINT a-z:LOAD"crispic",&C000:GOTO 20
16 RETURN
17 PRINT:PRINT"E R R E U R !":PRINT"":PRINT"S'IL VOUS PLAIT, INSEREZ / TOURNEZ LA   DISQUETTE!":WHILE INKEY$="":WEND:IF sp=0 THEN RUN ELSE RESUME 200
20 KEY DEF 0,0,137,133,137:KEY DEF 2,0,136,134,136:KEY DEF 8,0,135,135,135:KEY DEF 1,0,132,132,132
50 INK 1,26:INK 2,6:INK 3,5:ENV 1,10,-1,15,2,0,250:ar=107:ao=38:av=23:am=12:sp=83:wl=4:af=3:DIM r(ar),dg(ar,6),ob$(ao),ob(ao),v$(av),m$(am)
90 FOR n=1 TO 14:READ k,f,d:SOUND k,f,d*6,13,1:NEXT:GOTO 5000
95 DATA 1,213,32,2,0,4,2,179,32,4,0,8,4,119,32,1,159,16,2,127,16,4,119,16,1,179,16,2,142,16,4,119,16,1,213,32,2,179,32,4,119,32
140 RESTORE 150:r$=CHR$(13):FOR n=1 TO 6:READ d,dd$(n),d$(n):KEY d,d$(n)+r$:NEXT:ENV 2,15,1,1,15,-1,10:ENT-2,3,-10,1,3,10,1:ENV 3,15,-1,15:ok$="OKAY !":t$="PRESSEZ UNE TOUCHE S.V.P.":w$="TOURNEZ LA DISQUETTE S.V.P.":l$=STRING$(40,95)
150 DATA 137,AU NORD,NORD,136,AU SUD,SUD,135,A L'OUEST,OUEST,132,A L'EST,EST,133,HAUT,HAUT,134,BAS,BAS
160 FOR n=1 TO am:READ m$(n):NEXT:FOR n=0 TO 9:READ r$(n):r$(n)=SPACE$((40-LEN(r$(n)))/2)+r$(n):NEXT
180 DATA IL NE VOUS ARRIVE RIEN D'ETONNANT.,-,-,VOUS NE POUVEZ PAS FAIRE CA.,VOUS NAGEZ DANS LE LAC.,VOUS FAITES SURFACE.,VOUS PLONGEZ DANS LE LAC.,JE NE SAIS PAS COMMENT C'EST CENSE      FONCTIONNER.,VOUS NE L'AVEZ PAS.
181 DATA L'ARAIGNEE VOUS A TUE.,L'HOMME DES CAVERNES VOUS A ECRABOUILLE DE SA MASSUE.,REGARDEZ CE QU'IL Y A DANS LA CAVERNE.
185 DATA DANS UN BOYAU DE LA CAVERNE.,DEVANT UN PRECIPICE A PIC.,DANS UNE GALERIE DE LA CAVERNE.,DANS UNE ENORME SALLE DE LA CAVERNE.,DANS UNE SALLE DE LA CAVERNE.
186 DATA PRES D'UN GRAND LAC SOUTERRAIN.,DANS UN ENORME CANYON.,A LA SURFACE DU LAC.,"DANS LE LAC, SOUS L'EAU.",DANS LA JUNGLE. DES BUISSONS EPAIS.
191 MODE 1:LOCATE 4,11:INK 3,22:PRINT"CHARGER UNE POSITION ?";:GOSUB 5500:IF in$="N"THEN 196 ELSE LOCATE 4,13:PRINT"O.K.":OPENIN"POSDAT":INPUT#9,sp,ce,cl,oc:FOR n=1 TO ar:FOR m=1 TO 6:INPUT#9,dg(n,m):NEXT:NEXT
194 FOR n=1 TO ao:INPUT#9,ob(n):NEXT:FOR n=1 TO af:INPUT#9,f(n):NEXT:CLOSEIN:IF f(1)THEN LOCATE 4,13:PRINT w$
196 PRINT TAB(4)"\nDES EXPLICATIONS ?";:GOSUB 5500:FOR n=1 TO 1000:NEXT:IF in$="O"THEN 2800
200 pr=1:MODE 1:INK 1,22:INK 3,3:PEN 1:PAPER#1,0
210 WINDOW 1,40,19,25:WINDOW#1,1,40,2,16:WINDOW#2,1,40,1,1:WINDOW#3,1,40,17,18
294 IF sp<79 AND sp>74 AND ob(13)=-1 THEN ob(29)=-1:ob(13)=0:mm$="LA TORCHE S'ETEINT DANS L'EAU."
295 rn=RND*30:IF rn=0 AND sp<75 AND sp>1 AND m$=""THEN rn=INT(RND*3):GOSUB 6000
296 IF rn<2 AND sp>78 AND sp<87 AND m$=""THEN rn=INT(RND*3):GOSUB 6500
299 IF(NOT f(2)OR(ob(36)<>sp AND NOT ob(36)))AND NOT(ob(13)OR(sp>68 AND sp<73 AND ob(13)=70))AND(sp<79 AND sp>2 OR sp>89)THEN GOSUB 3400:PEN p:PEN#2,p:PRINT#2,"VOUS NE POUVEZ RIEN VOIR. IL FAIT TRES  NOIR.":GOTO 405
300 GOSUB 3000:PEN p:PEN#2,p:PEN#3,p:CLS#2:PRINT#2,r$(r(sp)):IF pr=0 THEN 350
310 g=0:FOR n=1 TO ao:IF ob(n)<>sp THEN 340
320 IF g=0 THEN cx=0:PRINT"VOUS POUVEZ VOIR:"
322 IF cx<5 THEN cx=cx+1:GOTO 330
324 IF fb THEN 330 ELSE PRINT SPC(9)t$
326 IF INKEY$=""THEN 326 ELSE cx=0:PRINT""SPC(39),""
330 PRINT"-"UPPER$(ob$(n)):g=1:IF n=6 OR n=10 OR n=18 OR n=23 THEN SOUND 2,106,150,0,2,2
340 NEXT
350 pr=1:g=0:FOR n=1 TO 6:IF dg(sp,n)=0 THEN 400
360 IF g=0 THEN CLS#3:PRINT#3,"VOUS POUVEZ ALLER ";
370 g=g+1:IF g=1 THEN PRINT#3,dd$(n);:GOTO 400
380 IF POS(#3)+LEN(dd$(n))<38 THEN PRINT#3,", "dd$(n);:GOTO 400
390 PRINT#3,",":PRINT#3,dd$(n);
400 NEXT:PRINT#3,"."
405 IF mm$<>""THEN PRINT"\n"mm$:mm$=""
406 IF m$<>""THEN PRINT"\n"m$:m$=""
410 '
420 IF f(3)=0 AND sp=29 THEN dg(28,2)=0:ob(18)=28
430 IF sp=54 AND ob(23)=sp THEN cu=cu+1 ELSE cu=0
440 IF sp=33 AND ob(6)=sp THEN cs=cs+1 ELSE cs=0
450 IF sp=77 OR sp=78 THEN cw=cw+1 ELSE cw=0
455 IF sp=85 OR sp=86 THEN cd=cd+1 ELSE cd=0
460 PRINT
470 in$="":SOUND 1,239,8,12:SOUND 1,190,8,12:SOUND 1,119,8,12:LINE INPUT"> ",in$:in$=UPPER$(in$):l=LEN(in$):IF in$=""THEN PRINT"":GOTO 470
472 IF cu=6 THEN m$=m$(11):GOTO 820 ELSE IF cs=6 THEN m$=m$(10):GOTO 820
474 IF cd=4 THEN sp=23:ob(35)=sp:m$="LE SOL S'EST AFFAISSE SOUS VOS PIEDS ET VOUS ETES TOMBE DE DEUX METRES DE HAUT. VOUS RESTEZ INCONSCIENT PENDANT UN      MOMENT.":GOTO 210
475 IF cw=3 THEN m$="VOUS ETES RESTE TROP LONGTEMPS SOUS     L'EAU.":GOTO 820
476 IF sp=28 AND ob(5)<>30 AND ob(31)<>30 THEN m$="VOUS ETES ENFERME ET VOUS DEVEZ MOURIR  DANS LA GROTTE.":GOTO 820
477 IF sp=28 AND ob(18)=sp AND ob(13)<>-1 THEN m$="VOUS ETES EMMURE ET VOUS N'AVEZ PAS DE  FEU POUR FAIRE EXPLOSER LES ROCHERS.":GOTO 820
480 a$=LEFT$(in$,1):IF a$=""THEN 500
485 a=ASC(a$):IF a<91 AND a>64 THEN 500
490 l=l-1:in$=RIGHT$(in$,l):IF in$=""THEN 470 ELSE 480
500 a$=RIGHT$(in$,1):IF a$=""THEN 520
505 a=ASC(a$):IF a<91 AND a>64 THEN 520
510 l=l-1:in$=LEFT$(in$,l):IF in$=""THEN 470 ELSE 500
520 IF in$="OUI"THEN PRINT"NON !":GOTO 410
550 IF in$="NON"THEN PRINT"OUI !":GOTO 410
560 l=LEN(in$):IF l>6 THEN 660 ELSE IF l>1 THEN 600
580 FOR n=1 TO 6:IF in$=LEFT$(d$(n),1)THEN 620
590 NEXT:GOTO 660
600 FOR n=1 TO 6:FOR m=2 TO LEN(d$(n)):IF in$=LEFT$(UPPER$(d$(n)),m)THEN 620
610 NEXT:NEXT:GOTO 660
620 in$=UPPER$(LEFT$(d$(n),2))
630 IF sp=21 AND n=1 AND ob(11)=sp THEN ob(11)=0:dg(sp,2)=0:f(1)=1:m$="A PEINE ETES VOUS ARRIVE DE L'AUTRE COTEQUE LA PLANCHE CEDE."+SPACE$(20)+" ATTENTION: "+w$+" "
640 IF dg(sp,n)<>0 THEN dir=n:sp=dg(sp,n):PRINT ok$:GOTO 210
650 PRINT"VOUS NE POUVEZ ALLER "dd$(n)".":GOTO 410
660 IF LEFT$(in$,3)<>"FIN"THEN 680
670 MODE 1:ENV 1,15,0,10:ENT 1,127,15,1:SOUND 1,30,127,15,1,1
671 LOCATE 4,11:PEN 1:INK 1,22:PRINT"SAUVEGARDER LA POSITION ?";:GOSUB 5500
672 IF in$<>"O"THEN 675 ELSE LOCATE 4,13:IF f(1)=1 THEN PRINT w$:WHILE INKEY$="":WEND
673 LOCATE 4,13:PRINT"O.K.":a$="POSDAT.*":|ERA,@a$:OPENOUT"POSDAT":PRINT#9,sp,ce,cl,oc:FOR n=1 TO ar:FOR m=1 TO 6:PRINT#9,dg(n,m):NEXT:NEXT
674 FOR n=1 TO ao:PRINT#9,ob(n):NEXT:FOR n=1 TO af:PRINT#9,f(n):NEXT:CLOSEOUT:f(1)=0
675 LOCATE 4,13:GOTO 870
680 FOR b=1 TO l:a=ASC(MID$(in$,b,1)):IF a<91 AND a>64 THEN NEXT ELSE b=b-1
690 ev$=LEFT$(in$,b)+" ":ev$=LEFT$(ev$,wl):rl=l-b
700 FOR vnr=1 TO av:IF ev$=v$(vnr)THEN 720
710 NEXT:PRINT"JE NE CONNAIS PAS LE MOT "LEFT$(in$,b)" !":GOTO 410
720 a$=MID$(in$,b+1,1):IF a$=""THEN rl=l-b:GOTO 730
725 a=ASC(a$):IF a<65 OR a>90 THEN b=b+1:GOTO 720 ELSE rl=l-b
730 IF rl<1 THEN o=0:eo$="":GOTO 810
740 mi$=MID$(in$,b+1,4):IF mi$="LES "OR mi$="DES "OR mi$="THE "THEN b=b+4:rl=l-b
745 mi$=MID$(in$,b+1,3):IF mi$="AU "OR mi$="LA "OR mi$="LE "THEN b=b+3:rl=l-b
750 a$=MID$(in$,b+1,1):IF a$=""THEN rl=l-b:GOTO 760
755 a=ASC(a$):IF a<65 OR a>90 THEN b=b+1:GOTO 750 ELSE rl=l-b
760 IF rl<1 THEN 730
770 eob$=RIGHT$(in$,rl):eo$=LEFT$(eob$,wl):IF eo$="RAT"THEN 780
775 IF LEN(eo$)<4 AND vnr<>17 AND vnr<>18 AND vnr<>21 AND vnr<>13 AND vnr<>7 THEN PRINT"TAPEZ S.V.P. AU MOINS 4 LETTRES/MOT!":GOTO 410
780 FOR o=1 TO ao:IF INSTR(ob$(o),eo$)<>0 THEN 800
790 NEXT:o=-1:GOTO 810
800 IF o=8 AND sp=ob(18)THEN o=18 ELSE IF o=8 AND sp<>ob(8)AND sp<>ob(18)THEN o=26 ELSE IF o=9 AND ob(27)=sp THEN o=27 ELSE IF o=27 AND ob(9)=sp THEN o=9 ELSE IF o=13 AND ob(29)THEN o=29
805 IF o=12 AND sp=ob(14)THEN o=14 ELSE IF o=12 AND sp=ob(15)THEN o=15 ELSE IF o=1 AND (sp=ob(30)OR ob(30)=-1)THEN o=30 ELSE IF o=24 AND sp=78 THEN o=25 ELSE IF o=5 AND ob(5)=0 THEN o=31 ELSE IF o=37 AND sp=87 THEN o=38
810 ON vnr GOTO 950,1190,1430,1530,1670,1820,1890,1940,1980,2050,2060,2070,2160,2370,2390,2470,2490,2490,2390,2600,2700,2800,1430
820 MODE 1:IF f THEN SOUND 2,0,180,15,3,,31 ELSE ENV 1,15,-1,20:ENT-1,5,-3,1,20,3,1,5,-3,1:SOUND 1,100,300,15,1,1
830 PEN 1:INK 1,22:PRINT"ET OUI, DOMMAGE...\n":PRINT m$:PRINT"\nVOUS ETES MORT!":PRINT l$"\n\n":GOTO 870
860 MODE 1:INK 1,0:INK 2,0:INK 3,0:LOAD"P0",&C000:INK 1,15:INK 2,11:INK 3,1:INK 0,6:ENV 1,5,1,10,10,-1,10,10,1,10,15,-1,15:ENT-1,4,-11,1,4,10,1:SOUND 1,230,470,10,1,1:WHILE INKEY$="":WEND
867 INK 0,0:CLS:INK 2,22:PRINT"AU CONTACT DU CRISTAL BLEU VOUS SEREZ   DEPLACE PAR UNE PUISSANCE MAGIQUE DANS  UNE TERRE ETRANGERE. VOUS NAGEZ DANS UNEMER.\n"
868 PRINT"ET QUOI ENCORE?  L'ILE?  LE BATEAU?     REQUIN OU BIEN SOUS-MARIN?\n":PRINT"ICI L'AVENTURE TOUCHE A SA FIN.\n\n\n"
870 PRINT"ENCORE ?";:GOSUB 5500:IF in$="N"THEN CALL 0
875 IF f(1)=1 THEN LOCATE 4,16:PRINT w$:WHILE INKEY$="":WEND:RUN ELSE RUN
950 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180
970 IF ob(o)<>sp AND NOT ob(o)THEN 1810
990 IF o=6 THEN PRINT"ELLE S'APPROCHE!":GOTO 410
1010 IF o=10 THEN PRINT"C'EST LE CRISTAL BLEU!":GOTO 410
1020 IF o=11 THEN PRINT"C'EST LONG ET STABLE.":GOTO 410
1030 IF o=16 THEN PRINT"LA DYNAMITE EST VIEILLE MAIS TOUJOURS   BONNE.":GOTO 410
1060 IF o=23 THEN PRINT"IL S'APPROCHE!":GOTO 410
1070 IF o=26 THEN PRINT"IL PARAIT PRECIEUX.":GOTO 410
1080 IF o=27 THEN PRINT"LA PORTE EST OUVERTE.":GOTO 410
1090 IF o=12 OR o=14 OR o=15 THEN PRINT"IL SEMBLE QUE CA VIENNE D'UN FEU.":GOTO 410
1110 IF o=34 AND dg(1,1)=0 THEN m$="DERRIERE LA CASCADE VOUS DECOUVREZ      L'ENTREE D'UNE CAVERNE VERS LE NORD.":dg(1,1)=2:pr=0:GOTO 210
1120 PRINT m$(1):GOTO 410
1180 PRINT"POURRIEZ-VOUS EXPLIQUER CELA D'UNE FACONUN PEU PLUS DETAILLE?":GOTO 410
1190 IF o=0 THEN 1180 ELSE IF eo$="MASS"AND ob(23)=sp THEN PRINT"L'HOMME DES CAVERNES VEUT VOUS BATTRE   AVEC CA!";:GOTO 410
1200 IF o=-1 THEN 1420 ELSE IF ob(o)<>sp AND NOT ob(o)THEN 1810
1220 IF ob(o)=-1 THEN PRINT"VOUS L'AVEZ DEJA.":GOTO 410
1250 IF o=4 OR o=12 OR o=14 OR o=15 OR o=18 OR o=19 OR o=24 OR o=25 OR o>31 AND o<36 OR o=37 OR o=38 THEN PRINT m$(8):GOTO 410
1260 IF o=6 THEN m$=m$(10):GOTO 820
1270 IF o=9 OR o=27 THEN PRINT m$(4):GOTO 410
1320 IF o=20 THEN PRINT"VOUS NE POUVEZ PAS LES ATTRAPER.":GOTO 410
1340 IF o=23 THEN m$=m$(11):GOTO 820
1350 IF o=5 OR o=31 OR o=8 THEN PRINT"C'EST TROP LOURD.":GOTO 410
1355 IF o=21 THEN ob(o)=0:m$="LE SQUELETTE TOMBE EN POUSSIERE.":GOTO 210
1357 IF oc>2 THEN PRINT"VOUS ETES TROP CHARGE.":GOTO 410
1360 IF o=10 THEN 860
1361 IF o=11 AND sp=21 THEN dg(sp,1)=0:GOTO 1370
1362 IF o=13 THEN ob(12)=0:ob(14)=0:ob(15)=0:GOTO 1370
1363 IF o=22 THEN m$="DES VERS TOMBENT.":GOTO 1370
1370 ob(o)=-1:oc=oc+1:PRINT ok$:GOTO 210
1420 IF r2=1 THEN PRINT"QU'EST-CE QUE C'EST QUE CA ?":r2=0:GOTO 410 ELSE PRINT"JE NE CONNAIS PAS CE MOT.":r2=1:GOTO 410
1430 IF o=6 THEN PRINT"PRENEZ-LA EN PREMIER !":GOTO 410
1440 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180 ELSE IF NOT ob(o)THEN PRINT m$(9):GOTO 410
1455 IF sp<79 AND sp>74 THEN ob(o)=0:oc=oc-1:m$="CA DISPARAIT DANS L'EAU.":IF o=36 OR o=13 THEN 210 ELSE PRINT m$:m$="":GOTO 410
1460 IF o=3 AND sp=ob(9)THEN 1730
1490 IF o=22 AND ob(6)=sp THEN ob(6)=0:ob(o)=0:oc=oc-1:dg(33,1)=48:dg(33,4)=41:m$="L'ARAIGNEE ATTRAPE LE CRANE AVEC LES    VERS ET DISPARAIT.":GOTO 210
1500 IF o=26 AND sp=ob(23)THEN m$="L'HOMME DES CAVERNES LAISSE TOMBER SA   MASSUE, S'EMPARE DU CAILLOU ET S'ENFUIT.":ob(28)=sp:ob(23)=0:ob(26)=0:oc=oc-1:dg(54,4)=55:GOTO 210
1510 IF o=11 AND sp=21 THEN dg(sp,1)=22:m$="VOUS POUVEZ VOUS SERVIR DE LA PLANCHE   COMME PONT ET TRAVERSER LA GORGE VERS LENORD.":pr=0
1515 IF o=13 THEN m$="LA TORCHE S'ETEINT.":ob(13)=0:ob(29)=sp:oc=oc-1:GOTO 210
1520 ob(o)=sp:oc=oc-1:PRINT ok$:GOTO 210
1530 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180
1550 IF ob(o)<>sp AND NOT ob(o)THEN 1810
1560 IF o=9 AND ob(3)=58 THEN dg(sp,1)=60:ob(27)=58:ob(o)=0:m$="GRACE AU POUVOIR MAGIQUE DE LA CLE, LA  PORTE S'OUVRE.":GOTO 210
1570 IF o=9 THEN PRINT"D'ABORD VOUS DEVREZ APPORTER LA CLE     MAGIQUE !":GOTO 410
1580 IF o=5 THEN PRINT"LA CAISSE EST FERMEE.":GOTO 410
1585 IF o=31 OR o=27 THEN PRINT"ELLE EST DEJA OUVERTE.":GOTO 410
1600 IF o=24 OR o=25 THEN PRINT"IL EST DEJA OUVERT.":GOTO 410
1610 PRINT m$(8):GOTO 410
1670 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180
1690 IF ob(o)<>sp AND NOT ob(o)THEN 1810
1700 IF o=11 AND sp=21 THEN 1430
1710 IF o=11 THEN PRINT"LA PLANCHE NE SERVIRA A RIEN DANS CE    CAS-CI.":GOTO 410
1715 IF o=1 AND sp=92 AND ob(8)=sp THEN ob(8)=0:dg(92,3)=91:m$="VOUS DEPLACEZ LE GROS CAILLOU.":GOTO 210
1720 IF sp<>58 AND o=3 THEN PRINT"CA NE RENTRE PAS JUSTE.":GOTO 410
1730 IF o=3 AND ob(o)=-1 THEN PRINT"VOUS PLACEZ LA PIERRE CARRE DANS LA     NICHE.":ob(o)=sp:oc=oc-1:GOTO 410
1740 IF o=3 THEN PRINT m$(9):GOTO 410
1750 IF o=16 AND NOT ob(13)THEN PRINT"VOUS N'AVEZ PAS DE FEU POUR ALLUMER LA  MECHE.":GOTO 410
1760 IF o=16 AND sp=28 THEN SOUND 2,0,180,15,3,,31:m$="VOUS LIBEREZ LE CHEMIN.":dg(28,2)=27:ob(18)=0:oc=oc+(ob(o)=-1):ob(o)=0:f(3)=-1:GOTO 210
1770 IF o=16 THEN f=1:m$="PAR LA FORCE DE L'EXPLOSION, LA GROTTE  S'ECROULE.":GOTO 820
1775 IF o=30 AND sp=30 AND ob(5)=sp THEN ob(16)=sp:ob(5)=0:ob(31)=sp:m$="VOUS CASSEZ LA CHAINE ET DECOUVREZ DE LADYNAMITE DANS LA CAISSE.":pr=0:GOTO 210
1776 IF o=36 AND cl>lm THEN PRINT"LES PILES SONT MORTES.":GOTO 410
1777 IF o=36 THEN IF f(2)THEN m$="VOUS ETEIGNEZ LA LAMPE.":f(2)=0:GOTO 210 ELSE m$="VOUS ALLUMEZ LA LAMPE.":f(2)=-1:GOTO 210
1780 PRINT m$(8):GOTO 410
1810 PRINT"JE VOIS PAS CA ICI!":GOTO 410
1820 IF sp<>50 AND sp<>51 AND(sp<75 OR sp>78)THEN PRINT"CE N'EST PAS POSSIBLE A CET ENDROIT.":GOTO 410
1840 IF sp=50 THEN sp=75:m$=m$(5):GOTO 210 ELSE IF sp=51 THEN sp=76:m$=m$(5):GOTO 210 ELSE IF sp=77 THEN sp=75:m$=m$(6):GOTO 210 ELSE IF sp=78 THEN sp=76:m$=m$(6):GOTO 210 ELSE IF sp=75 OR sp=76 THEN PRINT"VOUS ETES DEJA EN TRAIN DE NAGER!":GOTO 410
1890 IF sp<>50 AND sp<>51 AND(sp<75 OR sp>78)THEN 1820
1895 IF sp=50 OR sp=75 THEN sp=77:m$=m$(7):GOTO 210 ELSE IF sp=51 OR sp=76 THEN sp=78:m$=m$(7):GOTO 210
1930 IF sp=77 OR sp=78 THEN PRINT"VOUS ETES DEJA EN TRAIN DE PLONGER!":GOTO 410
1940 IF o=0 AND sp<>1 AND sp<>50 AND sp<>51 AND(sp<75 OR sp>78)THEN PRINT"JE NE VOIS RIEN A BOIRE ICI.":GOTO 410
1945 IF o>0 AND o<39 AND o<>34 THEN PRINT m$(8):GOTO 410
1950 IF o<>34 AND o<>0 THEN PRINT"CA N'EST PAS DISPONIBLE!":GOTO 410
1960 IF sp<>50 AND sp<>51 AND sp<>1 AND(sp<75 OR sp>78)THEN PRINT"JE NE VOIS PAS D'EAU ICI.":GOTO 410
1970 PRINT"VOUS PRENEZ UNE GORGEE D'EAU RAFRAICHISSANTE.":GOTO 410
1980 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180
2000 IF NOT ob(o)THEN PRINT m$(9):GOTO 410
2010 IF sp=21 THEN ob(o)=0:oc=oc-1:m$="CA TOMBE DANS LES PROFONDEURS.":IF o=36 OR o=13 THEN 210 ELSE PRINT m$:m$="":GOTO 410
2020 IF(sp=50 OR sp=51 OR(sp<79 AND sp>74))THEN ob(o)=0:oc=oc-1:m$="CA DISPARAIT DANS L'EAU.":IF o=36 OR o=13 THEN 210 ELSE PRINT m$:m$="":GOTO 410
2040 GOTO 1460
2050 IF sp=21 THEN m$="VOUS DISPARAISSEZ DANS LES PROFONDEURS.":GOTO 820
2052 IF sp=50 THEN m$="VOUS PLONGEZ DANS LE LAC.":sp=75:GOTO 210
2054 IF sp=51 THEN m$="VOUS PLONGEZ DANS LE LAC.":sp=76:GOTO 210
2056 IF(sp>74 AND sp<79)THEN PRINT m$(4):GOTO 410
2057 PRINT"VOUS SAUTEZ EN L'AIR.":GOTO 410
2060 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180
2062 IF ob(o)<>sp AND NOT ob(o)THEN 1810
2063 IF o=6 THEN m$=m$(10):GOTO 820
2064 IF o=16 THEN f=1:m$="LA DYNAMITE A EXPLOSE.":GOTO 820
2065 IF o=23 THEN m$=m$(11):GOTO 820
2066 PRINT"REGARDEZ D'ABORD,REFLECHISSEZ ET ENSUITEAGISSEZ!":GOTO 410
2070 IF o=-1 THEN 1420 ELSE IF o=0 THEN 1180
2090 IF ob(o)<>sp AND NOT ob(o)THEN 1810
2100 IF o=6 OR o=23 THEN PRINT m$(4):GOTO 410
2120 IF o=22 THEN PRINT"CA FAIT DEJA LONGTEMPS QUE CE TYPE EST  MORT.":GOTO 410
2140 PRINT"VOUS AVEZ DES IDEES BIZARRES!":GOTO 410
2160 cf=cf+1:IF cf=4 THEN PRINT"VOUS DEVENEZ CHIANT! CA FAIT LA";"QUATRIEME FOIS QUE VOUS TAPEZ CE MOT";"DEGOUTANT.":GOTO 410
2170 IF cf=6 THEN PRINT"CA SUFFIT! TAPEZ CE MOT UNE AUTRE FOIS  ET VOUS ALLEZ LE REGRETTER!":GOTO 410
2180 IF cf=7 THEN m$="VOUS L'AVEZ SIMPLEMENT DIT TROP SOUVENT!":GOTO 820
2190 IF o=-1 THEN 2280 ELSE IF o=0 THEN 1180
2202 IF ob(o)<>sp AND NOT ob(o)THEN 1810
2205 IF LEN(eo$)<4 THEN vnr=1:GOTO 775
2220 IF o=2 OR o=6 OR o=19 OR o=20 OR o=23 THEN PRINT"APPAREMMENT VOUS N'AVEZ PEUR DE RIEN!":GOTO 410
2230 IF o=21 THEN PRINT"VOUS LE FAITES AVEC N'IMPORTE QUI!":GOTO 410
2240 IF o=16 THEN f=1:m$="VOUS VENEZ JUSTE D'EXAGERER AVEC LE     DYNAMITE!":GOTO 820
2250 PRINT m$(8):GOTO 410
2280 IF eo$="OFF"AND ff=0 THEN PRINT"CETTE EXPRESSION NOUS LAISSE A PENSER   QUE VOTRE NIVEAU INTELLECTUEL PERSONNEL N'EST PAS EXACTEMENT COMPATIBLE AVEC CE JEU EXTRAORDINAIRE!":ff=1:GOTO 410
2300 PRINT"QUAND VOUS SEREZ A MOI...":GOTO 410
2370 IF o=-1 THEN 1810 ELSE IF o<>0 THEN PRINT m$(8) ELSE PRINT"JE NE VOIS RIEN A LIRE ICI."
2380 GOTO 410
2390 g=0:FOR n=1 TO ao:IF ob(n)<>-1 THEN 2450
2400 IF g=0 THEN g=1:PRINT"VOUS TRANSPORTEZ:"
2440 PRINT"-"UPPER$(ob$(n))
2450 NEXT:IF g=0 THEN PRINT"VOUS NE TRANSPORTEZ RIEN."
2460 GOTO 410
2470 IF sp=21 AND ob(11)=-1 THEN PRINT"VOUS TRANSPORTEZ QUELQUE CHOSE.":GOTO 410
2472 IF sp=21 AND ob(11)<>sp THEN PRINT m$(12):GOTO 410
2474 IF sp=28 AND ob(18)=0 THEN 2489
2475 IF sp=28 AND ob(16)=-1 THEN PRINT"VOUS AVEZ LE TRUC POUR VOUS DELIVRER.":GOTO 410
2476 IF sp=28 AND ob(16)=sp THEN PRINT"GARDEZ LES YEUX OUVERTS!":GOTO 410
2477 IF sp=28 THEN PRINT m$(12):GOTO 410
2479 IF sp=33 AND ob(6)=sp THEN PRINT"REFLECHISSEZ-Y MAIS PAS TROP LONGTEMPS!":GOTO 410
2482 IF sp=50 OR sp=51 THEN PRINT"NAGER, C'EST SAIN!":GOTO 410
2484 IF sp=54 AND ob(23)=sp THEN PRINT"MEME LES HOMMES DES CAVERNES ETAIENT    CORRUPTIBLES.":GOTO 410
2486 IF sp=58 AND ob(9)=sp AND ob(3)<>sp THEN PRINT"FAITES RESSORTIR LA CLE MAGIQUE!":GOTO 410
2489 PRINT"VOUS N'AVEZ PAS BESOIN D'AIDE.":GOTO 410
2490 IF o=0 THEN 1180
2495 IF eo$="WEG"THEN PRINT"CA NE VOUS AIDERA PAS.":GOTO 410
2500 eo$=LEFT$(eo$,1):IF eo$="N"OR eo$="S"OR eo$="O"OR eo$="E"OR eo$="B"OR eo$="H"THEN in$=eo$:GOTO 560
2599 PRINT"JE NE COMPRENDS PAS.":GOTO 410
2600 IF ob(16)<>sp AND ob(16)<>-1 THEN PRINT"VOUS N'AVEZ PAS D'EXPLOSIF.":GOTO 410
2602 IF o=0 THEN 1180 ELSE IF o=-1 THEN 1420 ELSE IF o=18 THEN o=16:GOTO 1750 ELSE IF o=16 THEN 1750
2610 IF ob(o)<>sp AND ob(o)<>-1 THEN 1810
2625 IF o=31 AND NOT ob(13)THEN PRINT"VOUS N'AVEZ PAS DE FEU.":GOTO 410
2630 IF o=31 THEN SOUND 2,0,180,15,3,,31:m$="VOUS FAITES EXPLOSER LA BOITE.":ob(o)=0:oc=oc+(ob(16)=-1):ob(16)=0:GOTO 210
2640 IF o=8 THEN f=1:m$="VOUS AVEZ DESTRUIT LA CLE MAGIQUE.":GOTO 820
2650 GOTO 2066
2700 IF o=0 THEN 1180 ELSE IF o=-1 THEN 1420
2710 IF ob(o)<>sp AND NOT ob(o)THEN 1810
2720 IF o=9 THEN PRINT"LA PORTE EST TROP EPAISSE.":GOTO 410
2725 IF o=6 OR o=23 THEN PRINT m$(4):GOTO 410
2730 IF o=18 AND (ob(16)=sp OR ob(16)=-1)THEN o=16:GOTO 1750
2770 GOTO 2066
2800 MODE 1:INK 1,24:INK 2,11:INK 3,15:PEN 2:a$=CHR$(230):PRINT"  "STRING$(36,230):PRINT" "a$;SPC(36);a$:FOR n=1 TO 21:PRINT a$"\n"a$;:NEXT:PRINT" "a$SPC(36)a$:PRINT"  "STRING$(36,230)
2810 PEN 3:LOCATE 6,3:PRINT"ORDRES"SPC(13)"DIRECTIONS":LOCATE 22,5:PRINT"TOUCHES-CURSEUR:":PEN 1:RESTORE 2830:FOR n=5 TO 22:READ a$:LOCATE 3,n:PRINT a$:NEXT:PEN 2
2820 LOCATE 29,9:PRINT CHR$(240):FOR n=10 TO 18:LOCATE 29,n:PRINT CHR$(149):NEXT:LOCATE 29,19:PRINT CHR$(241):LOCATE 25,14:PRINT CHR$(242):FOR n=26 TO 32:LOCATE n,14:PRINT CHR$(154):NEXT:LOCATE 33,14:PRINT CHR$(243):LOCATE 29,14:PRINT CHR$(159):PEN 1
2830 DATA EXAMINE,PREND,"POSE,DONNE,LANCE",OUVRE,UTILISE,"NAGE,PLONGE",BOIS,SAUTE,FRAPPE,TUE,DETRUIT,EXPLOSE,LIS,"AVANCE,COURT",AIDE,"RUSE,INVENTAIRE",VOCABLE,FIN
2840 LOCATE 27,8:PRINT d$(1):LOCATE 28,20:PRINT d$(2):LOCATE 20,14:PRINT d$(3):LOCATE 34,14:PRINT d$(4):LOCATE 23,7:PRINT"(&shift=HAUT)":LOCATE 23,21:PRINT"(&shift=BAS)
2850 LOCATE 9,24:PEN 3:PRINT t$:WHILE INKEY$="":WEND:GOTO 200
3000 fb=0:IF sp=84 OR sp=33 AND ob(6)=sp THEN 3500
3005 IF sp>78 AND sp<87 THEN spb=80:GOTO 3550
3010 IF sp=63 AND ob(26)=sp OR sp=21 AND ob(11)<>sp OR sp=70 AND ob(13)=sp OR sp=92 AND ob(8)=sp OR sp=50 AND dir=1 OR sp=51 AND dir=3 THEN 3500
3020 IF sp=54 AND ob(23)=sp OR sp=30 AND ob(5)=sp OR sp=28 AND ob(18)=sp OR sp=25 AND ob(21)=sp OR sp=58 AND ob(9)=sp OR sp=36 AND ob(22)=sp THEN 3500
3030 IF sp=1 OR sp>86 AND sp<90 OR sp=77 OR sp=78 OR sp=60 OR sp=6 OR sp=45 OR sp=23 AND ob(35)=sp OR sp=5 AND ob(2)=sp THEN 3500
3400 p=1:fb=1:INK 1,22:MODE 1:WINDOW#3,1,40,3,4:WINDOW 1,40,6,25:CLS#1:RETURN
3500 spb=sp:IF sp=87 AND dir=1 OR sp=89 AND dir=4 THEN spb=88
3510 IF sp=78 THEN spb=77
3520 IF sp=51 THEN spb=50
3550 IF spb<>spba THEN a$=STR$(spb):a$="P"+LEFT$(MID$(a$,2),LEN(a$)-1):LOAD a$,32989
3560 RESTORE 3600:READ d:WHILE spb<>d:READ d,d,d,d,d:WEND:READ p:FOR n=1 TO 3:READ d:INK n,d:NEXT:CLS:CALL 42589:spba=spb:RETURN
3600 DATA 1,1,25,9,11,80,1,15,9,18,84,1,19,12,9,88,1,18,12,19,87,2,18,25,15,89,2,18,25,15,50,2,9,12,5,77,2,2,9,18,60,2,11,26,2,54,2,30,17,16,33,1,17,28,4,21,2,17,12,4
3610 DATA 30,1,10,2,1,28,2,9,17,13,25,1,17,11,1,70,1,12,3,30,36,1,15,3,10,63,2,2,20,10,6,1,22,10,13,92,1,26,11,2,45,1,17,14,10,23,1,24,1,3,5,2,15,25,3,58,2,6,20,5
5000 RESTORE 5001:FOR n=1 TO ar:READ r(n):FOR m=1 TO 6:READ dg(n,m):NEXT:NEXT:FOR n=1 TO ao:READ ob$(n),ob(n):NEXT:FOR n=1 TO av:READ v$(n):NEXT
5001 DATA 1,,,79,80,,,2,3,1,,,,,2,4,2,,63,,,,,3,,,,5,,,,,,4,,3,21,7,8,,,,,6,,9,,,,3,20,10,17,6,,,,,,10,7,,,,8,,11,9,,,,,,12,10,,,2,18,13,,11,,,2,12,,,14,,,2,,,13,15,,16
5015 DATA 2,15,15,14,15,,,4,,,,,14,,3,19,18,,8,,,2,17,12,,,,,,91,17,,20,,,,,8,19,,,,2,,6,,,,,2,24,21,,23,,,3,37,26,22,40,,,2,34,22,25,,,,3,31,,,24,,27,2,23,,,69,,,2,28,,,,25,,3,29,27,,,,0
5029 DATA 2,,28,,,,30,3,,,,,29,,2,32,25,,,,,2,33,31,,,,,3,,32,,,,,2,,24,,35,,,2,,,34,36,,,2,43,37,35,38,,,,36,23,,,,,3,47,39,36,,,67,2,38,,,,,74,2,,,23,71,,,2,,42,33,,,,2,41,42,42,42,,0
5043 DATA 2,,36,,,44,,2,,,,,45,43,,,,,,84,44,3,,67,,,47,,2,,38,,,,46,2,,33,,,,49,,50,,,,48,,5,,49,,,,,5,,,,54,,,4,102,,65,,,,2,53,66,53,96,,,3,,,51,,,,2,58,56,54,61,,,2,55,,57,,,0
5057 DATA 2,57,57,57,56,,,3,,55,,59,,,2,,62,58,,,,3,,58,,,,,2,,,55,62,,,2,59,,61,,,,3,,64,3,,65,,4,63,,,,,,3,66,,,52,,63,2,53,65,,,,,,46,,,68,38,,2,68,68,67,68,,,2,,,26,70,,,3,71,,69,72,,0
5071 DATA 2,,70,40,,,,2,,,70,,,73,2,74,,,,72,,2,,73,,,39,,7,,,50,,,,7,,,,51,,,8,,,,78,75,,8,,,77,,76,,9,83,81,82,1,,,9,81,82,83,83,,,9,79,81,83,79,,,9,82,79,83,81,,,9,79,83,83,81,,,9,85,85,85,85,,45
5085 DATA 9,86,86,86,86,,,9,86,86,86,86,,,6,88,95,,,,,6,89,87,,,,,6,,88,90,,,,3,,94,,89,,,2,,19,,92,,,4,,,,93,,,3,,,92,94,,,2,90,94,93,94,,,2,87,96,,,,,3,95,97,53,,,,2,96,99,,,,,2,,,,99,,0
5099 DATA 2,97,,98,100,,,2,,101,99,,,,2,100,103,102,,,,2,,52,,101,,,3,101,105,104,,,,2,,106,,103,,,2,103,,106,,,,2,104,,107,105,,,2,,,,106,,0
5201 DATA une BARRE de bois stable,88,un RAT mort,5,"une PIERRE quadrangulaire, brillante",29,une GORGE profonde devant vous,21,une CAISSE de bois,30,une veritable ARAIGNEE devant la sortie,33,x,0
5207 DATA un gros CAILLOU,92,une PORTE faite en solide rocher,58,un CRISTAL bleu qui brille,60,une grosse PLANCHE,65,LUMIERE pale a l'est,69,une TORCHE allumee,70,LUMIERE pale au sud,71
5211 DATA LUMIERE pale a l'ouest,72,un petit peu de DYNAMITE,0,X,0,un amas de gros CAILLOUX,0,une NICHE,58,des CHAUVE-SOURIS,6
5214 DATA une SQUELETTE humain bleme,25,"un CRANE, plein de vers",36,un HOMME des cavernes avec massue,54,un PASSAGE sous marin a l'est,77,un PASSAGE sous marin a l'ouest,78
5219 DATA un CAILLOU etincelant,63,une vraie PORTE ouverte,0,une grosse MASSUE,0,une TORCHE eteinte,0,une BARRE de fer,27,une CAISSE de bois ouverte,0,une SORTIE au-dessus de vous,45,un TROU cache dans le sol,84
5224 DATA une grande CHUTE d'eau,1,une OUVERTURE au-dessus de vous,0,une LAMPE-TORCHE,2,l'ENTREE d'une grotte,89,l'ENTREE d'une grotte,87
5300 DATA EXAM,PREN,POSE,OUVR,UTIL,NAGE,PLON,BOIS,LANC,SAUT,FRAP,"TUE ",FUCK,"LIS ",RUSE,AIDE,AVAN,COUR,INVE,EXPL,DETR,VOCA,DONN
5400 s=6:FOR n=1 TO s:READ k,f,d:SOUND k,f,d*s,13,1:GOSUB 5470:NEXT
5410 FOR m=1 TO 2:RESTORE 5440:FOR n=1 TO 27:READ k,f,d:SOUND k,f,d*s,13,1:GOSUB 5470:NEXT:NEXT
5415 FOR n=1 TO 40:READ k,f,d,l:SOUND k,f,d*s,l,1:GOSUB 5470:NEXT:FOR m=1 TO 2:RESTORE 5460:FOR n=1 TO 32:READ k,f,d:SOUND k,f,d*s,13,1:GOSUB 5470:NEXT:NEXT
5430 FOR n=1 TO 47:READ k,f,d,l:SOUND k,f,d*s,l,1:GOSUB 5470:NEXT:RESTORE 5456:FOR n=1 TO 14:READ k,f,d,l:SOUND k,f,d*s,l,1:GOSUB 5470:NEXT:GOTO 5410
5439 DATA 1,159,16,2,127,16,4,119,16,1,179,16,2,142,12,4,119,8
5440 DATA 1,851,4,17,426,4,10,106,1,2,,5,1,851,4,2,106,1,1,426,4,2,,5,1,851,4,2,106,20,1,426,4,1,851,4,1,426,4,1,638,4,2,80,1,2,,5,1,319,4,1,638,4,1,319,4,2,80,1,2,,5,17,716,4,10,89,8,1,358,4,2,89,8,1,716,4,1,358,4
5450 DATA 17,268,16,13,10,134,8,14,2,106,4,13,2,89,4,13,1,239,16,13,2,119,8,14,2,95,4,13,2,89,4,13,1,213,32,13,2,106,6,13,2,119,6,13,2,106,12,13,2,106,8,13
5455 DATA 17,268,16,13,10,134,8,14,2,106,4,13,2,89,4,13,1,239,16,13,2,119,8,14,2,95,4,13,2,89,4,13,1,213,20,13,2,106,6,13,2,119,6,13,34,134,10,13,20,,12,13
5456 DATA 1,179,6,10,2,142,6,10,4,134,6,13,1,106,6,10,2,89,6,10,4,71,6,10,1,67,6,13,2,53,6,10,4,45,6,10,1,36,6,10,2,34,6,13,4,27,6,10,1,22,4,10,2,18,2,10
5460 DATA 1,851,4,33,426,4,12,142,1,4,,3,1,851,4,4,119,2,4,113,2,4,106,2,1,426,4,4,89,6,1,851,4,4,106,2,4,,6,1,426,4,1,851,4,1,426,4,4,71,4,1,638,4,4,80,6,1,319,4,1,638,4,4,89,6,1,319,4
5461 DATA 4,106,8,4,119,1,4,,3,4,119,2,4,106,2,1,716,4,1,358,4,1,716,4,1,358,4
5463 DATA 49,268,16,13,42,,4,9,28,,4,9,2,71,4,13,4,89,4,13,2,80,4,13,4,95,4,13,2,89,4,13,4,106,4,13,1,239,16,13,2,80,6,13,4,95,6,13,2,71,10,13,4,89,10,13,1,426,6,14,2,89,6,12,4,71,6,12
5464 DATA 1,478,6,14,2,95,6,12,4,79,6,12,1,426,12,14,2,89,20,12,4,71,20,12,1,71,8,13,49,268,16,13,42,,4,9,28,,4,9,2,71,4,13,4,89,4,13,2,80,4,13,4,95,4,13,2,89,4,13,4,106,4,13,1,239,16,13,2,80,6,13,4,95,6,13,2,71,10,13,4,89,10,13
5466 DATA 1,426,6,14,2,89,6,12,4,71,6,12,1,478,6,14,2,95,6,12,4,79,6,12,1,536,8,14,2,106,10,12,4,89,12,12
5470 IF INKEY$=""THEN RETURN ELSE ENV 1,2,0,250:ENT-1,1,40,3,1,-45,3:CALL &BCA7:SOUND 2,179,162,13,1,1:f=0:GOTO 140
5500 in$="":c$(1)=CHR$(248)+"":c$(2)=CHR$(249)+"":PRINT" (O/N) > ";
5510 FOR n=1 TO 2:FOR m=1 TO 20:PRINT C$(n);:in$=UPPER$(INKEY$):IF in$<>""THEN 5520
5515 NEXT:NEXT:GOTO 5510
5520 IF in$="O"THEN PRINT"OUI":RETURN ELSE IF in$="N"THEN PRINT"NON":RETURN ELSE 5515
6000 IF sp=33 OR sp=54 OR sp=28 THEN RETURN
6010 IF rn=0 THEN m$="UN SERPENT VISQUEUX S'APPROCHE.":RETURN
6020 IF rn=1 THEN m$="UN RAT DETALE DANS LE NOIR.":RETURN
6030 m$="UNE CHAUVE-SOURIS EFFRAYEE EFFLEURE     VOTRE TETE.":RETURN
6500 IF rn=0 THEN m$="UN SERPENT VENIMEUX VOUS ATTAQUE D'UN   ARBRE.":rn=RND*3:IF rn=0 THEN m$(0)=m$+" IL VOUS MORT.":GOTO 820 ELSE m$=m$+" IL VOUS MANQUE ET VOUS ETES      MAINTENANT HORS DE DANGER.":RETURN
6510 IF rn=1 THEN m$="VOUS DEBUSQUEZ UN PUTOIS D'UN FOURRE.":RETURN
6520 m$="UNE GROSSE ARAIGNEE VOUS TOMBE DESSOUS. ":rn=RND*3:IF rn=0 THEN m$(0)=m$+"ELLE VOUS MORT DANS LE COU.":GOTO 820 ELSE m$=m$+"VOUS ARRIVEZ A LA CHASSER.":RETURN