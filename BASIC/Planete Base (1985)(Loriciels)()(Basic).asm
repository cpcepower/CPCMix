2 '***********************************                Planete base                 ***********************************        COPYRIGHT 'LORICIELS'  MAI 85        ***********************************
3 GOTO 250
4 PX=&AA08:PY=&AA0A:PA=&AA06:PL=&A89F:PE=&A97C:P$=CHR$(255):ERP$(0)="  ARRETE,TU M'EN    DONNES DE TROP."
5 ERP$(1)="  TU M'EN A TROP        DONNE.":ERP$(2)=" STOP, C'EST DEJA    UNE DE TROP.":ERM$(0)="    JE N'EN AI"+SPACE$(8)+"PAS ASSEZ.":ERM$(1)=" TU NE M'EN A PAS    ASSEZ DONNE.":ERM$(2)=" IL M'EM MANQUE !"
6 DEMO=0:S=7:N=0:T=0:B=2:C=0:FOR J=0 TO 15:READ M(J):NEXT:H(3)=5:H(7)=5:H(11)=5:FOR J=0 TO 5:READ U(J):NEXT:FOR J=0 TO 4:READ E(J):READ F(J):NEXT:READ F(J):FOR J=0 TO 3:READ MU(J):NEXT
7 FOR SE=0 TO 3:FOR AT=1 TO 3:READ A:L(SE,AT,0,0)=INT(A/256):L(SE,AT,0,1)=A-L(SE,AT,0,0)*256:READ A
8 IF A THEN L(SE,AT,1,0)=INT(A/256):L(SE,AT,1,1)=A-L(SE,AT,1,0)*256 ELSE L(SE,AT,1,0)=L(SE,AT,0,0):L(SE,AT,1,1)=L(SE,AT,0,1)
9 NEXT:FOR A=0 TO 1:FOR C=0 TO 1:L(SE,0,A,C)=L(SE,2,A,C):NEXT:NEXT:NEXT:FOR A=0 TO 3:READ D(A):NEXT
10 ENV 1,1,15,1,1,0,2
11 KEY DEF 0,1,11:KEY DEF 1,1,9:KEY DEF 2,1,10:KEY DEF 8,1,8:KEY DEF 9,1,88:KEY DEF 77,1,13
12 GOTO 31
13 GOSUB 18:C=0:AFTER 1000 GOSUB 29:H=7:J=2:PEN 1:K$=INKEY$:RESTORE 275
14 EI:GOSUB 25:K$=INKEY$:IF C THEN 31 ELSE IF K$="" THEN 14
15 A=ASC(K$):IF A=48 THEN 30 ELSE IF A<49 OR A>(54-I) THEN 17 ELSE C=REMAIN(0)+REMAIN(1):ON (A-48) GOTO 112,74,87,86,31,100,16
16 IF T=1 THEN 118 ELSE 176
17 IF T THEN 14 ELSE C=REMAIN(0)+REMAIN(1):GOTO 112
18 MODE 1:PEN 1:PAPER 0:INK 0,1:INK 1,14:INK 2,24,6:INK 3,6,24:BORDER 1:SPEED INK 200,200:PEN 1:SPEED KEY 30,2
19 LOCATE 1,1:PRINT STRING$(15,".");"Planüte":PRINT TAB(22);"Base";STRING$(15,"."):A=FRE("")
20 LOCATE 5,7:PEN 2:PRINT"-Nouvelle Partie....... 1":PRINT:PEN 3:PRINT TAB(5);"-Choix de la Base...... 2":PRINT:PEN 2:PRINT TAB(5);"-Niveau................ 3":PRINT:PEN 3:PRINT TAB(5);"-Volume Sonore......... 4":PRINT:PEN 2
21 PRINT TAB(5);"-Dýmonstration......... 5":PRINT:PEN 3:PRINT TAB(5);"-Rügle du jeu.......... 6":IF I THEN PRINT:PEN 2:PRINT TAB (5);"-Retour au Jeu......... 7"
22 FOR j=312 TO 142+(32*I) STEP -32:PLOT 472,j,1:GOSUB 24:NEXT
23 PAPER 2-I:LOCATE 27,23-I:PEN 3+I:PRINT"Tape ton choix";:PAPER 0:PEN 1:PRINT"Copyright loriciels 85";:WHILE INKEY$<>"":WEND:RETURN
24 DRAWR -34,0:DRAWR 6,-6:DRAWR 0,-22:DRAWR -6,-4:PLOTR 6,4:DRAWR 22,0:DRAWR 6,-4:DRAWR 0,32:DRAWR -6,-6:DRAWR -20,0:PLOTR 20,0:DRAWR 0,-20:RETURN
25 DI:IF SQ(1)>4 AND SQ(1)<130 THEN RETURN
26 READ NOTE,TEM:IF TEM=0 THEN RESTORE 275:GOTO 26
27 TEM=TEM/1.8:SOUND 4,NOTE,TEM-1,S,1:SOUND 2,NOTE+2*SGN(NOTE),TEM,S,1:SOUND 4,0,1,1
28 RETURN
29 C=1:RETURN 
30 DEMO=1
31 MODE 0:BORDER 1:INTER=0:A=REMAIN (0):RESTORE 274:FOR J=0 TO 15:READ A:INK J,A:NEXT:INK 11,2:PEN 3:PAPER 0:PRINT:PRINT"   DEMONSTRATION:"
32 MX=X:MY=Y:MT=T:MP=P:MP1=P1:P1=0:P=0:MB=B:MN=N:FOR J=1 TO 5:MR(J)=R(J):MQ(J)=Q(J):NEXT
33 IF INKEY$<>"" OR INTER THEN 73
34 IF DEMO THEN N=13
35 AT=0:SE=1:X=80:Y=40:GOSUB 152:PAPER 9:PEN 2:WINDOW 1,20,21,25:CLS:PRINT,"     Bonjour !",," Je m'appelle PEPPO,":GOSUB 257:IF INKEY$<>"" OR INTER THEN 73 ELSE CLS:PRINT," je vais te montrer ",,"   comment jouer."
36 XA=7:YA=40:GOSUB 166:SE=1:GOSUB 152
37 IF TAXE THEN 65 ELSE IF MT THEN 50
38 IF INKEY$<>"" OR INTER THEN 73
39 GOSUB 257:POKE PA,198:POKE PA+1,106:POKE PX,100:POKE PY,70:SOUND 1,200,20,S,1:CALL PL
40 CLS:PRINT,,,"   NUMERUS A FAIM,":GOSUB 257:POKE PX,86:POKE PY,131:POKE PA,91:POKE PA+1,105:CALL PL:CLS:PRINT," il aime les pommes",,:PEN 3:PRINT TAB(7);"ROUGES.";:PEN 2
41 IF INKEY$<>"" OR INTER THEN 73
42 GOSUB 257:GOSUB 257:CLS:PRINT"  Malheureusement,",," elles ne poussent",,"pas sur PLANETE BASE";
43 GOSUB 257:GOSUB 257:SOUND 1,50,20,S,1:POKE PX,86:POKE PY,131:POKE PA,225:POKE PA+1,104:CALL PL:GOSUB 256:POKE PX,86:POKE PY,131:POKE PA,159:POKE PA+1,104:CALL PL
44 IF INKEY$<>"" OR INTER THEN 73
45 CLS:PRINT"Aide moi þ RECOLTER    des fruits que","j'ECHANGERAI contre",,"  une POMME";:PEN 3:PRINT" ROUGE."
46 IF INKEY$<>"" OR INTER THEN 73
47 GOSUB 257:GOSUB 257
48 GOTO 50
49 DRAWR 0,-2*A:DRAWR A,0:DRAWR 0,3*A:DRAWR -A,0:DRAWR 0,-A:DRAWR 2*A,0:DRAWR 0,-A:DRAWR -3*A,0:DRAWR 0,A:DRAWR A,0:RETURN
50 INK 0,1:INK 1,24:INK 2,2:INK 3,6
51 PAPER 0
52 INK 11,1:INK 4,1:MODE 0:FOR J=310 TO 344:PLOT 286,J,4:DRAWR 32,0:NEXT:X=80:GOSUB 152:A=34:LOCATE 10,3:PEN 6:PRINT"ð":LOCATE 9,5:PEN 8:PRINT"ò";:PAPER 4:PEN 0:PRINT"C";:PAPER 0:PEN 9:PRINT"ó":LOCATE 10,7:PEN 12:PRINT"ñ":PLOT 286,344,3:GOSUB 49
53 WINDOW 1,20,21,25:PEN 1:PAPER 2:CLS:PRINT," Regarde comment je",,"     me DEPLACE.":GOSUB 257:INK 9,26:XA=140:GOSUB 166:IF INKEY$<>"" THEN 73 ELSE INK 9,1:YA=110:INK 6,26:GOSUB 166:INK 6,1:XA=100:INK 8,26:GOSUB 166:INK 8,1
54 IF INKEY$<>"" OR INTER THEN 73 
55 INK 12,26:YA=56:GOSUB 166:INK 12,1:LOCATE 3,4:GOSUB 257:PRINT"prends un PANIER.":IF INKEY$<>"" OR INTER THEN 73 ELSE GOSUB 257
56 POKE PY,46:POKE PX,70:POKE PA+1,132:POKE PA,86:CALL PL:GOSUB 257:XA=73:INK 8,26:GOSUB 166:SE=1:GOSUB 152:INK 8,1:INK 4,18:GOSUB 257:POKE PA,152:POKE PA+1,168:GOSUB 153:P1=1
57 INK 11,24:GOSUB 256:POKE PY,46:POKE PX,70:POKE PA+1,132:POKE PA,126:CALL PL:GOSUB 152:INK 4,1
58 IF INKEY$<>"" OR INTER THEN 73
59 POKE PX,70:POKE PY,126:POKE PA,201:POKE PA+1,132:CALL PL:LOCATE 3,4:PRINT"CUEILLE un fruit":GOSUB 257:YA=77:INK 6,26:GOSUB 166:INK 6,1:GOSUB 257
60 IF INKEY$<>"" OR INTER THEN 73
61 POKE PA,152:POKE PA+1,104:POKE PX,69:POKE PY,Y:INK 4,18:CALL PL:GOSUB 256:POKE PX,70:POKE PY,126:POKE PA,230:POKE PA+1,132:CALL PL:INK 4,1:GOSUB 152
62 IF INKEY$<>"" OR INTER THEN 73
63 PAPER 0:GOSUB 256:SE=1:GOSUB 152:GOSUB 257:GOSUB 155:GOSUB 257:WINDOW 2,19,17,23:PEN 1:PRINT"Tout ceci pour la",,"    CUEILLETTE.",,," Chez le MARCHAND",,"c'est autre chose!";:GOSUB 257:GOSUB 257:GOSUB 257
64 IF INKEY$<>"" OR INTER THEN 73
65 B=3:XA=94:YA=70:GOSUB 243:R(2)=5:R(3)=12:GOSUB 244:INK 1,1:PAPER 14:PRINT"COPY":POKE PA,152:POKE PA+1,168:GOSUB 153:JP=3:Q(3)=0:GOSUB 248:P1=1:P=3:INK 11,U(3):GOSUB 152:GOSUB 257:PAPER 9:CLS:PRINT"ó":GOSUB 166
66 CLS:PAPER 14:PRINT" 'COPY'";:PAPER 9:PRINT" te sert a";" DONNER un fruit.":GOSUB 257:CLS:PAPER 14:PRINT"COPY":GOSUB 259:PAPER 9:IF TAXE THEN 70
67 CLS:PRINT" Si la base est 3,tu donnes 3 fruits";:GOSUB 257:GOSUB 257:GOSUB 259:GOSUB 259
68 IF INKEY$<>"" OR INTER THEN 73
69 CLS:PRINT"  Puis,tu valides    avec";:PEN 8:PRINT" 'ENTER'":GOSUB 257:CLS:PRINT"ENTER":GOSUB 257:CLS:PEN 6:PRINT"Si c'est correct..":PT=4:GOSUB 249:JP=4:GOSUB 248:R(4)=1:GOSUB 246:GOSUB 257:GOTO 72
70 CLS:INK 1,U(2):PRINT" Si la base est 3,  tu as 3 FRUITS.":JP=2:PT=2:FOR J=1 TO 3:GOSUB 249:GOSUB 248:R(2)=R(2)+1:GOSUB 246:NEXT:GOSUB 257
71 GOSUB 257
72 CLS:PRINT"Maintenant,  A TOI":GOSUB 261:GOSUB 257:GOSUB 257:IF DEMO THEN 33
73 X=MX:T=MT:Y=MY:P=MP:MP1=P1:B=MB:N=MN:DEMO=0:FOR J=1 TO 5:R(J)=MR(J):Q(J)=MQ(J):NEXT:GOTO 13
74 MODE 1
75 LOCATE 1,1:PEN 1:PRINT"Choix de la base d'echange:":Z=7:K$="....... ":B$="2"+K$+"2":A=2:C=7:GOSUB 84:B$="3"+K$+"3":GOSUB 84:B$="4"+K$+"4":GOSUB 84:B$="5"+K$+"5":GOSUB 84:B$="6"+K$+"6":GOSUB 84:B$="7"+K$+"7":GOSUB 84
76 K$="...... ":A=3:Z=7:C=24:B$="8."+K$+"8":GOSUB 84:B$="9."+K$+"9":GOSUB 84:B$="10"+K$+"A":GOSUB 84:B$="11"+K$+"B":GOSUB 84:B$="12"+K$+"C":GOSUB 84:B$="Surprise D":GOSUB 84
77 FOR J=312 TO 152 STEP -32:PLOT 264,J,1:GOSUB 24:PLOT 536,J:GOSUB 24:NEXT:PEN 2:IF B=16 THEN PAPER 3
78 LOCATE 12,22:PRINT"Hexadecimale.. E":PAPER 0:PLOT 440,72,1:GOSUB 24
79 PLOT 264,61,2:DRAWR 2,1:PLOT 312,397,1:DRAWR 2,1
80 K$=UPPER$(INKEY$):IF K$="" THEN 80 ELSE A=ASC(K$)
81 IF A<50 OR (A>57 AND A<65) OR A>69 THEN 13
82 T=0:I=0:J=0:IF A<60 THEN B=A-48 ELSE IF A<68 THEN B=A-55 ELSE IF A=68 THEN B=INT(RND*11)+2:J=1 ELSE B=16
83 IF J THEN 13 ELSE 75
84 A=3+(A=3):PEN A:IF VAL(B$)=B THEN PAPER 3+(A=3)
85 LOCATE C,Z:PRINT B$:Z=Z+2:PAPER 0:RETURN
86 SPEED KEY 10,2:B$="Sonore":K$="VOLUME":ENV 1,1,15,1,1,0,2:A=S:GOSUB 89:SOUND 129,284,0,1,1:S=A-(A=0):GOTO 13
87 SPEED KEY 10,2:B$="de Jeu":k$="NIVEAU":A=N:GOSUB 89:IF A<>N THEN I=0:T=0:N=A
88 GOTO 13
89 MODE 1:INK 2,24:INK 3,6:PEN 3:LOCATE 18,21:PRINT CHR$(242);"   ";CHR$(243):PLOT 330,120,1:GOSUB 24:PLOT 330,88:GOSUB 24:PLOT 330,56:GOSUB 24:PLOT 298,88:GOSUB 24:PLOT 362,88:GOSUB 24
90 PLOT 184,200:DRAWR 258,0:DRAWR 0,44:DRAWR -258,0:DRAWR 0,-44:LOCATE 13,9:PRINT"-";SPACE$(13);"+":FOR J=186 TO 186+16*A STEP 16:PLOT J,202,2:GOSUB 98:NEXT:J=J-16
91 LOCATE 1,1:PRINT"Reglage du Niveau ";B$:PEN 2:LOCATE 18,7:PRINT K$
92 PLOT 24,397,3:DRAWR 2,1
93 IF K$="VOLUME" THEN SOUND 1,284,-1,A-(A=0),1
94 B$=INKEY$:IF B$="" THEN 93
95 C=ASC(B$):IF C<8 OR C>9 THEN RETURN
96 IF C=8 AND A>0 THEN PLOT J,202,0:J=J-16:A=A-1:GOSUB 98 ELSE IF C=9 AND A<15 THEN A=A+1:J=J+16:PLOT J,202,2:GOSUB 98
97 GOTO 93
98 FOR Z=1 TO 4:DRAWR 0,40:DRAWR 2,0:DRAWR 0,-40:IF Z<4 THEN DRAWR 2,0
99 NEXT:RETURN
100 INK 0,1:INK 1,25:INK 2,26:PEN 2:MODE 1:BORDER 1
101 RESTORE 280
102 PRINT TAB(14);"REGLE DU JEU":PRINT:PRINT:PRINT:PRINT:PRINT
103 PEN 1:A=3:GOSUB 111:PRINT:A=2:GOSUB 111:PRINT:PRINT TAB(10);"Il faut donc ";:PEN 2:PRINT"rýcolter"
104 a=2:PEN 1:GOSUB 111:PRINT TAB(7);"afin d'aller les ";:PEN 2:PRINT"ýchanger":PEN 1:a=1:GOSUB 111
105 GOSUB 110
106 GOSUB 111:PRINT:A=3:GOSUB 111:A=2:PRINT:GOSUB 111:PRINT:A=4:GOSUB 111:GOSUB 110
107 PRINT:A=3:GOSUB 111:PRINT:a=7:GOSUB 111:GOSUB 110
108 A=7:GOSUB 111:PRINT:A=4:GOSUB 111:GOSUB 110
109 GOTO 13
110 LOCATE 24,25:PRINT"Tapez une touche";:WHILE INKEY$="":WEND:MODE 1:PRINT:PRINT:PRINT:PRINT:RETURN
111 FOR J=1 TO A:READ A$:PRINT TAB((41-LEN(A$))/2);A$:NEXT:RETURN
112 FOR J=1 TO 5:R(J)=0:Q(J)=1:NEXT:G=0:P=0:P1=0:FOR J=1 TO 16:H(J)=-5*(H(J)=5):NEXT:X=29:Y=12
113 IF B<3 THEN BM=1 ELSE IF B<5 THEN BM=2 ELSE BM=3
114 V=50-2*N-5*(B=16)
115 BX=B^(6-BM)
116 FOR JP=1 TO 14:IF H(JP)<>5 THEN H(JP)=INT(RND*2)+BM
117 NEXT
118 SE=0:GOSUB 155:T=1:INK 11,U(P):A=FRE("")
119 FOR JP=1 TO 4:GOSUB 156:NEXT
120 FOR JP=1 TO 14:IF H(JP)<>5 AND H(JP) THEN GOSUB 161
121 NEXT
122 A=REMAIN(0)+REMAIN(1):EVERY 30,0 GOSUB 158:EVERY V,1 GOSUB 163
123 I=0:SPEED KEY 5,2:EI
124 GOSUB 164
125 IF FIN THEN 172 ELSE EI:K$=INKEY$
126 DI:IF K$<>"" THEN 129
127 IF AT THEN AT=0:GOSUB 152
128 GOTO 125
129 IF K$=" " THEN A=REMAIN(0)+REMAIN(1):I=-1:GOTO 13 ELSE KE=ASC(K$):IF KE=MK THEN 135
130 IF AT THEN AT=0:MK=5:GOSUB 152
131 IF KE=88 THEN 139
132 IF KE<8 OR KE>11 THEN 127 ELSE DI
133 MK=KE:SE=11-KE:Y1=2*((KE=10)-(KE=11)):X1=2*((KE=8)-(KE=9))
134 GOSUB 152:GOTO 125
135 XTH=X+X1:YTH=Y+Y1:IF XTH<72 THEN H=INT((XTH-7)/10)ELSE H=INT((XTH+6)/10)
136 IF YTH>M(H) OR YTH<12 THEN 125
137 AT=(AT+1) MOD 4
138 X=XTH:Y=YTH:GOSUB 152:GOTO 125
139 IF AT THEN AT=0:GOSUB 152
140 IF Y=12 AND X<48 THEN 148 ELSE JP=INT(X/10):PPX=3+(10*JP-4*(JP<7)):IF Y<>M(JP) OR ABS(X-PPX)>2 THEN 151
141 IF P<>H(JP) OR P1=0 THEN 151
142 IF PPX=X THEN 145
143 SE=2-(X>PPX):GOSUB 152
144 X=PPX:GOSUB 152
145 GOSUB 146:K$=INKEY$:GOTO 125
146 SE=0:GOSUB 152:POKE PA,152:POKE PA+1,104:POKE PX,X-4:POKE PY,Y:CALL PL
147 H(JP)=0:GOSUB 161:R(P)=R(P)+1:FIN=(R(BM)+B*(1+R(BM+1))>=BX):GOSUB 152:MK=5:RETURN
148 JP=4-INT((X-12)/9):IF Q(JP)+P1>1 THEN 151 ELSE GOSUB 150
149 MK=5:GOSUB 256:GOSUB 255:GOTO 125
150 SE=1:GOSUB 152:POKE PA,152:POKE PA+1,168:GOSUB 153:Q(JP)=P1:GOSUB 156:P1=1-P1:P=JP*P1:INK 11,U(P):FOR J=1 TO 50:NEXT:GOSUB 152:RETURN
151 SOUND 1,30,10,S,1:GOSUB 157:GOTO 125
152 POKE PA,L(SE,AT,P1,1):POKE PA+1,L(SE,AT,P1,0):SOUND 1,MU(AT)+10*SE,5,S,1
153 POKE PX,X+D(SE):POKE PY,Y:IF SE>2 THEN CALL &A8AC ELSE CALL PL
154 RETURN
155 GOSUB 157:RESTORE 272:FOR J=0 TO 15:READ A:INK J,A:NEXT:MODE 0:U(0)=12:POKE PA,248:POKE PA+1,150:CALL PE:AT=0:SE=1:MK=5:GOSUB 152:RETURN
156 POKE PY,0:POKE PX,49-9*JP:POKE PA+1,132:POKE PA,F(JP*Q(JP)):CALL PL:RETURN
157 IF SQ(1)<>4 OR SQ(2)<>4 THEN 157 ELSE RETURN
158 DI
159 JP=INT(RND*14)+1:IF H(JP) THEN 160 ELSE H(JP)=INT(RND*2)+BM:GOSUB 161
160 RETURN
161 IF H(JP)=5 THEN RETURN ELSE POKE PX,10*JP-4*(JP<7):POKE PY,M(JP)+49:POKE PA,E(H(JP)):POKE PA+1,132:CALL PL
162 RETURN
163 DI:G=G+1:IF G>150 THEN FIN=1:RETURN
164 POKE PA+1,133:POKE PA,130:POKE PX,G:POKE PY,181:CALL PL
165 RETURN
166 AT=0:GOSUB 152:GOSUB 157:IF Y=YA THEN 169 ELSE SE=-(YA<Y):A=Y:AT=0
167 FOR Y=A TO YA STEP 2*SGN(YA-A)
168 GOSUB 152:GOSUB 157:AT=(AT+1)MOD 4:NEXT:Y=YA:AT=0:GOSUB 152:GOSUB 157
169 IF X=XA THEN RETURN ELSE AT=0:SE=3+(XA>X):A=X
170 FOR X=A TO XA STEP 2*SGN(XA-A)
171 GOSUB 152:GOSUB 157:AT=(AT+1)MOD 4:NEXT:X=XA:AT=0:GOSUB 152:RETURN
172 A=REMAIN(0)+REMAIN(1):FOR J=500 TO 900 STEP 5:SOUND 1,J,2,S,1:NEXT
173 YA=12:XA=29:GOSUB 166:SE=1:GOSUB 152:PEN 3:LOCATE 6,15:GOSUB 256:PRINT"Fin de la":LOCATE 5,17:PRINT"Cueillette";:SOUND 1,80,40,S,1
174 FOR J=1 TO 1000:NEXT:FOR J=40 TO 180 STEP 4:PLOT J,0,0:SOUND 1,J,5,S,1:DRAWR 0,120:NEXT:GOSUB 157
175 TAXE=0:PAD=100:FIN=0:CT=-1
176 T=2:GOSUB 243
177 A=FRE(""):GOSUB 244
178 U(0)=10:P=0:SPEED KEY 5,2:I=0:MK=5:B$=" Bonjour PEPPO !":GOSUB 264:GOSUB 265:GOSUB 224:IF FIN THEN 217
179 B$="Tu peux commencer,":GOSUB 264:PAPER 4:PEN BM+6:PRINT "ÿ=";:PEN BM+5:PRINT STRING$(B,P$);
180 PEN 6:PAPER 9:WHILE INKEY$<>"":WEND
181 IF FIN THEN 217 ELSE K$=INKEY$:IF K$=" " THEN 241 ELSE IF K$="" THEN 181 ELSE GOSUB 265:GOTO 186
182 IF FIN THEN 217
183 K$=INKEY$:IF K$<>"" THEN 186 ELSE IF K$=" " THEN 241
184 IF AT THEN AT=0:GOSUB 152
185 GOTO 182
186 KE=ASC(K$):IF KE=MK THEN 195
187 IF AT THEN AT=0:MK=5:GOSUB 152
188 IF PAD=B AND TAXE=0 THEN GOSUB 224
189 IF KE=8 THEN X1=-2:GOTO 192 ELSE IF KE=9 THEN X1=2:GOTO 192
190 IF KE=13 AND TAXE=0 AND PAD<100 AND X=94 THEN 205
191 IF KE=88 THEN 197 ELSE 184
192 IF X=94 THEN IF(PAD<>B AND TAXE=0) THEN 215 ELSE IF KE=8 THEN X=93:SE=1:GOSUB 152:X=92:SE=3:MK=8:GOTO 194
193 MK=KE:SE=1:GOSUB 152:SE=11-KE
194 GOSUB 152:GOTO 182
195 XTH=X+X1:IF XTH<28 OR XTH>92 THEN 182
196 AT=(AT+1)MOD 4:X=XTH:FOR J=1 TO 20:NEXT:GOSUB 152:GOTO 182
197 IF AT THEN AT=0:GOSUB 152
198 IF X<64 THEN 207 ELSE IF X*P1<92 THEN 215
199 IF PAD=0 AND TAXE=0 THEN GOSUB 259:B$=ERP$(INT(RND*3)):R(P)=R(P)+B+1:GOTO 213
200 IF R(P)=0 THEN B$="CE PANIER EST VIDE":GOTO 214
201 IF TAXE=0 AND R(P)<PAD THEN B$="Tu n'as plus assez     de pommes":GOTO 214
202 IF X<94 THEN SE=2:MK=5:GOSUB 152:X=94:GOSUB 152
203 GOSUB 259:PAD=PAD-1:PEN 5+P:PAPER 4:PRINT CHR$(255);:PAPER 9
204 IF TAXE THEN 225 ELSE GOSUB 255:GOTO 182
205 IF PAD=0 THEN PAD=B:PEN 6:INK 2,26:CLS:PRINT"  J'ai le compte":R(PT)=R(PT)+1:JP=PT:GOSUB 249:GOSUB 245:GOSUB 246:JP=P:GOSUB 224:GOSUB 265:IF PT=5 THEN 216 ELSE GOSUB 255:GOTO 180
206 B$=ERM$(INT(RND*3)):R(P)=B-PAD+R(P):GOTO 213
207 JP=5-INT((X-26)/8):IF P1+Q(JP)>1 OR JP>4+TAXE THEN 215
208 IF TAXE THEN IF JP<>CT AND JP <>CT+1 THEN B$="Inutile de prendre    ce PANIER.":GOTO 214
209 MK=5:SE=1:GOSUB 152:POKE PA,152:POKE PA+1,168:GOSUB 153:Q(JP)=P1:GOSUB 248:P1=1-P1:P=JP*P1:INK 11,U(JP*P1):FOR J=1 TO 100:NEXT:GOSUB 152
210 PAD=B:PT=JP+1-2*TAXE:INK 1,U(PT):IF P AND TAXE<>1 THEN INK 2,26:CLS:PRINT "Presse";:PEN 8:PRINT" ENTER ";:PEN 6:PRINT"þ la";" fin d'un ýchange.";:GOSUB 255:GOSUB 257 ELSE 212
211 CLS:PAPER 4:PEN 1+10*TAXE::PRINT"ÿ=";:PEN 11-10*TAXE:PRINT STRING$(B,P$);:GOSUB 256
212 GOSUB 256:GOSUB 255:GOTO 180
213 CLS:PEN 6:INK 2,26:PRINT B$;:FOR J=140 TO 232 STEP 2:PLOT 350,J,0:DRAWR 46,0:SOUND 1,3*J+400,3,S,1:NEXT:SE=1:MK=5:X=70-8*P:GOSUB 152:PEN 1:CLS:PAPER 4:PRINT "ÿ=";:PEN 11:PRINT STRING$(B,P$);:JP=P:GOSUB 246:PAD=B:GOSUB 255:GOTO 180
214 GOSUB 264
215 SOUND 1,30,10,S,1:GOSUB 157:GOSUB 255:GOTO 181
216 CLS:PRINT ,"   et c'est une"+SPACE$(6)+"POMME";:PEN 3:PRINT" ROUGE":GOSUB 257:IF FIN THEN 217 ELSE B$="Mais tu dois finir   tes ECHANGES":GOSUB 264:GOTO 180
217 FIN=0:IF TAXE THEN RETURN ELSE B$="  Tu as fini tes"+SPACE$(7)+"ECHANGES.":GOSUB 264:IF P1=0 THEN 219
218 XA=70-8*P:YA=Y:GOSUB 166:JP=P:MK=5:SE=1:GOSUB 152:POKE PA,152:POKE PA+1,168:GOSUB 153:Q(JP)=1:GOSUB 248:P1=0:P=0:INK 11,U(0):FOR J=1 TO 100:NEXT:GOSUB 152
219 IF N<6 THEN 229
220 FOR J=4 TO 2 STEP-1:IF R(J) THEN TAXE=1:TAP=B+INT(RND*R(J-1)):CT=J-1:J=2
221 NEXT
222 IF TAXE=0 THEN B$="  Tu ne peux pas    payer de TAXE":GOSUB 264:GOTO 229
223 B$="Mais tu dois payer     la TAXE.":GOSUB 264:CLS:PRINT "Tu me dois ";:PEN CT+5:PRINT STRING$(TAP,CHR$(255));:GOSUB 257:GOTO 180
224 IF TAXE THEN RETURN ELSE FOR J=1 TO 4 :FIN=FIN+(R(J)>=B):NEXT:FIN=SGN(FIN)+1:RETURN
225 IF P=CT AND R(P)+1>=TAP THEN B$="Merci de me payer      la TAXE":GOSUB 264:FOR J=1 TO TAP-1:GOSUB 259:NEXT:GOTO 229
226 IF P=CT THEN B$=ERM$(INT(RND*3)):R(CT)=R(CT)+1:GOSUB 246:GOSUB 264:GOSUB 265:PRINT "Tu me dois ";:PEN CT+5:PRINT STRING$(TAP,CHR$(255));:GOSUB 255:GOTO 180
227 IF R(CT)<TAP THEN PRINT"=":FOR J=1 TO B:GOSUB 249:JP=PT:GOSUB 245:R(PT)=R(PT)+1:GOSUB 246:NEXT:GOTO 180
228 B$=ERP$(INT(RND*3)):R(P)=R(P)+1:JP=P:GOSUB 246:GOSUB 264:GOSUB 265:SOUND 1,30,10,S,1:PRINT "Tu me dois ";:PEN CT+5:PRINT STRING$(TAP,CHR$(255));:GOSUB 257:GOTO 180
229 CLS:INK 2,10:IF P1 THEN XA=70-8*P:YA=Y:GOSUB 166:JP=P:MK=5:SE=1:GOSUB 152:POKE PA,152:POKE PA+1,168:GOSUB 153:Q(JP)=1:GOSUB 248:P1=0:P=0:INK 11,U(0):FOR J=1 TO 100:NEXT:GOSUB 152
230 IF R(5) THEN XA=30:GOSUB 166:SE=1:GOSUB 152:JP=5:POKE PA,152:POKE PA+1,168:GOSUB 153:Q(JP)=0:GOSUB 248:P1=1:INK 11,U(5):FOR J=1 TO 100:NEXT:GOSUB 152
231 FOR J=1 TO 1000:NEXT:WINDOW 1,20,22,25:B$="":FOR J=1 TO 5:B$=HEX$(R(J))+B$:NEXT:PEN 3:PAPER 9:CLS:PRINT TAB(4);B$;:PEN 6:PRINT " est le","  Rýsultat de ta",TAB(5);"Cueillette.";:GOSUB 261:GOSUB 157:FOR J=1 TO 1000:NEXT
232 MODE 0:PEN 1:RESTORE 274:FOR J=0 TO 15:READ A:INK J,A:NEXT:WINDOW 1,20,18,25:PAPER 0:CLS:FOR J=1 TO 1000:NEXT:IF R(5)=0 THEN INK 11,2
233 FOR J=40 TO 180 STEP 4:SOUND 1,J,5,S,1:NEXT:GOSUB 152:FOR J=20 TO 50:SOUND 1,J,5,S,1:NEXT
234 POKE PA,198:POKE PA+1,106:POKE PX,100:POKE PY,70:SOUND 1,200,20,S,1:CALL PL:IF R(5) THEN 236 ELSE INK 10,16,6:SPEED INK 5,5:PRINT," Quel dommage !!!!!",," Tu  n'as  pas  de",,,"    Pomme";:PEN 3:PRINT" ROUGE.":GOSUB 257:GOSUB 257
235 INK 10,16:GOTO 237
236 PRINT,"   C'est SUPER !!!",,"     J'adore les",,"    POMMES ROUGES.":SE=2:GOSUB 152:GOSUB 257:GOSUB 257:IF N<15 THEN N=N+1
237 PEN 1:CLS:POKE PX,86:POKE PY,131:POKE PA,91:POKE PA+1,105:CALL PL:IF R(5) THEN B$=" Mais" ELSE B$="  Et"
238 PRINT B$+"  J'ai  encore",,"    plus  faim,",,"   veux-tu  bien",,"  recommencer ?!?"
239 GOSUB 257:GOSUB 257:I=0:TAXE=0:T=0:IF INKEY$<>"" THEN 13
240 WHILE INKEY$="":WEND:GOTO 112
241 I=-1:IF X=94 AND TAXE=0 THEN R(P)=R(P)+B-PAD
242 GOTO 13
243 MODE 0:PEN 3:PAPER 0:LOCATE 3,12:PRINT"Chez le marchand";:RESTORE 273:FOR J=0 TO 15:READ A:INK J,A:NEXT:GOSUB 257:GOSUB 157:POKE PA+1,130:POKE PA,61:CALL PE:PAPER 15:PEN 10:LOCATE 6,7:PRINT RIGHT$(STR$(B+100),2):RETURN
244 PAPER 9:PEN 6:FOR JP=1 TO 5:SOUND 1,JP*50,5,S,1:Q(JP)=1:GOSUB 245:GOSUB 246:NEXT:FOR J=180 TO 40 STEP -4:SOUND 1,J,5,S,1:NEXT:X=44:Y=70:AT=0:SE=1:P1=0:GOSUB 152:FOR J=1 TO 1000:NEXT:WINDOW 2,19,23,24:RETURN
245 POKE PY,59:POKE PX,66-8*JP:POKE PA+1,132+(JP=5):POKE PA,F(JP*Q(JP)):CALL PL:RETURN
246 POKE PY,33:POKE PX,66-8*JP:POKE PA+1,131:POKE PA,198:CALL PL
247 FOR A=0 TO 20-R(JP):PLOT 264-32*JP,110-2*A,6:DRAWR 20,0:NEXT:RETURN
248 POKE PY,59:POKE PX,66-8*JP:POKE PA+1,132+((JP=5)AND-P1):POKE PA,F(JP*Q(JP)):CALL PL:RETURN
249 A=4.5:FOR H=113 TO 63 STEP -1:POKE PX,68-8*PT:POKE PY,H:POKE PA,67:POKE PA+1,131:CALL PL:SOUND 129,180-H,A,S,1:A=A-0.1:NEXT:RETURN
250 LOAD"!
251 CLEAR:CALL &BB48:MEMORY 25000
252 FOR J=0 TO 311:POKE 26465+J,PEEK(42645+J):NEXT
253 SYMBOL 255,0,8,16,24,60,60,60,24:SYMBOL 254,96,16,120,12,120,204,118:SYMBOL 253,6,8,60,102,126,96,60:SYMBOL 252,96,16,60,102,126,96,60
254 DEFINT B-N,P-V,X-Z:DIM E(4),R(5),MR(5),U(5),L(3,3,1,1),M(15),F(5),H(16),MH(16),Q(5),MQ(5),MU(3),ERP$(2),ERM$(2):GOTO 4
255 WHILE INKEY$<>"":WEND:RETURN
256 FOR JA=1 TO 400:NEXT:RETURN
257 FOR JA=1 TO 1000:NEXT:FOR JA=1 TO 1400-90*N:IF INKEY$<>"" THEN JA=1400:INTER=1:WHILE INKEY$<>"":WEND
258 NEXT:RETURN
259 SOUND 1,230,5,S,1:SOUND 1,210,5,S,1:POKE PY,96:POKE PX,96:POKE PA+1,131:POKE PA,39:CALL PL:GOSUB 256:POKE PY,95:POKE PX,105:POKE PA+1,130:POKE PA,246:CALL PL:GOSUB 256:POKE PY,96:POKE PX,96:POKE PA+1,131:POKE PA,53:CALL PL:GOSUB 256
260 POKE PY,95:POKE PX,102:POKE PA+1,130:POKE PA,87:CALL PL:JP=P:GOSUB 256:POKE PY,95:POKE PX,105:POKE PA+1,130:POKE PA,156:CALL PL:R(P)=R(P)-1:GOSUB 247:RETURN
261 RESTORE 277
262 READ NOTE,TEM:IF TEM THEN TEM=TEM/2:SOUND 1,NOTE,TEM-1,S,1:SOUND 2,NOTE/2,TEM,S,1:SOUND 1,0,1,1:GOTO 262
263 RETURN
264 CLS:PEN 6:INK 2,26:PRINT B$;:A=FRE(""):GOSUB 257:RETURN
265 CLS:INK 2,10:RETURN
266 DATA 0,36,36,46,78,92,102,102,96,92,78,40,24,20,12,0
267 DATA 12,26,25,14,1,6
268 DATA 230,126,226,122,201,86,176,50,151,14,234
269 DATA 500,500,800,800
270 DATA 39220,38949,41121,0,42346,0,42045,0,42644,0,41411,41726,39794,39528,40598,40833,40370,40090,39794,39528,40598,40833,40370,40090
271 DATA -6,-7,-7,7
272 DATA 12,20,13,6,26,0,26,25,14,1,16,12,18,19,9,3
273 DATA 10,26,10,6,27,0,26,25,14,1,16,10,11,12,9,3
274 DATA 2,24,26,6,23,0,1,24,1,1,16,6,1,18,15,3
275 DATA 159,80,190,40,239,40,159,80,190,40,239,40,253,40,239,40,213,40,319,40,239,40,213,40,190,40,319,40
276 DATA 159,80,190,40,239,40,159,80,190,40,239,40,253,40,239,40,213,40,319,40,239,80,0,80,0,0
277 DATA 478,40,478,40,319,80,478,40,319,40,478,40,319,40,284,40,358,80,0,80,478,20,0,20,478,20,478,20,319,80,478,40,319,40,478,40,319,40,284,40,268,80,0,160
278 DATA 239,40,239,40,159,80,239,40,159,40,239,40,159,40,142,40,179,80,0,80,239,20,0,20,239,20,239,20,159,80,239,40,159,40,239,40,159,40,142,40,134,80,0,0
279 DATA 80,65,83,67,65,76,32,74,65,82,82,89,32,164
280 DATA "Le but du jeu est d'aider Peppo","þ nourrir Numýrus","en lui procurant une pomme rouge.","Malheureusement, les pommes rouges","ne poussent pas sur Planüte Base.","le plus possible de pommes","blanches, jaunes, bleues et marines"
281 DATA "chez le marchand.","La rýcolte s'effectue en temps limitý.","Pour cueillir une pomme,","il faut se munir du panier","de la couleur correspondante.","Attention, la valeur d'une pomme","est fonction de sa couleur."
282 DATA "les pommes blanches valent moins","que les pommes jaunes,","qui valent moins que les pommes bleues,","qui valent moins que les marines."
283 DATA "Chez le marchand,","les ýchanges se font en fonction","de la base dýterminýe dans le jeu :","Si la base est 3,","il faut 3 pommes blanches","pour obtenir une pomme jaune,","3 jaunes pour en obtenir une bleue,","3 bleues pour une marine"
284 DATA "et enfin, il faut 3 pommes marines","pour avoir la pomme rouge."
285 DATA "Pour procýder aux ýchanges,","il suffit de se prýsenter","devant le marchand","muni du panier de la couleur","des pommes þ ýchanger,","et de lui donner","le bon nombre de pommes."
286 DATA "Dans un but pýdagogique,","le joueur est obligý de terminer","ses ýchanges,","avant de pouvoir retrouver Numýrus."