10 ' *MUSIC
30 ' *S.O.S
50 ' *
60 '*****************
70 '
80 ' Bui Huu Nhan - CLAVIER ou JOYSTICK 
90 '
100 KEY 1,"mode 2:pen 1:list"+CHR$(13) 
110 MODE 0:BORDER 0:FOR x=0 TO 15:READ a:INK x,a:NEXT
120 DATA 0,26,20,11,14,0,2,1,6,24,15,13,21,9,4,8
130 IF PEEK(&A000)=0 THEN MEMORY &5FFF:LOAD"SOS2.BIN":LOAD"SOS3.BIN":CALL &A000:LOAD"SOS4.BIN",&6000
140 IF PEEK(&8600)=0 THEN POKE &8600,63: POKE &8601,46:POKE &8602,38
150 POKE &610D,8:POKE &61E8,7:POKE &6255,7:POKE &6334,7:POKE &640F,8:POKE &64EC,7: POKE &654B,7:POKE &6635,8:POKE &66AE,7:POKE &678A,8:POKE &67F9,7:POKE &6850,7: POKE &68C9,7:POKE &69C6,8:POKE &6CB3,7 
160 POKE &6D1F,7:POKE &6DFB,8:POKE &6EE5,7:POKE &6F2F,7:POKE &709A,8:POKE &716A,7:POKE &71C6,8:POKE &723C,8:POKE &73B1,7:POKE &73FF,8:POKE &7471,7:POKE &74DC,8:POKE &756B,7
170 '
180 ' Presentation
190 '
200 DEFINT a-z:ENV 1,15,-1,10
210 |SPRITE,&9160,4,191,2,8:|SPRITE,&9160,96,191,2,8:|SPRITE,&9160,4,23,2,8:|SPRITE,&9160,96,23,2,8
220 FOR x=9 TO 95 STEP 8:|SPRITE,&9140,x,191,4,8:|SPRITE,&9140,x,23,4,8:NEXT
230 FOR y=183 TO 24 STEP -16:|SPRITE,&9170,4,y,2,16:|SPRITE,&9170,96,y,2,16:NEXT
240 a$="S O S MUSIC":x=110:y=180:GOSUB 1030  
250  a$="BUI HUU NHAN":x=10:y=10:GOSUB 1030
260 a$="ENERGIE":x=110:y=140:GOSUB 1030 
270 a$="CASSETTES":x=110:y=75:GOSUB 1030 
280 a$="J JOYSTICK":x=30:y=120:GOSUB 1030
290 a$="R REDEDINIR":x=30:y=100:GOSUB 1030
300 a$="C CLAVIER":x=30:y=80:GOSUB 1030 
310 WINDOW #1,2,12,3,22
320 PEN 5:LOCATE 15,10:PRINT"99 %":|DOUBLE,15,10,5:LOCATE 15,18:PRINT"0":|DOUBLE,15,18,1:INK 5,5
330 WHILE INKEY$<>"":WEND:RESTORE 970:FOR t=1 TO 12:READ a:SOUND 1,a,15,15,1:SOUND 2,a+1,15,8,1:SOUND 4,a-1,15,8,1:NEXT:READ a:SOUND 1,a,75,15,1:SOUND 2,a+1,75,8,1:SOUND 1,a-1,75,8,1
340 '
350 ' Clavier ou joystick ?
360 '
370 IF INKEY(45)=0 THEN gau=74:dro=75:feu=76:GOTO 440
380 IF INKEY(50)=0 THEN GOSUB 1080
390 IF INKEY(62)=0 THEN gau=PEEK(&8601):dro=PEEK(&8602):feu=PEEK(&8600):GOTO 440 
400 GOTO 370
410 '
420 ' Initialisation
430 '
440 CLS #1:DIM pie(5,10),m(4):ene=99:k7=0:cheat=PEEK(&BF80)
450 adr=&6000:FOR y=1 TO 10:FOR x=1 TO 5 :pie(x,y)=adr:adr=adr+110:NEXT x,y
460 px=1:py=1:POKE &8000,0:POKE &8001,&90:POKE &8002,0:POKE &8003,0:POKE &8004,32:POKE &8005,135:POKE &8006,4:POKE &8007,16
470 CLS #1:adr=pie(px,py):EVERY 50,0 GOSUB 810
490 ' Installation du tableau
500 '
510 nm=0:adrm=&800C:adrk7=&8030:adrce=&803C:POKE &800C+9,0:POKE &8018+9,0:POKE &8024+9,0:POKE adrk7+9,0:POKE adrce+9,0:FOR y=366 TO 78 STEP -32:FOR x=32 TO 352 STEP 32
520 a=PEEK(adr):adr=adr+1:IF a=0 THEN GOTO 590
530 IF a=2 THEN |SPRITE,&90C0,INT(x/4),INT(y/2),4,16:GOTO 590 ELSE IF a=3 THEN |SPRITE,&9100,INT(x/4),INT(y/2),4,16:GOTO 590
540 IF a=4 THEN POKE adrm,&90:POKE adrm+1,&91:POKE adrm+4,INT(x/4):POKE adrm+5,INT(y/2):POKE adrm+6,4:POKE adrm+7,16:|CALCUL,adrm:|AFFI,adrm:POKE adrm+9,1:adrm=adrm+12:nm=nm+1:GOTO 590
550 IF a=5 THEN POKE adrm,&D0:POKE adrm+1,&91:POKE adrm+4,INT(x/4):POKE adrm+5,INT(y/2):POKE adrm+6,4:POKE adrm+7,16:|CALCUL,adrm:|AFFI,adrm:POKE adrm+9,1:adrm=adrm+12:nm=nm+1:GOTO 590
560 IF a=6 THEN POKE adrm,&10:POKE adrm+1,&92:POKE adrm+4,INT(x/4):POKE adrm+5,INT(y/2):POKE adrm+6,4:POKE adrm+7,16:|CALCUL,adrm:|AFFI,adrm:POKE adrm+9,1:adrm=adrm+12:nm=nm+1:GOTO 590
570 IF a=7 THEN POKE adrk7,&50:POKE adrk7+1,&92:POKE adrk7+4,INT(x/4):POKE adrk7+5,INT(y/2):POKE adrk7+6,4:POKE adrk7+7,8:|CALCUL,adrk7:|AFFI,adrk7:POKE adrk7+9,1:adm=adr-1:GOTO 590
580 IF a=8 THEN POKE adrce,&B8:POKE adrce+1,&92:POKE adrce+4,INT(x/4):POKE adrce+5,INT(y/2):POKE adrce+6,4:POKE adrce+7,8:|CALCUL,adrce:|AFFI,adrce:POKE adrce+9,1:adm=adr-1:GOTO 590
590 NEXT x,y:|CALCUL,&8000:|AFFI,&8000
600 '
610 ' Routine principale
620 '
630 IF INKEY(gau)=0 AND INKEY(feu)=0 THEN POKE &8000,&80:|MOVE,5,&8000:GOTO 690 
640 IF INKEY(dro)=0 AND INKEY(feu)=0 THEN POKE &8000,&40:|MOVE,6,&8000:GOTO 690 
650 IF INKEY(feu)=0 THEN POKE &8000,0:|MOVE,2,&8000:GOTO 690
660 IF INKEY(gau)=0 THEN POKE &8000,&80:|MOVE,8,&8000:GOTO 690
670 IF INKEY(dro)=0 THEN POKE &8000,&40:|MOVE,7,&8000:GOTO 690
680 |CLEAR,&8000:POKE &8000,0:|MOVE,4,&8000
690 IF PEEK(&8004)=88 THEN POKE &8004,12:px=px+1:GOTO 470 ELSE IF PEEK(&8004)=8 THEN POKE &8004,84:px=px-1:GOTO 470 ELSE IF PEEK(&8005)=39 THEN POKE &8005,175:py=py+1:GOTO 470 ELSE IF PEEK(&8005)=183 THEN POKE &8005,47:py=py-1:GOTO 470
700 adr=&800C:FOR t=1 TO nm:|MOVE,m(t),adr:adr=adr+12:NEXT t
710 |COLLI,&8000,&800C:IF PEEK(&8500)=1 THEN LOCATE 14,10:ene=ene-cheat:INK 5,0:PRINT ene:|DOUBLE,15,10,3:INK 5,5:GOTO 740 
720 |COLLI,&8000,&8018:IF PEEK(&8500)=1 THEN LOCATE 14,10:ene=ene-cheat:INK 5,0:PRINT ene:|DOUBLE,15,10,3:INK 5,5:GOTO 740 
730 |COLLI,&8000,&8024:IF PEEK(&8500)=1 THEN LOCATE 14,10:ene=ene-cheat:INK 5,0:PRINT ene:|DOUBLE,15,10,3:INK 5,5
740 |COLLI,&8000,adrk7:IF PEEK(&8500)=1 THEN |CLEAR,adrk7:LOCATE 14,18:k7=k7+1:INK 5,0:PRINT k7:|DOUBLE,15,18,2:INK 5,5:POKE adrk7+9,0:POKE adm,0:SOUND 132,100,50,15,1:SOUND 130,150,50,15,1:WHILE SQ(2)<>4:WEND:IF k7=17 THEN GOTO 910
750 |COLLI,&8000,adrce:IF PEEK(&8500)=1 THEN |CLEAR,adrce:ene=ene+cheat:LOCATE 14,10:INK 5,0:PRINT ene:|DOUBLE,15,10,3:INK 5,5:POKE adrce+9,0:POKE adm,0:SOUND 132,100,50,15,1:SOUND 130,150,50,15,1:WHILE SQ(2)<>4:WEND
760 IF ene=0 THEN GOTO 850
770 GOTO 630
790 ' Direction des monstres
800 '
810 FOR t=1 TO 3:m(t)=INT(RND*8)+1:NEXT: RETURN
820 '
830 ' Perdu !!!
840 '
850 a=REMAIN(0):|CLEAR,&8000:POKE &8000,&78:POKE &8001,&92:|AFFI,&8000:OUT &BC00,8:OUT &BD00,1:SOUND 1,0,150,15,1,,31:WHILE SQ(1)<>4:WEND:OUT &BC00,8:OUT &BD00,0:FOR g=0 TO 200:NEXT g
860 CLS#1:a$="GAME OVER":x=30:y=100:GOSUB 1030
870 FOR g=0 TO 500:NEXT:WHILE INKEY$<>"":WEND:RESTORE 980:FOR t=1 TO 8:READ a:SOUND 1,a,15,15,1:SOUND 2,a+1,15,8,1:SOUND 4,a-1,15,8,1:NEXT:READ a:SOUND 1,a,75,15,1:SOUND 2,a+1,75,8,1:SOUND 4,a-1,75,8,1:CALL &BB06:RUN
880 '
890 ' Bravo...Vous avec recuperez les 17 cassettes
900 '
910 a=REMAIN(0):CLS#1:a$="FELICITATION":x=25:y=100:GOSUB 1030
920 RESTORE 990:FOR g=0 TO 500:NEXT:WHILE INKEY$<>"":WEND:READ a:SOUND 1,a,25,15,1:SOUND 2,a+1,25,8,1:SOUND 4,a-1,25,8,1:FOR t=1 TO 7:READ a:SOUND 1,a,15,15,1:SOUND 2,a+1,15,8,1:SOUND 4,a-1,15,8,1:NEXT:READ a
930 SOUND 1,a,75,15,1:SOUND 2,a+1,75,8,1:SOUND 4,a-1,75,8,1:CALL &BB06:RUN
950 ' Petites musiques
960 '
970 DATA 120,134,160,134,120,134,106,134,120,159,201,159,239
980 DATA 239,239,239,239,239,268,268,268,239
990 DATA 160,160,160,160,160,160,190,160,120
1000 '
1010 ' Affichages
1020 '
1030 FOR t=1 TO LEN(a$):b$=MID$(a$,t,1):IF b$=" " THEN b=27 ELSE b=ASC(b$):b=b-65
1040 |SPRITE,&92D8+b*16,x,y,2,8:x=x+4:NEXT:RETURN
1050 '
1060 ' Redifinition du clavier
1070 '
1080 FOR t=0 TO 300:NEXT t:CLS #1:a$="GAUCHE":x=25:y=120:GOSUB 1030
1090 FOR x=22 TO 71:IF INKEY(x)=0 THEN GOTO 1110
1100 NEXT x:GOTO 1090
1110 POKE &8601,x:a$="OK":x=60:y=120:GOSUB 1030:FOR t=0 TO 300:NEXT t
1120 a$="DROITE":x=25:y=100:GOSUB 1030 
1130 FOR x=22 TO 71:IF INKEY(x)=0 THEN GOTO 1150
1140 NEXT x:GOTO 1130
1150 POKE &8602,x:a$="OK":x=60:y=100:GOSUB 1030:FOR t=0 TO 300:NEXT t
1160 a$="FEU":x=25:y=80:GOSUB 1030
1170 FOR x=22 TO 71:IF INKEY(x)=0 THEN GOTO 1190
1180 NEXT x:GOTO 1170
1190 POKE &8600,x:a$="OK":x=60:y=80:GOSUB 1030:FOR t=0 TO 300:NEXT t:RUN