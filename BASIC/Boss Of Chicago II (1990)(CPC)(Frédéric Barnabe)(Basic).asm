100 '
110 ' init
120 '
130 MODE 1:INK 1,16:INK 2,10
140 WINDOW #2,3,38,2,17:PEN#2,0:WINDOW #3,3,15,19,24:WINDOW #4,26,38,19,24:WINDOW #5,1,40,20,25
150 GOSUB 1500
160 FOR I=1 TO 28:LOCATE 1,I+24:PRINT:NEXT
170 CLS:INK 2,26:INK 1,10
180 PLOT 30,384,1:DRAW 608,384:DRAW 608,126:DRAW 30,126:DRAW 30,384
190 PLOT 30,0:DRAW 30,112:DRAW 240,112:DRAW 240,0:DRAW 30,0
200 PLOT 398,0:DRAW 398,112:DRAW 608,112:DRAW 608,0:DRAW 398,0
210 FOR I=1 TO 24:LOCATE 1,I:PRINT"a":LOCATE 40,I:PRINT"a":NEXT
220 DIM LEVPOS(33,3)
230 LEVEL=1:GOSUB 1820:IF ARME=2 THEN 410
240 '
250 ' viseur 1
260 '
270 PLOT -2,0,2:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(3);:TAG#1
280 MOVE X1,Y1:PRINT#1,CHR$(236);
290 J1=JOY(0)
300 IF (J1 AND 16)<>0 THEN IF CHAR1>-1 THEN 600
310 IF J1=0 THEN IF ARME=0 OR ARME=2 THEN 410 ELSE 270
320 PLOT -2,0,1:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(2);:TAG#1:MOVE X1,Y1:PRINT#1,CHR$(143);
330 IF (J1 AND 1)<>0 THEN Y1=Y1+16
340 IF (J1 AND 2)<>0 THEN Y1=Y1-16
350 IF (J1 AND 4)<>0 THEN X1=X1-16
360 IF (J1 AND 8)<>0 THEN X1=X1+16
370 IF ARME=0 OR ARME=2 THEN 410 ELSE 270
380 '
390 ' viseur 2
400 '
410 PLOT -2,0,2:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(3);:TAG#1
420 MOVE X2,Y2:PRINT#1,CHR$(236);
430 GOSUB 520
440 IF (J2 AND 16)<>0 THEN IF CHAR2>-1 THEN 750
450 IF J2=0 THEN IF ARME=0 OR ARME=1 THEN 270 ELSE 410
460 PLOT -2,0,1:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(2);:TAG#1:MOVE X2,Y2:PRINT#1,CHR$(143); 
470 IF (J2 AND 1)<>0 THEN Y2=Y2+16
480 IF (J2 AND 2)<>0 THEN Y2=Y2-16
490 IF (J2 AND 4)<>0 THEN X2=X2-16
500 IF (J2 AND 8)<>0 THEN X2=X2+16
510 IF ARME=0 OR ARME=1 THEN 270 ELSE 410
520 J2=0
530 IF INKEY(0)=0 THEN J2=J2+1
540 IF INKEY(2)=0 THEN J2=J2+2
550 IF INKEY(8)=0 THEN J2=J2+4
560 IF INKEY(1)=0 THEN J2=J2+8
570 IF INKEY(47)=0 THEN J2=J2+16
580 RETURN
590 '
600 ' tir1
610 '
620 IF X1/16+1<1 OR 26-Y1/16<1 OR X1/16+1>40 OR 26-Y1/16>26 THEN 330
630 MOVE X1,Y1:PRINT#1,CHR$(237);
640 ENV 1,30,0,1,127,-1,15:SOUND 129,0,240,15,1,0,10
650 PLOT -2,0,1:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(2);:TAG#1:MOVE X1,Y1:PRINT#1,CHR$(143);
660 DI:LOCATE X1/16+1,26-Y1/16:CALL 26170:A$=CHR$(PEEK(26177)):EI
670 IF A$="d" THEN DI:LOCATE X1/16+1,26-Y1/16:PRINT"e":EI
680 IF A$>"p" AND Y1>50 THEN X=X1:Y=Y1:GOSUB 880:SCORE1=SCORE1+SCORE:DI:LOCATE 10,20:PRINT SCORE1:EI
690 IF A$="a" THEN DI:LOCATE X1/16+1,26-Y1/16:PRINT CHR$(96):EI
700 MUNI1=MUNI1-1:DI:LOCATE 4+MUNI1,22:PRINT" ":EI:IF MUNI1=0 THEN MUNI1=9:CHAR1=CHAR1-1:DI:LOCATE 4+CHAR1,24:PRINT" ":EI:IF CHAR1<>-1 THEN DI:LOCATE 4,22:PRINT STRING$(9,"p"):EI
710 GOTO 330
720 '
730 ' tir2
740 '
750 IF X2/16+1<1 OR 26-Y2/16<1 OR X2/16+1>40 OR 26-Y2/16>26 THEN 460
760 MOVE X2,Y2:PRINT#1,CHR$(237);
770 ENV 1,30,0,1,127,-1,15:SOUND 129,0,240,15,1,0,10
780 PLOT -2,0,1:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(2);:TAG#1:MOVE X2,Y2:PRINT#1,CHR$(143);
790 DI:LOCATE X2/16+1,26-Y2/16:CALL 26170:A$=CHR$(PEEK(26177)):EI
800 IF A$="d" THEN DI:LOCATE X2/16+1,26-Y2/16:PRINT"e":EI
810 IF A$>"p" AND Y2>50 THEN X=X2:Y=Y2:GOSUB 880:SCORE2=SCORE2+SCORE:DI:LOCATE 33,20:PRINT SCORE2:EI
820 IF A$="a" THEN DI:LOCATE X2/16+1,26-Y2/16:PRINT CHR$(96):EI
830 MUNI2=MUNI2-1:DI:LOCATE 37-MUNI2,22:PRINT" ":EI:IF MUNI2=0 THEN MUNI2=9:CHAR2=CHAR2-1:DI:LOCATE 37-CHAR2,24:PRINT" ":EI:IF CHAR2<>-1 THEN DI:LOCATE 29,22:PRINT STRING$(9,"p"):EI
840 GOTO 460
850 '
860 ' ici l'enneni meurt...
870 '
880 DI:BORDER 6:ENEMI=ENEMI-1:ATUER=ATUER-1:SCORE=10:IF ATUER=0 AND A$<>"q" THEN LEVEL=LEVEL+1:CLS#2:LOCATE #2,1,1:PRINT#2,REMAIN(0):GOSUB 1820:EI:SCORE=15:RETURN
890 IF A$="r" THEN LOCATE X/16+1,26-Y/16:PRINT"ff":LOCATE X/16+1,27-Y/16:PRINT"ff":BORDER 0:EI:RETURN
900 IF A$="s" THEN LOCATE X/16,26-Y/16:PRINT"ff":LOCATE X/16,27-Y/16:PRINT"ff":BORDER 0:EI:RETURN
910 IF A$="t" THEN LOCATE X/16+1,25-Y/16:PRINT"ff":LOCATE X/16+1,26-Y/16:PRINT"ff":BORDER 0:EI:RETURN
920 IF A$="u" THEN LOCATE X/16,25-Y/16:PRINT"ff":LOCATE X/16,26-Y/16:PRINT"ff":BORDER 0:EI:RETURN 
930 IF A$="v" THEN LOCATE X/16+1,26-Y/16:PRINT"  ":LOCATE X/16+1,27-Y/16:PRINT"  ":BORDER 0:EI:RETURN  
940 IF A$="w" THEN LOCATE X/16,26-Y/16:PRINT"  ":LOCATE X/16,27-Y/16:PRINT"  ":BORDER 0:EI:RETURN 
950 IF A$="x" THEN LOCATE X/16+1,25-Y/16:PRINT"  ":LOCATE X/16+1,26-Y/16:PRINT"  ":BORDER 0:EI:RETURN 
960 IF A$="y" THEN LOCATE X/16,25-Y/16:PRINT"  ":LOCATE X/16,26-Y/16:PRINT"  ":BORDER 0:EI:RETURN 
970 IF A$="z" THEN LOCATE X/16+1,26-Y/16:PRINT"f":BORDER 0:EI:SCORE=15:RETURN
980 IF A$="q" THEN 1440
990 '
1000 ' la,il revit
1010 '
1020 GOSUB 1100:CH=INT(RND*33)+1
1030 X3=LEVPOS(CH,1):Y3=LEVPOS(CH,2)
1040 IF LEVPOS(CH,3)=4 THEN LOCATE X3,Y3:PRINT"q":RETURN
1050 LOCATE X3,Y3:CALL 26170:B$=CHR$(PEEK(26177)):IF B$<>" " AND B$<>"f" THEN RETURN
1060 IF LEVPOS(CH,3)=1 THEN LOCATE X3,Y3:PRINT"z":ENEMI=ENEMI+1:RETURN
1070 LOCATE X3+1,Y3:CALL 26170:B$=CHR$(PEEK(26177)):IF B$<>" " AND B$<>"f" THEN RETURN 
1080 IF LEVPOS(CH,3)=2 THEN LOCATE X3,Y3:PRINT"vw":LOCATE X3,Y3+1:PRINT"xy":ENEMI=ENEMI+1:RETURN
1090 IF LEVPOS(CH,3)=3 THEN LOCATE X3,Y3:PRINT"rs":LOCATE X3,Y3+1:PRINT"tu":ENEMI=ENEMI+1:RETURN 
1100 '
1110 ' perte vie
1120 '
1130 IF ENEMI<5 THEN RETURN ELSE IF INT(RND*2+1)=1 THEN 1140 ELSE 1170
1140 '
1150 IF ARME=2 THEN 1180 ELSE VIE1=VIE1+5:IF VIE1/10=INT(VIE1/10) THEN LOCATE 1,25-VIE1/10:PRINT"b":IF VIE1>235 THEN 1200
1160 RETURN
1170 '
1180 IF ARME=1 THEN 1150 ELSE VIE2=VIE2+5:IF VIE2/10=INT(VIE2/10) THEN LOCATE 40,25-VIE2/10:PRINT"b":IF VIE2>235 THEN 1230
1190 RETURN
1200 CLS#3:LOCATE 5,22:PRINT"GAME OVER"
1210 PLOT -2,0,1:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(2);:TAG#1:MOVE X1,Y1:PRINT#1,CHR$(143);
1220 IF ARME>0 THEN 1260 ELSE ARME=2:RETURN
1230 CLS#4:LOCATE 28,22:PRINT"GAME OVER"
1240 PLOT -2,0,1:TAGOFF#1:PRINT#1,CHR$(23)+CHR$(2);:TAG#1:MOVE X2,Y2:PRINT#1,CHR$(143);
1250 IF ARME>0 THEN 1260 ELSE ARME=1:RETURN 
1260 FOR I=1 TO 3000:NEXT
1270 WHILE INKEY$<>"":WEND
1280 LOCATE 1,25:FOR I=1 TO 28:PRINT:NEXT
1290 INK 1,16:INK 2,10
1300 TAGOFF#1:PRINT#1,CHR$(23)+CHR$(3);:TAG#1
1310 X=50:Y=200:A$="SCORE JOUEUR 1":GOSUB 1380
1320 X=50:Y=160:A$=STR$(SCORE1):GOSUB 1380
1330 X=400:Y=200:A$="SCORE JOUEUR 2":GOSUB 1380 
1340 X=400:Y=160:A$=STR$(SCORE2):GOSUB 1380  
1350 FOR I=1 TO 2000:NEXT
1360 LOCATE 1,25:FOR I=1 TO 27:PRINT:NEXT
1370 RUN
1380 PLOT -2,0,2:MOVE X,Y:PRINT#1,A$;
1390 PLOT -2,0,3:MOVE X-4,Y+4:PRINT#1,A$;
1400 RETURN
1410 '
1420 ' encore des chargeurs
1430 '
1440 ENEMI=ENEMI+1:ATUER=ATUER+1:SCORE=50
1450 LOCATE X/16+1,26-Y/16:BORDER 0
1460 IF EFF=1 THEN PRINT"b":EI ELSE PRINT" ":EI
1470 IF X=X1 AND CHAR1<10 THEN CHAR1=CHAR1+1:DI:LOCATE 3+CHAR1,24:PRINT"q":EI
1480 IF X=X2 AND CHAR2<10 THEN CHAR2=CHAR2+1:DI:LOCATE 38-CHAR2,24:PRINT"q":EI 
1490 RETURN 
1500 '
1510 ' presentation
1520 '
1530 RESTORE 1710:CLS
1540 PRINT #1,CHR$(23)+CHR$(3);:TAG#1
1550 WHILE OK=0
1560 GOSUB 1670:IF A$="1" OR A$="0" OR A$="2" THEN OK=1:GOTO 1650
1570 PRINT#1,"(0) 2 JOUEUR KEY/JOY";:GOSUB 1700:PRINT#1,"(0) 2 JOUEUR KEY/JOY";
1580 GOSUB 1670:IF A$="1" OR A$="0" OR A$="2" THEN OK=1:GOTO 1650 
1590 PRINT#1,"(1) 1 JOUEUR JOYSTICK";:GOSUB 1700:PRINT#1,"(1) 1 JOUEUR JOYSTICK";
1600 GOSUB 1670:IF A$="1" OR A$="0" OR A$="2" THEN OK=1:GOTO 1650 
1610 PRINT#1,"(2) 1 JOUEUR CLAVIER";:GOSUB 1700:PRINT#1,"(2) 1 JOUEUR CLAVIER";
1620 GOSUB 1670:IF A$="1" OR A$="0" OR A$="2" THEN OK=1:GOTO 1650 
1630 MOVE 200,50:PRINT#1,"F.BARNABE & CPC";
1640 GOSUB 1700:MOVE 196,54:PRINT#1,"F.BARNABE & CPC";
1650 WEND:ARME=VAL(A$)
1660 RETURN
1670 GOSUB 1710:CLS#5
1680 A$=INKEY$
1690 PLOT -2,0,2:MOVE 150,50:RETURN
1700 PLOT -2,0,3:MOVE 146,54:RETURN
1710 '
1720 ' musique
1730 '
1740 ENT -1,1,1,5,2,-1,5,1,1,5:ENV 1,5,3,2,9,-1,20,5,-1,2
1750 FOR I=1 TO 4:READ S,D:IF S=1 THEN RESTORE 1710:READ S,D
1760 IF S=0 THEN SOUND 1,0,D*50,4:SOUND 2,0,D*50,4 ELSE SOUND 1,S,D*50,14,1,1:SOUND 2,S/2,D*50,13,0,1
1770 NEXT:RETURN
1780 DATA 379,1,284,2,379,1,284,1,338,2,379,1,426,1,379,3,0,0.1,379,1,284,2,379,1,284,1,338,2,379,1,426,1,379,3
1790 DATA 284,1,213,2,284,1,213,1,253,2,284,1,319,1,284,3,379,1,284,2,379,1,284,1,338,2,379,1,426,1,379,3
1800 DATA 0,1,358,1,284,1,239,1,253,2,239,1,213,1,190,0.5,213,1,239,0.5,284,2,0,1,358,1,284,1,239,1,253,2,239,1,213,1,319,3,0,1
1810 DATA 358,1,284,1,239,1,253,2,239,1,213,1,190,0.5,213,1,239,0.5,284,2,0,1,1,0
1820 '
1830 ' changement tab
1840 '
1850 BORDER 0
1860 IF (LEVEL-1)/5=INT((LEVEL-1)/5) THEN RESTORE 1860
1870 CLS#2:LOCATE 18,9:PRINT"LEVEL"LEVEL
1880 FOR I=1 TO 1000:NEXT
1890 CLS#2:CLS#3:CLS#4:FOR I=1 TO 16
1900 READ A$:LOCATE 3,I+1:PRINT A$:NEXT
1910 X1=256:Y1=254:X2=352:Y2=254:ENEMI=0:CHAR1=4:MUNI1=9:CHAR2=4:MUNI2=9:IF ARME=0 THEN ATUER=80 ELSE ATUER=50
1920 FOR I=1 TO 33:READ LEVPOS(I,1),LEVPOS(I,2),LEVPOS(I,3):NEXT:READ EFF
1930 IF ARME<>2 THEN LOCATE 4,22:PRINT STRING$(9,"p"):LOCATE 4,24:PRINT"qqqq":LOCATE 4,20:PRINT"SCORE:"SCORE1
1940 IF ARME<>1 THEN LOCATE 29,22:PRINT STRING$(9,"p"):LOCATE 34,24:PRINT"qqqq":LOCATE 27,20:PRINT"SCORE:"SCORE2
1950 IF ARME=2 THEN LOCATE 5,22:PRINT"GAME OVER"
1960 IF ARME=1 THEN LOCATE 28,22:PRINT"GAME OVER"
1970 EVERY 52-LEVEL*2,0 GOSUB 1020
1980 RETURN
1990 '
2000 ' data tabl
2010 '
2020 DATA"bbbbbbb             bfbfb         bb"
2030 DATA"bbbbbbb             bbbbb         bf"
2040 DATA"iiiiiii             bfbfb         bb"
2050 DATA"ddddddf             bbbbb         bf"
2060 DATA"ffdffdd         bbbbbfbfb     aaaaaa"
2070 DATA"ffdffdf         bfbfbbbbb    ccaaaaa"
2080 DATA"iidiidd     aaaaaaaaafbfb    cc   bb"
2090 DATA"ddddddfcddddk       occddddddccaaana"
2100 DATA"iiiiiiicddddk  bbb  occddddddccaaana"
2110 DATA"ddddddfcddddahaaaaahaccddddddccaaana"
2120 DATA"ffdffddcdddaaaajjjaaaacddddddccaaana"
2130 DATA"ffdffdfcddd  aajjjaa  ccdddddccaaana"
2140 DATA"iidiiddcddd  aajjjaa  ccdddddccaaana"
2150 DATA"ddddddfcddd iiiiiiiii ccdddddccaaana"
2160 DATA"dddddddbbbb  bbbbbbb  bbbbbbbbbbbbbb"
2170 DATA"ddddddfbbbbbbbbbbbbbbbbbbbbbbbbbbbbb"
2180 DATA 3,6,3,6,6,3,10,7,2,11,7,2,12,7,2,13,7,2,15,6,2,16,6,2,17,6,2,16,9,2,21,9,2,28,7,2,29,7,2,30,7,2
2190 DATA 20,7,1,22,7,1,24,8,1,24,6,1,24,4,1,24,2,1,26,2,1,26,4,1,26,6,1,26,8,1,38,3,1,38,5,1,3,6,3,6,6,3,3,12,3,6,12,3,11,16,4,28,17,4,34,16,4,1
2200 DATA""
2210 DATA"aa             iibiib"
2220 DATA"  c            ggjggj     iib"
2230 DATA"ancddddddddddddiibiibdddddggjddddddd"
2240 DATA"ancddddddddddiibiibiibddddiibddddddd"
2250 DATA"ancddddddddddggjggjggjdddiibiibddddd"
2260 DATA"ancddddddddddiibiibiibdddggjggjddddd"
2270 DATA"             iiiib       iibiib"
2280 DATA"   iiiib     nnnnj            iiiib"
2290 DATA"   nnnnj     nnnnj   iib      nnnnj"
2300 DATA"   nnnnj     iiiib   ggj      nnnnj" 
2310 DATA"   iiiib  iiiibiiiib iib      iiiib"
2320 DATA"iiiibiiiibnnnnjnnnnj    iiiibiiiibii"
2330 DATA"nnnnjnnnnjnnnnjnnnnj    nnnnjnnnnjnn"
2340 DATA"nnnnjnnnnjiiiibiiiib    nnnnjnnnnjnn"
2350 DATA"iiiibiiiib              iiiibiiiibii"
2360 DATA 6,3,2,8,3,2,10,3,2,12,3,2,14,3,2,16,3,2,24,3,2,26,3,2,27,3,2,29,2,2,30,2,2,32,3,2,34,3,2,36,3,2
2370 DATA 3,12,2,4,12,2,11,12,2,14,11,2,15,11,2,21,11,2,24,9,2,25,9,2,27,12,2,28,12,2,29,12,2,30,12,2,31,12,2,21,11,2,11,12,2,4,12,2,24,15,4,12,9,4,27,9,4,0
2380 DATA"                  aaaaaaaaaaaaaaaaaa"
2390 DATA"                  aaiiiiiiiiiaaaaaaa"
2400 DATA"                  aaiiiiiiiiiaaaaaaa"
2410 DATA"     ml           aaiiiiiiiiiaaaaaaa"
2420 DATA"dddddmlddddddddcccaa  aaaa   affaffa"
2430 DATA"cdddmlmldddcddiibcaa  k  o   affaffa" 
2440 DATA"ddddmlmlddddddggjcaa aajjaa  aiiaiim"
2450 DATA"dddmlmlmldddcciibdaa bajjabmlaaaaaam"
2460 DATA"dddmlmlmlddddiibiiba biiiibmlaaaaaml"
2470 DATA"             ggjggj               ml"
2480 DATA" iiiib       iibiib
2490 DATA" nnnnj                iiiibiiiib"
2500 DATA" nnnnj                nnnnjnnnnj"
2510 DATA" iiiib                nnnnjnnnnj" 
2520 DATA"iiiibiiiib            iiiibiiiib"
2530 DATA"nnnnjnnnnj          iiiibiiiibiiiib"
2540 DATA 3,4,2,5,4,2,8,3,2,10,4,2,12,4,2,14,4,2,14,4,2,16,4,2,9,14,2,11,14,2,23,15,2,25,11,2,27,11,2,29,11,2,31,11,2,33,11,2,35,15,2,30,7,2
2550 DATA 36,6,3,33,6,3,8,3,2,10,4,2,12,4,2,14,4,2,16,4,2,9,14,2,11,14,2,23,15,2,25,11,2,30,7,2,14,11,4,33,11,4,17,17,4,0
2560 DATA"     mdddddddddddmdddccccccddddddddd"
2570 DATA"     mddccccdddddmdddddddddddddddddc"
2580 DATA"bbbb iiiiiiiiiiiiiiiiiiiiiiiiiiiiiii"
2590 DATA"fbfb m           m"
2600 DATA"bbbb mdddcccccdddmddddddddddddddcccc"
2610 DATA"fbfb mcddddccddddmddddffdffdffddddcc"
2620 DATA"bbbb mcdffdffdddcmccddffcffdffdddddd"
2630 DATA"fbfb mccffdffdcccmcdddiiciiciidddddd"
2640 DATA"bbbbbmcdiidiiccccmccddcccdcccdddddcc"
2650 DATA"fbfbfmddddddddcccmcdddffcffdffdffddc"
2660 DATA"bbbbbmcdffdffdddcmddddffdffdffdffddd"
2670 DATA"fbfbfmddffdffddddmccddiidiidiidiiddd"
2680 DATA"bbbbbmddiidiiddddmlddddddddddddddddd"
2690 DATA"fbfbfmdddddddddddmldddffdffdffdffddd"
2700 DATA"mlbbbmddffcffdffmlmldcffcffdffdffdml"
2710 DATA"mlfbfmddffcffcffmlmlcciiciidiidiidml"
2720 DATA 3,5,1,3,7,1,3,11,1,3,13,1,3,15,1,5,7,1,5,9,1,5,13,1,5,15,1,7,17,1,7,15,1,7,13,1,7,11,1,4,15,4,6,17,4
2730 DATA 11,8,3,14,8,3,11,12,3,14,12,3,11,16,3,14,16,3,17,16,3,25,7,3,25,11,3,25,15,3,28,15,3,28,11,3,28,7,3,31,7,3,31,11,3,31,15,3,34,15,3,34,11,3,1
2740 DATA""
2750 DATA"   k        fffffbbbb          o"
2760 DATA"   k        fffffbbbb          o" 
2770 DATA"   k        fffffbbbb          aa"
2780 DATA"  ak    aaaafffffbbbb         aaa"
2790 DATA"  ak    aaaafffffbbbbaaa      aaa"
2800 DATA" nak    aaaafffffbbbbaaa    ffffbb"
2810 DATA" nak    aaaafffffbbbbaaa    ffffbb"
2820 DATA" nnn bbbbbbbfffffbbbbfffffaaffffbb"
2830 DATA" nnn bbbbbbbfffffbbbbfffffaaffffbb"
2840 DATA"nnnn bbbbbbbfffffbbbbfffffaaffffbbaa"
2850 DATA"nnnn bbbaaaaaaaaaaabbfffffaaffffbbaa"
2860 DATA"ffffffbbfffaafffaaabbfffffaaffffbbaa"
2870 DATA"ffffffbbfffaafffaaabbfffffaaffaaanaa"
2880 DATA"ffffffbbfffaafffaaabbfffffaaffaaanaa" 
2890 DATA"ffffffbbfffaafffaaabbfffffabbbaaanaa"
2900 DATA 16,4,1,18,4,1,17,5,1,15,5,1,18,6,1,19,6,1,15,7,1,18,8,1,16,8,1,17,9,1,15,9,1,16,10,1,18,10,1,16,12,1,17,12,1,13,17,1,5,16,1,6,15,1,7,16,1
2910 DATA 6,17,1,12,15,1,12,17,1,13,16,1,16,15,1,18,14,1,25,11,1,26,12,1,26,14,1,28,15,1,9,17,4,22,17,4,32,10,1,33,14,1,1