1000 '------------------------
1010 '---- XFORCE-WARRIOR ----
1020 '-- ALIEN OF MERCY MIX --
1030 '------------------------
1040 '-- Part 1    THE GAME --
1050 '-- (c) im Januar 1989 --
1060 '-- by Alexander Stroh --
1070 '------------------------
1080 '-- [CPC 464/664/6128] -- 
1090 '------------------------
1100 ' [1] PREPARING PARTS
1110 ' Fill memory
1120 MEMORY &3FFF:IF PEEK(&93C0)=&C9 THEN GOSUB 2730:GOTO 1200
1130 LOAD"!xforce.bin",&8000:CALL &93C0:DEG:RESTORE 3330:GOSUB 3050
1140 INK 15,8:ik=0:EVERY 3,0 GOSUB 2540:GOSUB 2650:GOSUB 2740
1150 b=0:FOR l=&9A00 TO &9FFF
1160 c=INT(RND*13):IF c=b THEN 1160
1170 POKE l,c:b=c:NEXT
1180 FOR l=&A000 TO &A5FF STEP 3:POKE l,2+RND*4
1190 POKE l+1,151+2*INT(RND*20):POKE l+2,4+RND*6:NEXT
1200 IF PEEK(&AC01)THEN POKE &B4E8,&FF ELSE POKE &B632,&FF
1210 r=REMAIN(0):GOSUB 2960
1220 ' Defining part
1230 DEFINT a-r,t-z:KEY DEF 66,1,0:KEY DEF 70,1,0
1240 DEF FNX=PEEK(&9926)*PEEK(&992E)
1250 DEF FNc1(x)=(x+1)MOD 3:DEF FNc2(x)=PEEK(&9951+x):DEF FNc3(x)=128+2^x
1260 ENT-1,1,3,1:ENT-2,2,1,1,4,-1,1,2,1,1
1270 ENV 1,1,14,1,14,-1,2:ENV 2,1,15,1,1,0,7,15,-1,7
1280 ENV 3,1,13,1,13,-1,9:ENV 4,1,11,1,1,0,9,2,-1,5,9,-1,19
1290 DIM bd(5),f(11),n(8),p(15),q(15),s(9),d$(9),n$(9)
1300 OPENIN"!xforce.top":FOR l=0 TO 9:INPUT#9,n$(l),s(l),d$(l):NEXT:CLOSEIN
1310 RESTORE 3420:FOR l=0 TO 5:READ bd(l):NEXT
1320 FOR l=0 TO 11:READ f(l):NEXT
1330 FOR l=0 TO 15:READ p(l),q(l):NEXT
1340 FOR l=0 TO 8:READ n(l):NEXT
1350 ' Welcome
1360 PAPER 0:MODE 1:PEN 1:LOCATE 14,1:PRINT"XFORCE-WARRIOR":PEN 2
1370 LOCATE 19,3:PRINT CHR$(164)+" by":PEN 3:LOCATE 16,5:PRINT"SUPRA-SOFT"
1380 PEN 2:LOCATE 1,8
1390 PRINT"  The XFORCE-mission in your mind will"
1400 PRINT"  take away your breath. Danger waits"
1410 PRINT"  in every second and the end is death."
1420 PEN 3:PRINT
1430 PRINT"  Won't you play it 'cause you are the"
1440 PRINT"  loosing guy? And won't you fly about"
1450 PRINT"  the town?"
1460 PEN 2:PRINT
1470 PRINT"  Can't you see them, babe, the aliens"
1480 PRINT"  in the sky? Your turn to do it! Soon"
1490 PRINT"  be down!"
1500 PEN 3:LOCATE 16,20:PRINT"GOOD LUCK!"
1510 PEN 1:LOCATE 8,23:PRINT"PRESS ANY KEY TO CONTINUE;"
1520 CALL &BD19:INK 1,4:INK 2,8:INK 3,17
1530 CALL &BB03:CALL &BB18:RANDOMIZE TIME
1540 ' Set up screen
1550 GOSUB 2960:MODE 0
1560 WINDOW#1,4,17,2,9:PAPER#1,0:WINDOW#2,1,20,1,12:PAPER#2,15
1570 RESTORE 3180:FOR h=1 TO 25:READ i$:j=LEN(i$)
1580 IF j=9 THEN|SPLIT:|OUTLINE,1,h,@i$:|TURN,h:|SPLIT ELSE|OUTLINE,1,h,@i$
1590 NEXT:PRINT CHR$(23)CHR$(1);:FOR l=0 TO 398 STEP 2:MOVE 0,l:DRAWR 200,0,2
1600 NEXT:PRINT CHR$(23)CHR$(0);:|COPY:PAPER 15:CLS:GOSUB 2980
1610 RESTORE 3120:PAPER 2:FOR h=1 TO 5:READ i,j,k,l
1620 FOR m=i TO j:LOCATE k,m:PRINT SPACE$(l):NEXT m,h
1630 FOR h=1 TO 16:READ i,j:PLOT i,j,15:NEXT
1640 RESTORE 3250:|SPLIT:FOR h=1 TO 11:READ i,j,k$
1650 |OUTLINE,i,j,@k$:IF ASC(LEFT$(k$,1))>57 AND h<9 THEN|STRETCH,i,j,4,3
1660 NEXT:LOCATE 3,24:|FORMAT,s(0):|SPLIT:PAPER 15
1670 ' Initiating part
1680 a=1:b=0:lev=1:sc=0:x=40:y=167:POKE &992E,0:POKE &994D,2:POKE &A666,0
1690 |INIT:GOSUB 2670:CALL &BD19:ON a+1 GOSUB 2800,2840,2900:|LOOP,1:b=b+1
1700 IF lev=1 THEN GOSUB 2580:|BOARD,199:CALL &BB03:CALL &BB18 ELSE|LOOP,2
1710 GOSUB 2460:POKE &A66C,1:|SPRITE,x,y,13:SOUND 129,92,29192,2,,,lev*5-3
1720 AFTER 1800*(lev+1),3 GOSUB 1810:EVERY 3,2 GOSUB 1770:EVERY 1,1 GOSUB 1790
1730 ' [2] MAIN PROGRAM
1740 ' Top-loop
1750 WHILE FNX<>124:WEND:POKE &9851,&C9:GOTO 1840
1760 ' Scrolling
1770 CALL &94A7:RETURN
1780 ' Fight
1790 CALL &9710:RETURN
1800 ' Break
1810 POKE &992E,1:RETURN
1820 ' [3] THE ROUTINES
1830 ' Next level
1840 FOR l=1 TO 2222:NEXT:FOR l=1 TO 3:r=REMAIN(l):NEXT:CALL &BCA7:CLS#1
1850 |LOOP,2:sc=PEEK(&992F)+256*PEEK(&9930):sc=sc+lev*10*PEEK(&9932):sc2=sc/2
1860 ds=0:bn=PEEK(&9931):b=b+1:lev=lev+1:IF PEEK(&9932)=0 OR lev=4 THEN 1910
1870 GOSUB 2460:|LOOP,3:GOSUB 2500:LOCATE 15,19:|FORMAT,0:|LOOP,3:GOSUB 2280
1880 LOCATE 15,19:|FORMAT,115-lev*15:POKE &A66C,0:LOCATE 16,24:|FORMAT,lev
1890 GOSUB 2760:GOSUB 2960:|LOOP,2:GOTO 1690
1900 ' Topten (type in)
1910 |LOOP,4:GOSUB 2960:PAPER 0:MODE 1
1920 IF sc<=s(9)THEN 2090 ELSE s(9)=sc:PEN 1:LOCATE 13,8
1930 PRINT"GOT A TOP-SCORE!":LOCATE 13,18:PRINT"<C> TO CONTINUE;":PEN 2
1940 LOCATE 13,10:PRINT"ENTER YOUR NAME:":LOCATE 13,14:PRINT"TYPE IN THE DAY:"
1950 CALL &BD19:CALL &BB03:INK 1,4:INK 2,8:INK 3,17
1960 PEN 3:LOCATE 13,12:LINE INPUT"",n$(9):IF LEN(n$(9))<17 THEN 1980
1970 LOCATE 13,12:PRINT SPACE$(28);:LOCATE 1,13:PRINT SPACE$(40);:GOTO 1960
1980 LOCATE 13,16:PRINT"**            **":LOCATE 16,16:LINE INPUT"",d$(9)
1990 IF LEN(d$(9))=10 THEN 2010
2000 LOCATE 13,16:PRINT SPACE$(28);:LOCATE 1,17:PRINT SPACE$(40);:GOTO 1980
2010 IF INKEY(62)THEN 2010 ELSE en=1
2020 ' Topten (sort)
2030 FOR l=9 TO 1 STEP-1:IF s(l)<=s(l-1)THEN 2070
2040 s1=s(l):s(l)=s(l-1):s(l-1)=s1
2050 n1$=n$(l):n$(l)=n$(l-1):n$(l-1)=n1$
2060 d1$=d$(l):d$(l)=d$(l-1):d$(l-1)=d1$
2070 NEXT:CLS:GOSUB 2960
2080 ' Topten (print)
2090 PEN 1:LOCATE 13,1:PRINT"THE HALL OF FAME":LOCATE 13,2
2100 PRINT STRING$(16,45):PEN 2:FOR l=1 TO 10
2110 LOCATE 21,2+l*2:PRINT USING"#####";s(l-1);:PRINT"0"
2120 IF s(l-1)<10000 THEN LOCATE 21,2+l*2:PRINT"0"
2130 NEXT:PEN 3:FOR l=1 TO 10:ln=LEN(n$(l-1)):LOCATE 1,2+l*2
2140 PRINT n$(l-1);STRING$(16-ln,46):LOCATE 31,2+l*2:PRINT d$(l-1)
2150 NEXT:PEN 1:LOCATE 8,24:PRINT STRING$(26,45):LOCATE 8,25
2160 PRINT"PRESS ANY KEY TO CONTINUE;";:INK 1,4:INK 2,8:INK 3,17
2170 CALL &BB03:CALL &BB18
2180 ' Exit
2190 RESTORE 3340:GOSUB 3050:INK 15,8:ik=0:EVERY 3,0 GOSUB 2540
2200 FOR l=1 TO 9999:NEXT:r=REMAIN(0)
2210 GOSUB 3050:INK 15,8:ik=0:EVERY 3,0 GOSUB 2540
2220 IF INKEY(1)<>INKEY(75)THEN 2240 ELSE IF INKEY(8)=INKEY(74)THEN 2220
2230 r=REMAIN(0):GOSUB 2760:GOSUB 2730:GOTO 1550
2240 r=REMAIN(0):|LOOP,1:IF en=0 THEN 2260
2250 OPENOUT"!xforce.top":FOR l=0 TO 9:WRITE#9,n$(l),s(l),d$(l):NEXT:CLOSEOUT
2260 |LOOP,5:PAPER 0:PEN 1:MODE 1:CALL &BC02:CALL &BB00:END
2270 ' The nice game (1)
2280 FOR l=7 TO 11:INK l,ik:NEXT:MOVE 100,206:DRAW 538,206,15:CLS#2
2290 PEN 7:LOCATE 5,6:PRINT"1 4 8  0 9 2":LOCATE 5,8:PRINT"2 7 3  5 6 1"
2300 k=8:FOR l=4 TO 8 STEP 2:FOR m=6 TO 8 STEP 2
2310 MOVE l*32-8,(26-m)*16+2:GOSUB 2710:MOVE(l+7)*32-8,(26-m)*16+2:GOSUB 2710
2320 NEXT m,l:k=10:FOR l=0 TO 15:MOVE p(l)*32-8,(26-q(l))*16+2:GOSUB 2710:NEXT
2330 ON a+1 GOSUB 2940,2880,2940:a=(a+1)MOD 3:|LOOP,1:PEN 9:k=0:l=0:n=0
2340 EVERY 29-2*lev GOSUB 2410:CALL &BB03
2350 i$="":WHILE i$="":i$=INKEY$:WEND
2360 IF i$<>"X"AND i$<>CHR$(224)THEN 2350 ELSE r=REMAIN(0)
2370 IF f=f(l)THEN h=0:GOSUB 2560:ad=f:RETURN
2380 h=6:GOSUB 2560:n=n+1:sc=sc-10*bn*n:GOSUB 2500
2390 IF n<3 THEN 2340 ELSE ad=0:RETURN
2400 ' The nice game (2)
2410 LOCATE p(k)+1,q(k):PRINT" ":ds=1-ds
2420 k=(k+1)MOD 16:IF k*(k-6)*(k-8)*(k-14)THEN l=(l+1)MOD 12
2430 f=INT(RND*10):LOCATE p(k)+1,q(k):PRINT USING"#";f
2440 sc=sc-(bn AND sc>sc2 AND ds=0):GOSUB 2500:h=3:GOTO 2560
2450 ' New board
2460 l=bd(b-1):EVERY 1,0 GOSUB 2480
2470 WHILE l>bd(b)-1:WEND:r=REMAIN(0):|LOOP,2:RETURN
2480 l=l-2:IF l<bd(b)THEN RETURN ELSE|BOARD,l:RETURN
2490 ' Set score
2500 IF sc<0 THEN sc=0
2510 POKE &9930,INT(sc/256):POKE &992F,sc-256*PEEK(&9930)
2520 LOCATE 3,19:|FORMAT,sc:RETURN
2530 ' Color-movement
2540 INK ik,0:ik=(ik+1)MOD 4:INK ik,4:RETURN
2550 ' Trivial sound
2560 FOR i=0 TO 2:SOUND 128+2^i,n(h+i),0,0,3:NEXT:RETURN
2570 ' Welcome sound
2580 c1=2:c3=7:c4=0:WHILE c3<>14:c1=FNc1(c1):c2=FNc2(c4)
2590 IF FNc2(c4+1)=0 THEN c3=FNc2(c4+2):c4=c4+3 ELSE c3=7:c4=c4+1
2600 SOUND FNc3(c1),c2,0,0,4,2:c5=2:AFTER c3 GOSUB 2630
2610 WHILE c5:WEND:WEND:|LOOP,4:RETURN
2620 ' Set-flag-subroutine
2630 c5=FNc1(c5):RETURN
2640 ' Set landscape
2650 FOR l=1 TO 8:POKE &9906+2*l,8+16*l:POKE &9907+2*l,0:NEXT l:RETURN
2660 ' Show landscape
2670 CLS#2:CLS#1:MOVE 100,206:DRAWR 438,0,12:MOVE 100,384:DRAWR 438,0,0
2680 POKE &946C,24:FOR k=24 TO 120 STEP 16:|SPRITE,k,127,0:NEXT:POKE &946C,14
2690 RETURN
2700 ' Paint square
2710 DRAWR 40,0,k:DRAWR 0,-20:DRAWR-40,0:DRAWR 0,20:RETURN
2720 ' Actualize (1)
2730 RESTORE 3370:FOR l=0 TO 42:READ a$:POKE &9908+l,VAL("&"+a$):NEXT
2740 POKE &946C,24:POKE &9851,&3A:POKE &992E,0:POKE &A66C,0:RETURN
2750 ' Actualize (2)
2760 POKE &9851,&3A:POKE &9918,lev*&18:POKE &9919,&9A:POKE &9924,0:POKE &9925,0
2770 POKE &9928,0:POKE &9929,&A0:POKE &992E,0:POKE &9931,(bn+10*ad)AND lev<4
2780 POKE &9932,115-lev*15:POKE &994D,lev*5-3:GOTO 2650
2790 ' Inks on (1)
2800 BORDER 13:INK 15,13:ik=13
2810 INK 14,1:INK 13,13:INK 12,12:INK 11,2:INK 10,1:INK 9,0:INK 8,3:INK 7,9
2820 INK 6,2:INK 5,1:INK 4,0:INK 3,6:INK 2,0:INK 1,24:INK 0,0:RETURN
2830 ' Inks on (2.1)
2840 BORDER 0:INK 15,0:ik=0
2850 INK 14,24:INK 13,0:INK 12,9:INK 11,6:INK 10,0:INK 9,0:INK 8,3:INK 7,18
2860 INK 6,24:INK 5,12:INK 4,12:INK 3,2:INK 2,1:INK 1,26:INK 0,1:RETURN
2870 ' Inks on (2.2)
2880 INK 11,6:INK 10,6:INK 9,2:INK 8,3:INK 7,1:RETURN
2890 ' Inks on (3.1)
2900 BORDER 9:INK 15,9:ik=9
2910 INK 14,26:INK 13,9:INK 12,26:INK 11,20:INK 10,26:INK 9,1:INK 8,13:INK 7,11
2920 INK 6,6:INK 5,3:INK 4,26:INK 3,0:INK 2,2:INK 1,13:INK 0,2:RETURN
2930 ' Inks on (3.2)
2940 INK 11,2:INK 10,2:INK 9,26:INK 8,1:INK 7,6:RETURN
2950 ' Inks off
2960 BORDER 0:FOR l=0 TO 15:INK l,0:NEXT:RETURN
2970 ' The title
2980 PAPER 0:PEN 1:INK 0,0:INK 1,0:LOCATE 1,25:PRINT"X-FORCEWARRIOR"
2990 FOR k=2 TO 14 STEP 2:FOR l=0 TO 220 STEP 4
3000 IF TEST(l,k)THEN PLOT 74+l+k*2,182+k,4:PLOTR 0,-2,6
3010 NEXT l,k:FOR k=2 TO 14 STEP 2:FOR l=224 TO 444 STEP 4
3020 IF TEST(l,k)THEN PLOT 130+l-k*2,182+k,4:PLOTR 0,-2,6
3030 NEXT l,k:PAPER 15:LOCATE 1,25:PRINT SPACE$(16):PAPER 0:RETURN
3040 ' Last before four
3050 GOSUB 2960:MODE 0:PAPER 14:CLS:PEN 15:READ a1$,a2$
3060 LOCATE 8,8:PRINT a1$:LOCATE 8,14:PRINT a2$
3070 |STRETCH,8,8,6,5:|STRETCH,8,14,6,5
3080 ORIGIN 320,200:k=0:MOVE 200,0:FOR l=5 TO 360 STEP 5
3090 DRAW 200*COS(l),160*SIN(l),k:k=(k+1)MOD 4:NEXT:RETURN
3100 ' [4] THE DATAS
3110 ' Place the fields
3120 DATA 2,12,4,14,15,25,3,4,15,25,15,4,15,19,8,6,21,25,8,6
3130 ' Round the corner
3140 DATA 64,0,188,0,224,0,412,0,448,0,572,0
3150 DATA 224,78,412,78,224,96,412,96
3160 DATA 64,174,188,174,224,174,412,174,448,174,572,174
3170 ' Level-lines
3180 DATA "PRESS ANY","  KEY TO  ","  START  "," MISSION "
3190 DATA " ","  XFORCE  ","ATTACKING","TYRODROME","   CITY"
3200 DATA " "," LEVEL IS ","   OVER   "," "
3210 DATA "  XFORCE  "," ORBITING "," THE ICE ","  VALLEY  "
3220 DATA " "," LEVEL IS ","   OVER   "," "
3230 DATA "  XFORCE  ","EXPECTING"," FIGHT IN ","THE NIGHT"
3240 ' Display-words
3250 DATA 3,16,"SCORE:",15,16,"BONUS:"
3260 DATA 3,19,"000000",15,19,"001000"
3270 DATA 3,21,"HI-SC:",15,21,"LEVEL:"
3280 DATA 3,24,"476700",15,24,"000001"
3290 DATA 8,22,"  (C) BY  "
3300 DATA 8,23,"**********"
3310 DATA 8,24,"SUPRA-SOFT"
3320 ' Triple message
3330 DATA "PLEASE","!WAIT!"
3340 DATA " GAME "," OVER "
3350 DATA "AGAIN?","Y<-->N"
3360 ' Constant values
3370 DATA 18,00,28,00,38,00,48,00,58,00,68
3380 DATA 00,78,00,88,00,00,9A,37,38,00,00
3390 DATA 54,C1,5C,F9,70,6F,00,00,7C,05,00
3400 DATA A0,37,38,31,30,00,00,00,0A,64,00
3410 ' Board start
3420 DATA 199,159,127,95,63,31
3430 ' Inner circle
3440 DATA 1,4,8,0,9,2,1,6,5,3,7,2
3450 ' Just around it
3460 DATA 4,4,6,4,8,4,11,4,13,4,15,4,17,6,17,8
3470 DATA 15,10,13,10,11,10,8,10,6,10,4,10,2,8,2,6
3480 ' Trivial notes
3490 DATA 119,95,80,239,190,159,478,379,319