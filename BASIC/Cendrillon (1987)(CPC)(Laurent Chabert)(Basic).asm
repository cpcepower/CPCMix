1 '
2 'CENDRILLON
3 '
4 'Laurent CHABERT
5 'Mai 1986
6 '
10 ON BREAK GOSUB 410
15 GOSUB 65000:RESTORE
20 GOTO 880
30 '** TEST OBJETS EN POCHE **
40 PRINT#2:PRINT#2:PRINT#2
50 IF ccleja=0 AND cclebu=0 AND clam=0 AND poch$="" THEN PRINT#2,"Tu ne":PRINT#2,"transportes":PRINT#2,"rien.":RETURN
60 PRINT#2,"Tu transportes":PRINT#2
70 IF clam<>0 THEN PRINT#2,"Une lampe"
80 IF ccleja=1 AND cclebu=1 THEN PRINT#2,"Deux cles"
90 IF ccleja=1 AND cclebu=0 THEN PRINT#2,"Une cle"
100 IF ccleja=0 AND cclebu=1 THEN PRINT#2,"Une cle"
110 IF poch$<>"" THEN PRINT#2,poch$
120 RETURN
130 '** TEST VISITE BELLE-MERE **
140 tps=tps+1:LOCATE #3,1,1:PRINT#3,"Temps :";tps:IF tps=60 THEN CLS:PRINT"Tu n'es pas allee voir":PRINT"ta belle-mere depuis plus d'une heure,":PRINT"tu es privee de bal.":GOTO 50000
150 RETURN
160 '** TEST TACHES EFFECTUEES **
170 t=t-1:
180 IF t>1 THEN PRINT"Il te reste";t;"taches a effectuer."
190 IF t=1 THEN PRINT"Il ne te reste qu'une tache a effectuer."
200 IF t=0 THEN PRINT"Tu as termine ton travail, retourne":PRINT"dans ta chambre, pour te preparer."
210 RETURN
220 '** TEST VOCABULAIRE **
230 esp=0
240 FOR i=1 TO LEN(act$)
250 IF MID$(act$,i,1)=" " THEN esp=i
260 NEXT i
270 IF esp=0 THEN PRINT"Je ne comprend pas !":RETURN
280 actg$=LEFT$(act$,esp-1):ver$=LEFT$(actg$,4)
290 actd$=RIGHT$(act$,LEN(act$)-esp):obj$=LEFT$(actd$,4)
300 ver=0:obj=0
310 RESTORE 30000
320 FOR i=1 TO 15
330 READ dat$:IF dat$=ver$ THEN ver=1
340 NEXT i
350 FOR i=1 TO 28
360 READ dat$:IF dat$=obj$ THEN obj=1
370 NEXT i
380 IF ver=0 THEN PRINT"Je ne comprend pas ";actg$;" !"
390 IF obj=0 THEN PRINT"Je ne comprend pas ";actd$;" !"
400 RETURN
410 WINDOW #0,1,40,1,25:MODE 2:INK 0,13:PAPER 0:INK 1,0:PEN 1:BORDER 13:LIST
420 '** DESSIN ARMOIRE **
421 FOR x=0 TO 20 STEP 2:MOVE x,0:DRAW x,280,2:NEXT x
422 FOR x=20 TO 140 STEP 2:MOVE x,10:DRAW x,280,2:NEXT x
423 FOR x=140 TO 160 STEP 2:MOVE x,0:DRAW x,280,2:NEXT x
424 MOVE 10,20:DRAW 10,270,1:DRAW 150,270,1:DRAW 150,20,1:DRAW 10,20:MOVE 80,20:DRAW 80,270,1
425 MOVE 60,140:DRAW 70,140,1:MOVE 90,140:DRAW 100,140,1
426 RETURN
430 '** DESSIN LIT **
431 FOR y=20 TO 50 STEP 2:MOVE 180,y:DRAW 370,y,3:NEXT y
432 FOR x=170 TO 180 STEP 2:MOVE x,0:DRAW x,100,2:NEXT x
433 FOR x=370 TO 380 STEP 2:MOVE x,0:DRAW x,140,2:NEXT x
434 FOR i=1 TO 360 STEP 15:ORIGIN 355,65:DRAW COS(i)*14,SIN(i)*14,3:NEXT i
435 ORIGIN 0,0:RETURN
440 '** DESSIN TABLE **
441 FOR x=145 TO 160 STEP 2:MOVE x,0:DRAW x,100,2:MOVE x+95,0:DRAW x+95,100,2:NEXT x
442 FOR y=100 TO 110 STEP 2:MOVE 100,y:DRAW 300,y,3:NEXT y
443 RETURN
450 '** DESSIN ASPIRATEUR **
451 MOVE 196,0:DRAW 244,0,2:MOVE 196,2:DRAW 244,2,2:MOVE 198,4:DRAW 242,4,2:MOVE 214,6:DRAW 226,6,2
452 FOR y=8 TO 50 STEP 2:MOVE 218,y:DRAW 222,y,3:NEXT y
453 MOVE 206,52:DRAW 234,52,2:MOVE 206,54:DRAW 234,54,2:MOVE 204,56:DRAW 236,56,2:MOVE 204,58:DRAW 236,58,2
454 FOR y=60 TO 120 STEP 2:MOVE 202,y:DRAW 238,y,2:NEXT y
455 MOVE 204,122:DRAW 236,122,2:MOVE 204,124:DRAW 236,124,2:MOVE 206,126:DRAW 234,126,2:MOVE 206,128:DRAW 234,128,2
456 FOR y=130 TO 150 STEP 2:MOVE 218,y:DRAW 222,y,3:NEXT y
457 FOR y=70 TO 110 STEP 2:MOVE 218,y:DRAW 222,y,3:NEXT y
458 MOVE 218,152:DRAW 222,152,2:PLOT 220,154,2
459 RETURN
800 '** INTERROGATION **
801 act$="":PRINT
802 LOCATE 1,VPOS(#0):PRINT"?>";act$;"< ";
803 IF LEN(act$)=26 THEN SOUND 4,200,50,7:SOUND 1,200/3,50,7:SOUND 2,200/5,50,7:PRINT" ":PRINT"Cette phrase est trop longue !":GOTO 800
804 in$=INKEY$:IF in$="" GOTO 804
805 IF in$=CHR$(13) GOTO 870
806 IF in$=CHR$(127) AND LEN(act$)=0 THEN SOUND 4,100,10,7:SOUND 1,100/3,10,7:SOUND 2,100/5,10,7:GOTO 804
807 IF in$=CHR$(127) AND LEN(act$)>0 THEN act$=LEFT$(act$,LEN(act$)-1):GOTO 802
808 act$=act$+in$:GOTO 802
870 PRINT " ":act$=UPPER$(act$):RETURN
880 '** DEBUT **
885 ENV 1,2,1,2,2,-1,2:ENV 2,2,-2,4
890 GOSUB 40000
891 MODE 1
895 DEG
900 WINDOW #0,1,40,1,6:PAPER 0:PEN 2:CLS
910 WINDOW #1,1,25,7,25:PAPER #1,1:PEN #1,2:CLS #1
920 WINDOW #2,26,40,7,24:PAPER #2,3:PEN #2,2:CLS #2
930 WINDOW #3,26,40,25,25:PAPER #3,3:PEN #3,2:CLS #3
940 brk=0:fp=0:t=8:gr1=0:clam=0:cclebu=0:ccleja=0:cof2=0:cof8=0:arm4=0:arm6=0:arm7=0:arm9=0:arm10=0:ref=0:con=0:portcav=0:portja=0
950 clebu=1:cleja=1:aspdef=1:aspbon=1:lam=1:dra=1:ser=1:linsal=1:linsec=1:via=1:boi=1:bou=1
951 coef=12
960 poch$=""
970 tps=0:LOCATE #3,1,1:PRINT#3,"Temps :";tps
980 EVERY 250,3 GOSUB 130
990 GOTO 10000
1000 '** GRENIER #1 **
1005 BORDER 1:INK 0,1:INK 1,15:INK 2,26:INK 3,0
1010 CLS #1:CLS #2
1020 LOCATE #1,8,1:PRINT#1,"GRENIER N. 1"
1029 CLS #2
1030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2,"Bas":PRINT#2:PRINT#2,"Un message"
1040 GOSUB 30
1050 GOSUB 800
1070 IF act$="DROITE" GOTO 2000
1080 IF act$="BAS" GOTO 6000
1090 IF act$="GAUCHE" THEN gr1=1:GOTO 11000
1100 IF act$="HAUT" GOTO 1140
1110 GOSUB 220
1120 IF esp=0 OR ver=0 OR obj=0 GOTO 1050
1130 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Faire du feu.":GOTO 1050
1140 PRINT"Impossible !":GOTO 1050
2000 '** GRENIER #2 **
2005 BORDER 0:INK 0,0:INK 1,13:INK 2,26:INK 3,3
2010 CLS #1:CLS #2
2020 LOCATE #1,8,1:PRINT#1,"GRENIER N. 2"
2021 FOR y=0 TO 60 STEP 2:MOVE 120,y:DRAW 280,y,3:NEXT y
2022 FOR y=0 TO 60 STEP 10:MOVE 120,y:DRAW 280,y,0:NEXT y:MOVE 120,0:DRAW 120,60,0:MOVE 280,0:DRAW 280,60,0
2023 x1=122:x2=278:FOR y=62 TO 70 STEP 2:MOVE x1,y:DRAW x2,y,3:x1=x1+6:x2=x2-6:NEXT y
2025 MOVE 120,60:DRAW x1,y,0:DRAW x2,y,0:DRAW 280,60,0
2029 CLS #2
2030 PRINT#2,"Gauche":PRINT#2
2040 PRINT#2,"Un coffre"
2050 IF clebu=1 AND cof2=1 THEN PRINT#2,"Une cle"
2060 GOSUB 30
2070 GOSUB 800
2090 IF act$="GAUCHE" GOTO 1000
2100 IF act$="HAUT" OR act$="DROITE" OR act$="BAS" GOTO 2160
2110 GOSUB 220
2120 IF esp=0 OR ver=0 OR obj=0 GOTO 2070
2130 IF ver$="OUVR" AND obj$="COFF" AND cof2=1 THEN PRINT"Le coffre est deja ouvert.":GOTO 2070
2140 IF ver$="OUVR" AND obj$="COFF" AND cof2=0 THEN PRINT"Il y a une cle a l'interieur.":cof2=1:GOTO 2029
2150 IF ver$="PREN" AND obj$="CLE" AND cof2=1 AND clebu=1 THEN PRINT"D'accord.":clebu=0:cclebu=1:GOTO 2029
2160 PRINT"impossible !":GOTO 2070
3000 '** GRENIER #3 **
3005 BORDER 13:INK 0,13:INK 1,2:INK 2,0:INK 3,26
3010 CLS #1:CLS #2
3020 LOCATE #1,8,1:PRINT#1,"GRENIER N. 3"
3030 PRINT#2,"Droite":PRINT#2
3040 IF aspdef=1 THEN PRINT#2,"Un aspirateur":GOSUB 450
3050 GOSUB 30
3060 GOSUB 800
3080 IF act$="DROITE" GOTO 4000
3090 IF act$="HAUT" OR act$="GAUCHE" OR act$="BAS" GOTO 3130
3100 GOSUB 220
3110 IF esp=0 OR ver=0 OR obj=0 GOTO 3060
3120 IF ver$="PREN" AND obj$="ASPI" AND poch$="" THEN PRINT"D'accord.":aspdef=0:poch$="Aspirateur":GOTO 3010
3130 PRINT"Impossible !":GOTO 3060
4000 '** GRENIER #4 **
4005 BORDER 0:INK 0,0:INK 1,2:INK 2,25:INK 3,1
4010 CLS #1:CLS #2
4020 LOCATE #1,8,1:PRINT#1,"GRENIER N. 4"
4021 GOSUB 420
4029 CLS #2
4030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2
4040 PRINT#2,"Un message":PRINT#2,"Une armoire"
4050 IF arm4=1 AND cleja=1 THEN PRINT#2,"Une cle"
4060 GOSUB 30
4070 GOSUB 800
4090 IF act$="GAUCHE" GOTO 3000
4100 IF act$="DROITE" GOTO 5000
4110 IF act$="HAUT" OR act$="BAS" GOTO 4180
4120 GOSUB 220
4130 IF esp=0 OR ver=0 OR obj=0 GOTO 4070
4140 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Passer l'aspirateur dans":PRINT"la salle a manger.":GOTO 4070
4150 IF ver$="OUVR" AND obj$="ARMO" AND arm4=0 THEN PRINT"Il y a une cle a l'interieur.":arm4=1:GOTO 4029
4160 IF ver$="OUVR" AND obj$="ARMO" AND arm4=1 THEN PRINT"Elle est deja ouverte":GOTO 4070
4170 IF ver$="PREN" AND obj$="CLE" AND arm4=1 AND cleja=1 THEN PRINT"D'accord.":cleja=0:ccleja=1:GOTO 4029
4180 PRINT"Impossible !":GOTO 4070
5000 '** GRENIER #5 **
5005 BORDER 9:INK 0,9:INK 1,8:INK 2,0:INK 3,25
5010 CLS #1:CLS #2
5020 LOCATE #1,8,1:PRINT#1,"GRENIER N. 5"
5021 GOSUB 440
5029 CLS #2
5030 PRINT#2,"Gauche":PRINT#2,"Bas":PRINT#2
5040 PRINT#2,"Une table"
5050 IF lam=1 THEN PRINT#2,"Une lampe":FOR y=112 TO 130 STEP 2:MOVE 220,y:DRAW 234,y,2:NEXT:FOR y=122 TO 126 STEP 2:MOVE 224,y:DRAW 230,y,3:NEXT
5060 GOSUB 30
5070 GOSUB 800
5090 IF act$="GAUCHE" GOTO 4000
5100 IF act$="BAS" GOTO 10000
5110 IF act$="HAUT" OR act$="DROITE" GOTO 5150
5120 GOSUB 220
5130 IF esp=0 OR ver=0 OR obj=0 GOTO 5070
5140 IF ver$="PREN" AND obj$="LAMP" AND lam=1 THEN PRINT"D'accord.":lam=0:clam=2:FOR y=112 TO 130 STEP 2:MOVE 220,y:DRAW 234,y,1:NEXT y:GOTO 5029
5150 PRINT"Impossible !":GOTO 5070
6000 '** LINGERIE **
6005 BORDER 26:INK 0,26:INK 1,14:INK 2,1:INK 3,17
6010 CLS #1:CLS #2
6020 LOCATE #1,10,1:PRINT#1,"LINGERIE"
6021 GOSUB 420
6029 CLS #2
6030 PRINT#2,"Droite":PRINT#2,"Haut":PRINT#2,"Bas":PRINT#2
6040 PRINT#2,"Un message":PRINT#2,"Une armoire"
6050 IF arm6=1 AND dra=1 THEN PRINT#2,"Des draps"
6060 IF arm6=1 AND ser=1 THEN PRINT#2,"Des serviettes"
6070 GOSUB 30
6080 GOSUB 800
6100 IF act$="HAUT" GOTO 1000
6110 IF act$="BAS" GOTO 12000
6120 IF act$="DROITE" GOTO 7000
6130 IF act$="GAUCHE" GOTO 6220
6140 GOSUB 220
6150 IF esp=0 OR ver=0 OR obj=0 GOTO 6080
6160 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Prendre la viande dans le congelateur":PRINT"et la ranger dans le refrigerateur.":GOTO 6080
6170 IF ver$="OUVR" AND obj$="ARMO" AND arm6=0 THEN PRINT"Il y a des serviettes et":PRINT"des draps a l'interieur.":arm6=1:GOTO 6029
6180 IF ver$="OUVR" AND obj$="ARMO" AND arm6=1 THEN PRINT"Elle est deja ouverte.":GOTO 6080
6190 IF ver$="PREN" AND obj$="SERV" AND arm6=1 AND ser=1 AND poch$="" THEN PRINT"D'accord.":ser=0:poch$="Serviette":GOTO 6029
6200 IF ver$="PREN" AND obj$="DRAP" AND arm6=1 AND dra=1 AND poch$="" THEN PRINT"D'accord.":dra=0:poch$="Draps":GOTO 6029
6210 IF ver$="RANG" AND obj$="LING" AND arm6=1 AND poch$="Linge sec" THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 6029
6220 PRINT"Impossible !":GOTO 6080
7000 '** CHAMBRE DE LA BELLE-MERE **
7005 BORDER 25:INK 0,25:INK 1,16:INK 2,3:INK 3,17
7010 CLS #1:CLS #2
7020 LOCATE #1,1,1:PRINT#1,"CHAMBRE DE LA BELLE-MERE"
7021 GOSUB 420:GOSUB 430
7029 CLS #2
7030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2
7040 PRINT#2,"Un lit":PRINT#2,"Une armoire"
7050 GOSUB 30
7060 GOSUB 800
7080 IF act$="HAUT" OR act$="BAS" GOTO 7140
7090 IF act$="GAUCHE" GOTO 6000
7100 IF act$="DROITE" GOTO 8000
7110 GOSUB 220
7120 IF esp=0 OR ver=0 OR obj=0 GOTO 7060
7130 IF ver$="RANG" AND obj$="BOUI" AND poch$="Bouillotte" AND arm7=1 THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 7029
7131 IF ver$="OUVR" AND obj$="ARMO" AND arm7=0 THEN PRINT"Elle ne contient rien d'interessant.":arm7=1:GOTO 7060
7132 IF ver$="OUVR" AND obj$="ARMO" AND arm7=1 THEN PRINT"Elle est deja ouverte.":GOTO 7060
7140 PRINT"Impossible !":GOTO 7060
8000 '** SALLE DE BAIN **
8005 BORDER 16:INK 0,16:INK 1,26:INK 2,2:INK 3,0
8010 CLS #1:CLS #2
8020 LOCATE #1,7,1:PRINT#1,"SALLE DE BAIN"
8021 FOR y=0 TO 110 STEP 4:MOVE 100,y:DRAW 190,y,2:MOVE 100,y+2:DRAW 190,y+2,0:NEXT y
8022 MOVE 100,0:DRAW 190,0,3:DRAW 190,110,3:DRAW 100,110,3:DRAW 100,0,3
8023 FOR y=93 TO 96:MOVE 100,y:DRAW 190,y,3:NEXT y
8024 MOVE 220,140:DRAW 220,160,2:MOVE 222,138:DRAW 222,162,2:MOVE 224,138:DRAW 224,162,2:MOVE 226,140:DRAW 226,160,2
8025 MOVE 320,140:DRAW 320,160,2:MOVE 322,138:DRAW 322,162,2:MOVE 324,138:DRAW 324,162,2:MOVE 326,140:DRAW 326,160,2
8026 MOVE 220,156:DRAW 320,156,2:MOVE 220,144:DRAW 320,144,2
8029 CLS #2
8030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2
8040 PRINT#2,"Un message":PRINT#2,"Un coffre & un":PRINT#2,"Porte-serviette"
8050 IF cof8=1 AND linsal=1 THEN PRINT#2,"Du linge sale"
8060 GOSUB 30
8070 GOSUB 800
8090 IF act$="HAUT" OR act$="BAS" GOTO 8190
8100 IF act$="GAUCHE" GOTO 7000
8110 IF act$="DROITE" GOTO 9000
8120 GOSUB 220
8130 IF esp=0 OR ver=0 OR obj=0 GOTO 8070
8140 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Ranger le linge sec dans la lingerie.":GOTO 8070
8150 IF ver$="OUVR" AND obj$="COFF" AND cof8=1 THEN PRINT"Il est deja ouvert.":GOTO 8070
8160 IF ver$="OUVR" AND obj$="COFF" AND cof8=0 THEN PRINT"Il y a du linge sale a l'interieur.":cof8=1:GOTO 8029
8170 IF ver$="PREN" AND obj$="LING" AND cof8=1 AND linsal=1 AND poch$="" THEN PRINT"D'accord.":linsal=0:poch$="Linge sale":GOTO 8029
8180 IF ver$="ACCR" AND obj$="SERV" AND poch$="Serviette" THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 8029
8190 PRINT"Impossible !":GOTO 8070
9000 '** CHAMBRE DES SOEURS **
9005 BORDER 15:INK 0,15:INK 1,14:INK 2,0:INK 3,6
9010 CLS #1:CLS #2
9020 LOCATE #1,5,1:PRINT#1,"CHAMBRE DES SOEURS"
9021 GOSUB 430:GOSUB 420
9029 CLS #2
9030 PRINT#2,"Gauche":PRINT#2
9040 PRINT#2,"Un lit":PRINT#2,"Une armoire"
9050 GOSUB 30
9060 GOSUB 800
9080 IF act$="HAUT" OR act$="BAS" OR act$="DROITE" GOTO 9130
9090 IF act$="GAUCHE" GOTO 8000
9100 GOSUB 220
9110 IF esp=0 OR ver=0 OR obj=0 GOTO 9060
9120 IF ver$="FAIR" AND obj$="LIT" AND poch$="Draps" THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 9029
9128 IF ver$="OUVR" AND obj$="ARMO" AND arm9=1 THEN PRINT"Elle est deja ouverte.":GOTO 9060
9129 IF ver$="OUVR" AND obj$="ARMO" AND arm9=0 THEN PRINT"Elle est vide.":arm9=1:GOTO 9060
9130 PRINT"Impossible !":GOTO 9060
10000 '** CHAMBRE DE CENDRILLON **
10005 BORDER 14:INK 0,14:INK 1,16:INK 2,1:INK 3,25
10010 CLS #1:CLS #2:IF tps<1 THEN CLS:LOCATE 1,6
10020 LOCATE #1,3,1:PRINT#1,"CHAMBRE DE CENDRILLON"
10025 GOSUB 420:GOSUB 430
10030 PRINT#2,"Haut":PRINT#2,"Bas":PRINT#2:PRINT#2,"Un message":PRINT#2,"Un lit":PRINT#2,"une armoire"
10040 GOSUB 30
10045 IF t=0 THEN CLS:PRINT"Tu as termine ton travail,":PRINT"tu peux te preparer pour aller au bal.":fp=1:coef=10:GOTO 50000
10050 GOSUB 800
10070 IF act$="DROITE" OR act$="GAUCHE" GOTO 10150
10080 IF act$="HAUT" GOTO 5000
10090 IF act$="BAS" GOTO 16000
10100 GOSUB 220
10110 IF esp=0 OR ver=0 OR obj=0 GOTO 10050
10120 IF ver$="OUVR" AND obj$="ARMO" AND arm10=0 THEN PRINT"Elle est vide.":arm10=1:GOTO 10050
10130 IF ver$="OUVR" AND obj$="ARMO" AND arm10=1 THEN PRINT"Elle est deja ouverte.":GOTO 10050
10140 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Faire le lit des soeurs.":GOTO 10050
10150 PRINT"Impossible !":GOTO 10050
11000 '** JARDIN **
11005 BORDER 2:INK 0,2:INK 1,18:INK 2,0:INK 3,26
11010 CLS #1:CLS #2
11015 IF gr1=1 GOTO 11030
11020 tps=0:LOCATE #3,1,1:PRINT#3,"Temps :";tps:PRINT"Tu es venue voir ta belle-mere,":PRINT"ton temps recommence a 0."
11030 LOCATE #1,11,1:PRINT#1,"JARDIN"
11032 FOR x=10 TO 20 STEP 2:MOVE x,0:DRAW x,200,2:MOVE x+370,0:DRAW x+370,200,2:NEXT x:MOVE 20,200:DRAW 390,200,2
11039 CLS #2
11040 PRINT#2,"Droite":PRINT#2
11050 PRINT#2,"Un message"
11060 IF linsec<>1 GOTO 11070
11061 PRINT#2,"Du linge sec"
11062 FOR y=50 TO 198 STEP 2:MOVE 40,y:DRAW 280,y,3:NEXT y
11063 FOR y=100 TO 170 STEP 2:MOVE 300,y:DRAW 320,y,0:MOVE 330,y:DRAW 350,y,0:NEXT y:FOR y=172 TO 198 STEP 2:MOVE 300,y:DRAW 350,y,0:NEXT y:MOVE 326,175:DRAW 326,198,2
11070 GOSUB 30
11075 IF gr1=1 THEN CLS:PRINT"Cette porte donne dans le vide,":PRINT"tu tombes dans le jardin et":PRINT"tu te casses une jambe.":PRINT"Tu n'iras pas au bal ce soir.":GOTO 50000
11080 GOSUB 800
11090 tps=0:LOCATE #3,1,1:PRINT#3,"Temps :";tps
11110 IF act$="HAUT" OR act$="BAS" OR act$="GAUCHE" GOTO 11170
11120 IF act$="DROITE" GOTO 12000
11130 GOSUB 220
11140 IF esp=0 OR ver=0 OR obj=0 GOTO 11080
11150 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Accrocher une serviette dans":PRINT"la salle de bain.":GOTO 11080
11160 IF ver$="PREN" AND obj$="LING" AND linsec=1 AND poch$="" THEN PRINT"D'accord.":linsec=0:poch$="Linge sec":FOR y=50 TO 198 STEP 2:MOVE 40,y:DRAW 350,y,1:NEXT y:GOTO 11039
11170 PRINT"Impossible !":GOTO 11080
12000 '** ENTREE **
12005 BORDER 1:INK 0,1:INK 1,4:INK 2,26:INK 3,0
12010 CLS #1:CLS #2
12020 LOCATE #1,11,1:PRINT#1,"ENTREE"
12029 CLS #2
12030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2,"Haut":PRINT#2,"Bas":PRINT#2
12040 GOSUB 30
12045 IF ref=1 THEN CLS:PRINT"Tu as oublie de fermer le refrigerateur,":PRINT"tu es privee de bal.":GOTO 50000
12050 GOSUB 800
12070 IF act$="HAUT" GOTO 6000
12080 IF act$="BAS" GOTO 17000
12090 IF act$="DROITE" GOTO 13000
12100 IF act$="GAUCHE" AND ccleja=0 THEN PRINT"La porte est fermee a cle.":GOTO 12050
12110 IF act$="GAUCHE" AND ccleja=1 GOTO 11000
12120 GOSUB 220
12130 IF esp=0 OR ver=0 OR obj=0 GOTO 12050
12140 PRINT"Impossible !":GOTO 12050
13000 '** CUISINE **
13005 BORDER 16:INK 0,16:INK 1,14:INK 2,26:INK 3,0
13010 CLS #1:CLS #2
13020 LOCATE #1,10,1:PRINT#1,"CUISINE"
13021 FOR x=130 TO 260 STEP 2:MOVE x,0:DRAW x,220,2:NEXT x
13022 MOVE 130,0:DRAW 130,158,3:DRAW 260,158,3:DRAW 260,0,3:DRAW 130,0,3
13023 MOVE 130,162:DRAW 130,220,3:DRAW 260,220,3:DRAW 260,162,3:DRAW 130,162,3
13024 FOR x=140 TO 144:MOVE x,70:DRAW x,130,3:MOVE x,170:DRAW x,210,3:NEXT x
13029 CLS #2
13030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2,"Bas":PRINT#2
13040 PRINT#2,"refrigerateur"
13050 GOSUB 30
13060 GOSUB 800
13080 IF act$="HAUT" GOTO 13190
13090 IF act$="GAUCHE" GOTO 12000
13100 IF act$="DROITE" GOTO 14000
13110 IF act$="BAS" AND cclebu=0 THEN PRINT"La porte est fermee a cle.":GOTO 13060
13120 IF act$="BAS" AND cclebu=1 GOTO 18000
13130 GOSUB 220
13140 IF esp=0 OR ver=0 OR obj=0 GOTO 13060
13150 IF ver$="OUVR" AND obj$="REFR" AND ref=0 THEN PRINT"D'accord.":ref=1:GOTO 13060
13160 IF ver$="OUVR" AND obj$="REFR" AND ref=1 THEN PRINT"Il est deja ouvert.":GOTO 13060
13170 IF ver$="RANG" AND obj$="VIAN" AND ref=1 AND poch$="Viande" THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 13029
13180 IF ver$="RANG" AND obj$="VIAN" AND ref=0 AND poch$="Viande" THEN PRINT"Le refrigerateur est ferme.":GOTO 13060
13184 IF ver$="FERM" AND obj$="REFR" AND ref=0 THEN PRINT"Il est deja ferme.":GOTO 13060
13185 IF ver$="FERM" AND obj$="REFR" AND ref=1 THEN PRINT"D'accord.":ref=0:GOTO 13060
13190 PRINT"Impossible !":GOTO 13060
14000 '** SALLE A MANGER **
14005 BORDER 15:INK 0,15:INK 1,14:INK 2,1:INK 3,26
14010 CLS #1:CLS #2
14020 LOCATE #1,7,1:PRINT#1,"SALLE A MANGER"
14021 GOSUB 440
14022 FOR x=35 TO 40 STEP 2:MOVE x,0:DRAW x,140,2:MOVE x+60,0:DRAW x+60,60,2:MOVE x+265,0:DRAW x+265,60,2:MOVE x+325,0:DRAW x+325,140,2:NEXT x:FOR y=60 TO 65 STEP 2:MOVE 40,y:DRAW 100,y,3:MOVE 300,y:DRAW 360,y,3:NEXT y
14029 CLS #2
14030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2,"Bas":PRINT#2
14040 PRINT#2,"Une table":PRINT#2,"Des chaises"
14050 GOSUB 30
14055 IF ref=1 THEN CLS:PRINT"Tu as oublie de fermer le refrigerateur,":PRINT"tu es privee de bal.":GOTO 50000
14060 GOSUB 800
14080 IF act$="HAUT" GOTO 14160
14090 IF act$="GAUCHE" GOTO 13000
14100 IF act$="DROITE" GOTO 15000
14110 IF act$="BAS" GOTO 19000
14120 GOSUB 220
14130 IF esp=0 OR ver=0 OR obj=0 GOTO 14060
14140 IF ver$="PASS" AND obj$="ASPI" AND poch$="Aspirateur" AND aspdef=0 THEN CLS:PRINT"Cet aspirateur est defectueux,":PRINT"Il fait sauter les plombs.":PRINT"Tu es privee de bal.":GOTO 50000
14150 IF ver$="PASS" AND obj$="ASPI" AND poch$="Aspirateur" AND aspbon=0 THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 14029
14160 PRINT"Impossible !":GOTO 14060
15000 '** SALON **
15005 BORDER 14:INK 0,14:INK 1,25:INK 2,2:INK 3,3
15010 CLS #1:CLS #2
15020 LOCATE #1,11,1:PRINT#1,"SALON"
15021 FOR y=0 TO 30 STEP 2:MOVE 80,y:DRAW 320,y,2:NEXT y
15022 FOR i=1 TO 360 STEP 10:ORIGIN 90,40:DRAW COS(i)*10,SIN(i)*10:ORIGIN 310,40:DRAW COS(i)*10,SIN(i)*10:NEXT i:ORIGIN 0,0
15023 FOR y=21 TO 70 STEP 2:MOVE 106,y:DRAW 295,y,2:NEXT y
15024 MOVE 108,71:DRAW 293,71,2:MOVE 110,73:DRAW 291,73,2
15025 MOVE 80,0:DRAW 320,0,3:DRAW 320,30,3:DRAW 80,30,3:DRAW 80,0,3
15026 MOVE 106,30:DRAW 106,15,3:DRAW 295,15,3:DRAW 295,30,3:MOVE 160,15:DRAW 160,30,3:MOVE 240,15:DRAW 240,30,3
15027 MOVE 106,21:DRAW 106,70,3:DRAW 110,73,3:DRAW 291,73,3:DRAW 295,70,3:DRAW 295,21,3
15029 CLS #2
15030 PRINT#2,"Gauche":PRINT#2,"Droite":PRINT#2
15040 PRINT#2,"Un canape"
15050 IF bou=1 THEN PRINT#2,"Une bouillotte":MOVE 32,0:DRAW 48,0,3:FOR y=2 TO 20 STEP 2:MOVE 30,y:DRAW 50,y,3:NEXT y:MOVE 32,22:DRAW 48,22,3:MOVE 38,24:DRAW 42,24,3:MOVE 36,26:DRAW 44,26,3
15060 GOSUB 30
15070 GOSUB 800
15090 IF act$="HAUT" OR act$="BAS" GOTO 15150
15100 IF act$="GAUCHE" GOTO 14000
15110 IF act$="DROITE" GOTO 16000
15120 GOSUB 220
15130 IF esp=0 OR ver=0 OR obj=0 GOTO 15070
15140 IF ver$="PREN" AND obj$="BOUI" AND bou=1 AND poch$="" THEN PRINT"D'accord.":bou=0:poch$="Bouillotte":FOR y=0 TO 26 STEP 2:MOVE 30,y:DRAW 50,y,1:NEXT y:GOTO 15029
15150 PRINT"Impossible !":GOTO 15070
16000 '** BIBLIOTHEQUE **
16005 BORDER 14:INK 0,14:INK 1,22:INK 2,3:INK 3,16:p=1
16010 CLS #1:CLS #2
16020 LOCATE #1,8,1:PRINT#1,"BIBLIOTHEQUE"
16021 FOR y=0 TO 120 STEP 2:MOVE 180,y:DRAW 220,y,3:DRAW 340,y,0:DRAW 380,y,3:NEXT y:FOR y=121 TO 150 STEP 2:MOVE 160,y:DRAW 398,y,2:NEXT y
16022 MOVE 220,120:DRAW 220,0,2:DRAW 340,0,2:DRAW 340,120,2:MOVE 180,120:DRAW 180,0,2:DRAW 380,0,2:DRAW 380,120,2:MOVE 225,10:DRAW 335,10,2:MOVE 225,40:DRAW 335,40,2:FOR x=230 TO 330 STEP 9:MOVE x,0:DRAW x,50,2:NEXT x
16023 FOR x=10 TO 140 STEP 6:p=p+1:IF p=2 THEN p=3
16024 IF p=4 THEN p=0
16025 FOR i=x TO x+6 STEP 2:MOVE i,10:DRAW i,240,p:NEXT i:NEXT x
16026 FOR x=0 TO 10 STEP 2:MOVE x,0:DRAW x,240,2:MOVE x+140,0:DRAW x+140,240,2:NEXT x
16027 FOR y=0 TO 240 STEP 40:FOR i=y TO y+8 STEP 2:MOVE 0,i:DRAW 150,i,2:NEXT i:NEXT y
16029 CLS #2
16030 PRINT#2,"Gauche":PRINT#2,"Haut":PRINT#2,"Bas":PRINT#2
16040 PRINT#2,"Des livres":PRINT#2,"Une cheminee"
16050 GOSUB 30
16060 GOSUB 800
16080 IF act$="DROITE" GOTO 16150
16090 IF act$="HAUT" GOTO 10000
16100 IF act$="GAUCHE" GOTO 15000
16110 IF act$="BAS" GOTO 21000
16120 GOSUB 220
16130 IF esp=0 OR ver=0 OR obj=0 GOTO 16060
16140 IF ver$="FAIR" AND obj$="FEU" AND poch$="Bois" THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 16029
16145 IF ver$="LIRE" AND obj$="LIVR" THEN PRINT"Tu n'es pas la pour lire.":GOTO 16060
16150 PRINT"Impossible !":GOTO 16060
17000 '** CAVE #1 **
17005 BORDER 15:INK 0,15:INK 1,13:INK 2,3:INK 3,20
17010 CLS #1:CLS #2
17020 LOCATE #1,9,1:PRINT#1,"CAVE N. 1"
17030 PRINT#2,"HAUT":PRINT#2
17040 PRINT#2,"Un message"
17041 IF boi<>1 GOTO 17060
17042 FOR i=1 TO 360 STEP 30
17043 ORIGIN 140,10:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 170,10:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 200,11:DRAW COS(i)*11,SIN(i)*10,2:ORIGIN 230,11:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 260,11:DRAW COS(i)*11,SIN(i)*11,2
17044 ORIGIN 155,30:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 185,30:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 215,30:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 245,30:DRAW COS(i)*11,SIN(i)*11,2
17045 ORIGIN 170,50:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 200,50:DRAW COS(i)*11,SIN(i)*11,2:ORIGIN 230,50:DRAW COS(i)*11,SIN(i)*11,2
17057 NEXT i
17058 ORIGIN 0,0
17059 PRINT#2,"Du bois"
17060 GOSUB 30
17070 GOSUB 800
17090 IF act$="GAUCHE" OR act$="DROITE" OR act$="BAS" GOTO 17150
17100 IF act$="HAUT" GOTO 12000
17110 GOSUB 220
17120 IF esp=0 OR ver=0 OR obj=0 GOTO 17070
17130 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Ranger la bouillotte dans":PRINT"la chambre de la belle-mere.":GOTO 17070
17140 IF ver$="PREN" AND obj$="BOIS" AND boi=1 AND poch$="" THEN PRINT"D'accord.":boi=0:poch$="Bois":GOTO 17010
17150 PRINT"Impossible !":GOTO 17070
18000 '** BUANDERIE **
18005 BORDER 2:INK 0,2:INK 1,17:INK 2,0:INK 3,26
18010 CLS #1:CLS #2
18020 LOCATE #1,10,1:PRINT#1,"BUANDERIE"
18021 FOR x=150 TO 250 STEP 2:MOVE x,0:DRAW x,120,3:NEXT x
18022 MOVE 150,0:DRAW 250,0,2:DRAW 250,120,2:DRAW 150,120,2:DRAW 150,0,2:MOVE 150,100:DRAW 250,100,2
18023 MOVE 155,115:DRAW 165,115,0:MOVE 155,106:DRAW 170,106,0:FOR x=230 TO 240 STEP 2:MOVE x,105:DRAW x,115,0:NEXT x
18024 FOR i=1 TO 180 STEP 6:ORIGIN 200,50:MOVE COS(i)*20,SIN(i)*20:DRAW COS(i)*20,SIN(i)*-20,2:NEXT i:ORIGIN 0,0
18029 CLS #2
18030 PRINT#2,"Haut":PRINT#2
18040 PRINT#2,"Une machine a":PRINT#2,"laver"
18050 GOSUB 30
18055 IF ref=1 THEN CLS:PRINT"Tu as oublie de fermer le refrigerateur,":PRINT"tu es privee de bal.":GOTO 50000
18060 GOSUB 800
18080 IF act$="GAUCHE" OR act$="DROITE" OR act$="BAS" GOTO 18130
18090 IF act$="HAUT" GOTO 13000
18100 GOSUB 220
18110 IF esp=0 OR ver=0 OR obj=0 GOTO 18060
18120 IF ver$="LAVE" AND obj$="LING" AND poch$="Linge sale" THEN PRINT"D'accord.":poch$="":GOSUB 160:GOTO 18029
18130 PRINT"Impossible !":GOTO 18060
19000 '** CAVE #2 **
19005 BORDER 25:INK 0,25:INK 1,16:INK 2,1:INK 3,2
19010 CLS #1:CLS #2
19020 LOCATE #1,9,1:PRINT#1,"CAVE N. 2"
19030 PRINT#2,"Haut":PRINT#2,"Droite":PRINT#2
19040 PRINT#2,"Un message":IF aspbon=1 THEN PRINT#2,"Un aspirateur":GOSUB 450
19050 GOSUB 30
19055 IF con=1 THEN CLS:PRINT"Tu as oublie de fermer le congelateur,":PRINT"tu es privee de bal.":GOTO 50000
19060 GOSUB 800
19080 IF act$="GAUCHE" OR act$="BAS" GOTO 19150
19090 IF act$="HAUT" GOTO 14000
19100 IF act$="DROITE" GOTO 20000
19110 GOSUB 220
19120 IF esp=0 OR ver=0 OR obj=0 GOTO 19060
19130 IF ver$="LIRE" AND obj$="MESS" THEN PRINT"Laver le linge sale.":GOTO 19060
19140 IF ver$="PREN" AND obj$="ASPI" AND poch$="" THEN PRINT"D'accord.":aspbon=0:poch$="Aspirateur":GOTO 19010
19150 PRINT"Impossible !":GOTO 19060
20000 '** CAVE #3 **
20005 BORDER 1:INK 0,1:INK 2,26:INK 3,0
20010 CLS #1:CLS #2
20020 LOCATE #1,9,1:PRINT#1,"CAVE N. 3"
20030 CLS #2
20040 PRINT#2,"Gauche":PRINT#2
20050 IF clam<>1 THEN INK 1,0:PRINT#2,"Il fait noir.":GOTO 20080
20051 INK 1,16
20052 FOR y=0 TO 100 STEP 2:MOVE 100,y:DRAW 300,y,2:NEXT y
20053 MOVE 100,0:DRAW 300,0,3:DRAW 300,80,3:DRAW 100,80,3:DRAW 100,0,3
20054 MOVE 100,85:DRAW 300,85,3:DRAW 300,100,3:DRAW 100,100,3:DRAW 100,85,3
20055 FOR x=117 TO 130 STEP 4:PLOT x,92,3:NEXT x
20060 PRINT#2,"Un congelateur"
20070 IF con=1 AND via=1 THEN PRINT#2,"De la viande"
20080 GOSUB 30
20090 GOSUB 800
20110 IF act$="DROITE" OR act$="BAS" OR act$="HAUT" GOTO 20220
20120 IF act$="GAUCHE" GOTO 19000
20130 GOSUB 220
20140 IF esp=0 OR ver=0 OR obj=0 GOTO 20090
20150 IF ver$="ALLU" AND obj$="LAMP" AND clam=2 THEN PRINT"D'accord.":clam=1:GOTO 20030
20160 IF ver$="ALLU" AND obj$="LAMP" AND clam=1 THEN PRINT"Elle est deja allumee.":GOTO 20090
20170 IF ver$="ALLU" AND obj$="LAMP" AND clam=0 GOTO 20220
20180 IF clam<>1 GOTO 20220
20190 IF ver$="OUVR" AND obj$="CONG" AND con=0 THEN PRINT"Il y a de la viande a l'interieur.":con=1:GOTO 20030
20200 IF ver$="OUVR" AND obj$="CONG" AND con=1 THEN PRINT"Il est deja ouvert.":GOTO 20090
20210 IF ver$="PREN" AND obj$="VIAN" AND con=1 AND via=1 THEN PRINT"D'accord.":via=0:poch$="Viande":GOTO 20030
20214 IF ver$="FERM" AND obj$="CONG" AND con=0 THEN PRINT"Il est deja ferme.":GOTO 20090
20215 IF ver$="FERM" AND obj$="CONG" AND con=1 THEN PRINT"D'accord.":con=0:GOTO 20090
20220 PRINT"Impossible !":GOTO 20090
21000 '** CAVE #4 **
21005 BORDER 2:INK 0,2:INK 1,16:INK 2,26:INK 3,9
21010 CLS:CLS #1:CLS #2
21020 LOCATE #1,9,1:PRINT#1,"CAVE N. 4"
21025 GOSUB 30
21030 MOVE 202,0:DRAW 240,0,3:MOVE 200,2:DRAW 240,2,3:MOVE 200,4:DRAW 240,4,3
21031 MOVE 188,6:DRAW 240,6,3:MOVE 188,8:DRAW 240,8,3
21032 MOVE 200,10:DRAW 240,10,3:MOVE 200,12:DRAW 240,12,3:MOVE 202,14:DRAW 240,14,3
21040 PRINT"Ton pied roule sur une bouteille":PRINT"et tu te casses un bras.":PRINT"Tu ne pourras pas aller au bal ce soir.":GOTO 50000
30000 '** DATAS VOCABULAIRE **
30010 DATA LIRE,OUVR,PREN,FAIR,ACCR,RANG,PASS,POSE,LAVE,ALLU,FERM,GAUC,DROI,BAS,HAUT
30020 DATA BIBL,PANT,MESS,COFF,CLE,ASPI,LAMP,ARMO,DRAP,SERV,LIT,LING,VIAN,BOIS,REFR,CONG,TABL,BOUI,PORT,CHAI,CANA,LIVR,CHEM,FEU,GAUC,DROI,BAS,HAUT
40000 MODE 2:WINDOW #0,1,80,1,25:BORDER 0:INK 0,0:PAPER 0:INK 1,0:PEN 1
40010 PRINT:PRINT:PRINT"     Tu es Cendrillon et tu veux aller danser. Tu n'auras l'autorisation d'y    aller que lorsque tu auras termine tout ton travail."
40020 PRINT:PRINT:PRINT"     Tu sauras ce que tu dois faire en lisant les messages dissemines dans la   maison."
40030 PRINT:PRINT:PRINT"     En outre, tu dois aller voir ta belle-mere dans le jardin au moins une foistoutes les heures, sous peine d'etre privee de bal.
40035 PRINT:PRINT:PRINT:PRINT"     Tu peux te deplacer en indiquant seulement : gauche, droite, haut et bas."
40040 PRINT:PRINT:PRINT"     Par contre, tes autres actions doivent se composer de deux mots : un verbe a l'infinitif et le nom auquel il s'applique."
40050 PRINT:PRINT:PRINT:PRINT:PRINT TAB(28) "Presse ENTER pour jouer...";
40060 bp=0
40500 IF INKEY$<>"" GOTO 40500
40510 FOR t=1 TO 0.5 STEP -0.1
40511 bp=bp+1
40512 WHILE SQ(1)>127:WEND
40513 IF bp=1 THEN BORDER 4:INK 0,4:INK 1,26
40514 IF bp=2 THEN BORDER 25:INK 0,25:INK 1,0
40515 IF bp=3 THEN BORDER 26:INK 0,26:INK 1,6
40516 IF bp=4 THEN BORDER 16:INK 0,16:INK 1,3
40517 IF bp=5 THEN BORDER 14:INK 0,14:INK 1,1
40520 RESTORE 60000
40530 FOR i=1 TO 114
40540 READ so:so=so*t:READ d:d=d*12:READ sob:sob=sob*t
40550 SOUND 4,so,d,15,2:SOUND 1,so/2,d,15,2:SOUND 2,sob,d,13,1
40560 IF INKEY$=CHR$(13) THEN i=114:t=0.5:brk=1
40570 NEXT i,t
40580 IF brk=0 GOTO 40060
40590 FOR i=1 TO 8:CALL &A000:NEXT i:RETURN
50000 '** FIN DE PARTIE **
50005 AFTER 1,3 GOSUB 120
50007 GOSUB 63000
50010 LOCATE 1,6:PRINT"Presse ENTER, si tu veux rejouer...";
50020 IF INKEY$<>"" GOTO 50020
50030 IF INKEY$<>CHR$(13) GOTO 50030
50040 CLEAR:FOR i=1 TO 8:CALL &A000:NEXT i:GOTO 1
60000 '** MUSIQUE PRESENTATION **
60010 DATA 284,2,568,190,2,568,213,2,568,190,1,568,284,2,568,284,1,568,190,2,568,213,2,568,190,2,568
60020 DATA 239,2,478,190,2,478,213,2,478,190,1,478,239,2,478,239,1,478,190,2,478,213,2,478,190,2,478
60030 DATA 319,2,638,213,2,638,239,2,638,213,1,638,319,2,638,319,1,638,213,2,638,239,2,638,213,2,638
60040 DATA 319,2,638,213,2,638,239,2,638,213,1,638,319,2,638,319,1,638,213,2,638,239,2,638,213,2,638
60050 DATA 284,2,568,190,2,568,213,2,568,190,1,568,284,2,568,284,1,568,190,2,568,213,2,568,190,2,568
60060 DATA 239,2,478,190,2,478,213,2,478,190,1,478,239,2,478,239,1,478,190,2,478,213,2,478,190,2,478
60070 DATA 319,2,638,213,2,638,239,2,638,213,1,638,319,2,638,319,1,638,213,2,638,239,2,638,213,2,638
60080 DATA 319,2,638,213,2,638,239,2,638,213,1,638,319,2,638,319,1,638,213,2,638,239,2,638,213,2,638
60090 DATA 119,2,568,127,2,568,142,2,568,159,1,568,142,6,568
60100 DATA 159,1,568,142,1,568,159,1,568,119,2,478,127,2,478,142,2,478,159,1,478,142,6,478
60110 DATA 159,1,478,142,1,478,159,1,478,119,2,638,127,2,638,142,2,638,159,1,638,213,8,638
60120 DATA 119,2,568,127,2,568,142,2,568,159,1,568,142,6,568
60130 DATA 159,1,568,142,1,568,159,1,568,119,2,478,127,2,478,142,2,478,159,1,478,142,6,478
60140 DATA 159,1,478,142,1,478,159,1,478,119,2,638,127,2,638,142,2,638,159,1,638,213,8,638
61000 '** MUSIQUE GAGNE **
61010 DATA 239,2,213,4,426,2,426,2,358,2,284,2,239,2,284,2,239,2,284,4
62000 '** MUSIQUE PERDU **
62010 DATA 478,2,358,2,319,2,284,4,284,4,284,2,319,2,284,2,268,2,284,4
63000 '** MUSIQUE FIN DE PARTIE **
63010 IF fp=0 THEN RESTORE 62000 ELSE RESTORE 61000
63020 FOR i=1 TO 10
63030 READ so:READ d:d=d*coef
63040 SOUND 1,so,d,15,1:SOUND 2,so/3,d,13,2
63050 NEXT i
63060 RETURN
65000 MEMORY &9FFF:RESTORE 65090
65010 oct=&9FFF
65020 oct=oct+1
65030 READ pok$
65040 pok=VAL("&"+pok$)
65050 POKE oct,pok
65060 IF pok$="c9" GOTO 65080
65070 GOTO 65020
65080 RETURN
65090 DATA 97,21,00,c0,cb,3e,23,bc,20,fa,c9