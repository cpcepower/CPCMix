100 '    *****************
110 '   *                 *
120 '   * S H O O T I N G *
130 '   *                 *
140 '   *   (C) 1987 BY   *   
150 '   *                 *
160 '   *  MICHAEL KLEIN  *  
170 '   *                 *  
180 '    *****************
190 '
200 '   *** INIT 1 ***
210 '
220 RESTORE 3010:FOR BES=1 TO 10:READ SCR(BES),NAM$(BES):NEXT:TOP=SCR(1)
230 DIM REP$(34,12),TON(40):RESTORE 2970:POKE &A604,0
240 FOR REC=1 TO 33:READ XPO,YPO,SYM$:REP$(XPO,YPO)=SYM$:NEXT
250 PLA=1:NUM=1:ANZ=0:LIM=750
260 ENV 1,100,-2,6
270 '
280 '   *** TITLE ***
290 '
300 FOR SCR=25 TO 1 STEP-1:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT:MODE 1
310 PEN 1:PAPER 0:BORDER 0:CLS
320 INK 0,0:INK 1,26:INK 2,11:INK 3,9
330 LOCATE 12,1:PRINT"MCSOFTY PRESENT":LOCATE 5,25:PRINT"COPYRIGHT 1987 BY MICHAEL KLEIN"
340 PLOT 0,30,1:DRAW 622,30:PLOT 0,368:DRAW 622,368
350 WINDOW#1,1,39,3,8:PAPER#1,2:CLS#1
360 WINDOW#2,1,39,9,14:PAPER#2,3:CLS#2
370 PLOT 0,174,1:DRAW 622,174
380 PAPER 2:PEN 0:LOCATE 2,3:PRINT"acccccccccccccccccccccccccccccccccccb":PAPER 0:PEN 1
390 LOCATE 2,4:PRINT STRING$(37,32):LOCATE 2,10:PRINT STRING$(37,32);
400 LOCATE 2,5:PRINT" accb d  d accb accb acb d accb accb ";
410 LOCATE 2,6:PRINT" c    c  c c  c c  c  c  c c  c c    ";
420 LOCATE 2,7:PRINT" fccb cccc c  c c  c  c  c c  c c fb ";
430 LOCATE 2,8:PRINT"    c c  c c  c c  c  c  c c  c c  c ";
440 LOCATE 2,9:PRINT" fccg e  e fccg fccg  e  e e  e fccg ";
450 LOCATE 11,11:PRINT"  ":LOCATE 28,11:PRINT"  ":LOCATE 8,14:PRINT"        ":LOCATE 25,14:PRINT"        "
460 PAPER 3:PEN 0:LOCATE 9,12:PRINT"accccb":LOCATE 26,12:PRINT"accccb"
470 LOCATE 8,13:PRINT"accccccb":LOCATE 25,13:PRINT"accccccb":PEN 1:PAPER 0
480 FOR BUL=2 TO 36 STEP 34
490 PEN 2:PAPER 0:LOCATE BUL,16:PRINT"pqr":LOCATE BUL,17:PRINT"hji":LOCATE BUL,18:PRINT CHR$(138)CHR$(143)CHR$(133):LOCATE BUL,19:PRINT"kcl":PAPER 2:PEN 1:LOCATE BUL,20:PRINT"mno":PAPER 0:PEN 2:LOCATE BUL,21:PRINT"jjj":LOCATE BUL,22:PRINT"jjj"
500 LOCATE BUL+1,23:PRINT CHR$(208):NEXT BUL
510 PLOT 32,368:DRAW 16,352:DRAW 606,352:DRAW 590,368:PLOT 606,352:DRAW 606,240:DRAW 16,240:DRAW 16,352:PLOT 160,240:DRAW 160,206:DRAW 190,206:DRAW 190,240
520 PLOT 112,174,1:DRAW 112,192:DRAW 239,192:DRAW 239,174:PLOT 112,192:DRAW 142,222:DRAW 159,222:PLOT 192,222:DRAW 208,222:DRAW 239,192
530 PLOT 432,240,1:DRAW 432,206:DRAW 462,206:DRAW 462,240:PLOT 385,174:DRAW 385,192:DRAW 510,192:DRAW 510,174:PLOT 385,192:DRAW 415,222:DRAW 432,222:PLOT 462,222:DRAW 480,222:DRAW 510,192
540 PAPER 3:PEN 1:LOCATE 19,12:PRINT"TOP":LOCATE 18,13:PRINT USING"**###";TOP:PAPER 0:PLOT 10,174,1:DRAW 77,240:PLOT 614,174:DRAW 548,240
550 LOCATE 7,16:PRINT"SELECT NUMBER OF PLAYERS #";:PRINT USING"#";PLA
560 LOCATE 8,18:PRINT"START SHOOTING CONTESTANT"
570 LOCATE 9,20:PRINT"SEE WORLD BEST SHOOTERS"
580 LOCATE 10,22:PRINT"FINISH THE >SHOOTING<"
590 FOR SCR=1 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT:GOSUB 2690
600 IF INKEY(64)=0 THEN PLA=PLA+1:AEN=1:IF PLA>4 THEN PLA=1
610 IF AEN=1 THEN AEN=0:LOCATE 33,16:PRINT USING"#";PLA:SOUND 1,200,20,15,1:FOR WAI=1 TO 200:NEXT
620 IF INKEY(65)=0 THEN NUM=1:SCO=0:GOTO 1670
630 IF INKEY(57)=0 THEN SEE=1:GOTO 1820
640 IF INKEY(56)=0 THEN POKE &BB5A,&C7:GOTO 100
650 GOTO 600
660 '
670 '   *** INIT 2 *** 
680 '
690 ROU=1:BUL=9:TLV=70:PUN=0
700 BEL=200:BER=440:BZL=100:BZR=540
710 FKX=166:FKY=180:TRF=0
720 SHR=8:STE=0:SHO=0
730 PLOT 1000,1000,1
740 XPO=0:TEY=160:TX(0)=175:TX(1)=207:TX(2)=239:TX(3)=271:TX(4)=303:TX(5)=335:TX(6)=367:TX(7)=399:TX(8)=431:TX(9)=463
750 '
760 '  *** SHOOTING GRAPIC ***
770 '  
780 FOR SCR=25 TO 1 STEP-1:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
790 MODE 1:PAPER 0:PEN 1:CLS
800 IF PEEK(&A604)<>255 THEN POKE &A604,255:GOTO 810 ELSE CALL &62BB:GOSUB 2580:FOR SCR=1 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT:GOTO 1100
810 LOCATE 4,1:PRINT"SHOOTING (C) 1987 BY MICHAEL KLEIN"
820 LOCATE 4,25:PRINT"UP SCORE       ROUNDS    BULLETS"
830 GOSUB 2580
840 WINDOW#1,1,40,3,14:PAPER#1,2:CLS#1
850 WINDOW#2,1,40,15,23:PAPER#2,3:CLS#2
860 PLOT 0,30,1:DRAW 640,30:PLOT 0,368:DRAW 640,368:PLOT 0,174,0:DRAW 640,174
870 FOR BRG=0 TO 100:PLOT BEL,176,3:DRAW BEL,176+STE:PLOT BER,176:DRAW BER,176+STE:STE=STE+INT(RND*3):BEL=BEL-2:BER=BER+2:NEXT:STE=0
880 FOR BRG=0 TO 110:PLOT BZL,176,3:DRAW BZL,176+STE:PLOT BZR,176:DRAW BZR,176+STE:STE=STE+INT(RND*3.5):BZL=BZL+2:BZR=BZR-2:NEXT
890 PAPER 2:PEN 1:LOCATE 2,5:PRINT"st":LOCATE 7,10:PRINT"st":LOCATE 33,9:PRINT"st":LOCATE 15,4:PRINT"st":LOCATE 27,6:PRINT"st":LOCATE 37,4:PRINT"st"
900 FOR ZAU=1 TO 40:LOCATE ZAU,14:PRINT CHR$(22);CHR$(1);CHR$(15);CHR$(1);CHR$(117);CHR$(8);CHR$(15);CHR$(0);CHR$(118);CHR$(22);CHR$(0);:NEXT:PEN 1
910 LOK=-17:FOR SCS=1 TO 2:LOK=LOK+21:FOR STA=LOK TO LOK+10 STEP 5
920 PAPER 3:PEN 0:LOCATE STA,13:PRINT CHR$(142)CHR$(143)CHR$(141):LOCATE STA,14:PRINT STRING$(3,143):LOCATE STA,15:PRINT CHR$(139)CHR$(143)CHR$(135):LOCATE STA+1,16:PRINT CHR$(149)
930 PAPER 1:PEN 0:LOCATE STA+1,17:PRINT CHR$(145):PAPER 3:PEN 1:LOCATE STA,17:PRINT CHR$(214):LOCATE STA+2,17:PRINT CHR$(215):LOCATE STA,18:PRINT STRING$(3,131):PEN 0:LOCATE STA+1,19:PRINT CHR$(231)
940 NEXT:NEXT:PEN 0:LOCATE 19,13:PRINT CHR$(142)CHR$(143)CHR$(143)CHR$(141):LOCATE 19,15:PRINT CHR$(139)CHR$(143)CHR$(143)CHR$(135):PEN 1:PAPER 0:LOCATE 19,14:PRINT"wxyz":PAPER 3:PEN 0:LOCATE 20,16:PRINT CHR$(149)CHR$(149)
950 PAPER 1:LOCATE 20,17:PRINT CHR$(145)CHR$(145):PAPER 3:PEN 1:LOCATE 19,17:PRINT CHR$(214):LOCATE 22,17:PRINT CHR$(215):LOCATE 19,18:PRINT STRING$(4,131)
960 RESTORE 3030:FOR SHE=1 TO 6:READ XPO,ZXP,LOC(SHE):TRY(SHE)=184:TRX(SHE)=XPO
970 FOR KRE=ZXP TO XPO+1 STEP-5.85:KZX=XPO:KZY=184:KRX=KRE:KRY=184
980 RAS=SQR((KRX-KZX)^2+(KRY-KZY)^2):ORIGIN KZX,KZY
990 DEP=PI/RAS:KRX=RAS:KRY=0:MOVER KRX,KRY:FOR KRS=1 TO RAS+2
1000 KRX=KRX-KRY*DEP:KRY=KRY+KRX*DEP:DRAW KRX,KRY,1
1010 KRY=KRY+KRX*DEP:KRX=KRX-KRY*DEP:DRAW KRX,KRY,1
1020 NEXT:NEXT:NEXT
1030 ORIGIN 0,0:FOR SHE=1 TO 6:PLOT TRX(SHE),TRY(SHE),1:NEXT
1040 FOR SCR=1 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
1050 PLX=320:PLY=60:SHE=0:TIM=0:GOSUB 1500
1060 PLOT PLX-4,PLY,2:PLOT PLX-2,PLY:PLOT PLX+3,PLY:PLOT PLX+5,PLY:PLOT PLX,PLY-4:PLOT PLX,PLY-2:PLOT PLX,PLY+2:PLOT PLX,PLY+4:CALL &62C7
1070 '
1080 ' *** MAIN PART ***
1090 '
1100 IF SHE=0 THEN GOSUB 1230
1110 IF SHE>0 THEN TIM=TIM+1:IF TIM>=TLV THEN SOT=0:GOTO 1330
1120 ALX=PLX:ALY=PLY
1130 IF SHE<4 THEN RIX=-8 ELSE RIX=8
1140 IF(JOY(0)AND 4)=4 THEN IF PLX>4 AND PLX<636 THEN PLX=PLX+RIX:BEW=1
1150 IF(JOY(0)AND 8)=8 THEN IF PLX<636 AND PLX>4 THEN PLX=PLX+RIX:BEW=1
1160 IF(JOY(0)AND 1)=1 THEN IF PLY<210 THEN PLY=PLY+4:BEW=1
1170 IF(JOY(0)AND 16)=16 AND SOT=0 THEN GOSUB 1410:SOT=1
1180 IF BEW=1 THEN PLOT ALX-4,ALY,PL1:PLOT ALX-2,ALY,PL2:PLOT ALX+3,ALY,PL3:PLOT ALX+5,ALY,PL4:PLOT ALX,ALY-4,PL5:PLOT ALX,ALY-2,PL6:PLOT ALX,ALY+2,PL7:PLOT ALX,ALY+4,PL8
1190 IF BEW=1 THEN GOSUB 1500:PLOT PLX-4,PLY,2:PLOT PLX-2,PLY:PLOT PLX+3,PLY:PLOT PLX+5,PLY:PLOT PLX,PLY-4:PLOT PLX,PLY-2:PLOT PLX,PLY+2:PLOT PLX,PLY+4:BEW=0
1200 IF SOT=1 THEN TIM=100:SCO1=1:GOSUB 2590
1210 IF GAO=1 AND SHE=0 THEN 1560
1220 GOTO 1100
1230 '
1240 ' *** TARGET ***
1250 '
1260 SHE=INT(RND*8):IF SHE>6 OR SHE<1 THEN 1260
1270 SOUND 1,100,30,13,1
1280 PAPER 3:PEN 1:LOCATE LOC(SHE),19:PRINT CHR$(231);:PAPER 0
1290 RETURN
1300 '
1310 ' *** TIME ***
1320 '
1330 PAPER 3:PEN 0:LOCATE LOC(SHE),19:PRINT CHR$(231);:PEN 1
1340 SXP=SHE:SHE=0:PLOT PLX-4,PLY,PL1:PLOT PLX-2,PLY,PL2:PLOT PLX+3,PLY,PL3:PLOT PLX+5,PLY,PL4:PLOT PLX,PLY-4,PL5:PLOT PLX,PLY-2,PL6:PLOT PLX,PLY+2,PL7:PLOT PLX,PLY+4,PL8
1350 PLX=320:PLY=60:GOSUB 1500:PLOT PLX-4,PLY,2:PLOT PLX-2,PLY:PLOT PLX+3,PLY:PLOT PLX+5,PLY:PLOT PLX,PLY-4:PLOT PLX,PLY-2:PLOT PLX,PLY+2:PLOT PLX,PLY+4
1360 BUL=BUL-1:GOSUB 2640
1370 TIM=0:FOR WAI=1 TO 300:NEXT:GOTO 1210
1380 '
1390 ' *** HIT OR FAIL ***
1400 '
1410 IF PLX>TRX(SHE)-20 AND PLY>TRY(SHE)-20 AND PLX<TRX(SHE)+20 AND PLY<TRY(SHE)+20 THEN SCO=SCO+25:PUN=PUN+25
1420 IF PLX>TRX(SHE)-15 AND PLY>TRY(SHE)-15 AND PLX<TRX(SHE)+15 AND PLY<TRY(SHE)+15 THEN SCO=SCO+25:PUN=PUN+25
1430 IF PLX>TRX(SHE)-10 AND PLY>TRY(SHE)-10 AND PLX<TRX(SHE)+10 AND PLY<TRY(SHE)+10 THEN SCO=SCO+25:PUN=PUN+25
1440 IF PLX>TRX(SHE)-5 AND PLY>TRY(SHE)-5 AND PLX<TRX(SHE)+5 AND PLY<TRY(SHE)+5 THEN SCO=SCO+25:PUN=PUN+25
1450 IF PLX>TRX(SHE)-2 AND PLY>TRY(SHE)-2 AND PLX<TRX(SHE)+2 AND PLY<TRY(SHE)+2 THEN SCO=SCO+150:PUN=PUN+150
1460 SOUND 4,250,35,15,1,0,8:RETURN
1470 '
1480 ' *** TESTS PLAYER ***
1490 ' 
1500 PL1=TEST(PLX-4,PLY):PL2=TEST(PLX-2,PLY):PL3=TEST(PLX+3,PLY):PL4=TEST(PLX+5,PLY)
1510 PL5=TEST(PLX,PLY-4):PL6=TEST(PLX,PLY-2):PL7=TEST(PLX,PLY+2):PL8=TEST(PLX,PLY+4)
1520 RETURN
1530 '
1540 ' *** NEXT ROUND ***
1550 '
1560 GAO=0:IF PUN<LIM THEN 1640
1570 ROU=ROU+1:BUL=9:TLV=TLV-10:PUN=0
1580 FOR SCR=25 TO 1 STEP-1:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
1590 SHE=0:TIM=0:GOSUB 2580
1600 FOR SCR=1 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT:GOTO 1100
1610 '
1620 '  *** NEXT PLAYER ***
1630 ' 
1640 IF NUM<PLA THEN 1660
1650 SCO(NUM)=SCO:GAO=2:GOSUB 2220:GOSUB 1690:IF PLA>1 THEN GOTO 2300 ELSE 270
1660 SCO(NUM)=SCO:SCO=0:GAO=0:GOSUB 1690:NUM=NUM+1
1670 CLS:PAPER 0:PEN 1:LOCATE 11,13:PRINT"GET READY PLAYER #";:PRINT USING"#";NUM
1680 FOR WAI=1 TO 2000:NEXT:GOTO 690
1690 '
1700 '  *** TOP TEN ***
1710 '
1720 IF SCO(NUM)<SCR(10)THEN RETURN
1730 FOR CHE=1 TO 9
1740 IF SCR(CHE)>SCO(NUM)THEN 1780
1750 FOR PUS=10 TO CHE+1 STEP-1
1760 SCR(PUS)=SCR(PUS-1):NAM$(PUS)=NAM$(PUS-1):NEXT
1770 ZAL=CHE:GOTO 1820
1780 NEXT:ZAL=10
1790 '
1800 '  *** GRAPHIC TOP TEN ***
1810 '
1820 FOR SCR=25 TO 0 STEP-1:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
1830 MODE 1:PAPER 0:PEN 2:CLS
1840 RESTORE 2900:XPO=2:YPO=1
1850 READ SYM:LOCATE XPO,YPO:PRINT CHR$(SYM);
1860 XPO=XPO+1:IF XPO=39 THEN XPO=2:YPO=YPO+1:IF YPO=4 THEN 1880
1870 GOTO 1850
1880 PEN 1:PLOT 0,342,1:DRAW 616,342
1890 LOCATE 5,6:PRINT"A  B  C  D  E  F  G  H  I  J  K";:LOCATE 5,9:PRINT"L  M  N  O  P  Q  R  S  T  U  V";:LOCATE 5,12:PRINT"W  X  Y  Z  [  ]  -  .  {  |  }";
1900 IF SEE=1 THEN PEN 2:LOCATE 10,14:PRINT"CONTINUE WITH BUTTON.":PEN 1:GOTO 1930
1910 LOCATE 17,14:PRINT">":LOCATE 18,15:PRINT"------"
1920 SCR(ZAL)=SCO(NUM):NAM$(ZAL)=""
1930 YPO=17:FOR PRI=1 TO 5
1940 LOCATE 4,YPO:PRINT USING"#";PRI;:PRINT". ";:PRINT"------";:LOCATE 7,YPO:PRINT USING"&";NAM$(PRI);:LOCATE 13,YPO:PRINT" ";:PRINT USING"**###";SCR(PRI):YPO=YPO+2:NEXT
1950 YPO=17:FOR PRI=6 TO 10
1960 LOCATE 21,YPO:PRINT USING"##";PRI;:PRINT". ";:PRINT"------";:LOCATE 25,YPO:PRINT USING"&";NAM$(PRI);:LOCATE 31,YPO:PRINT" ";:PRINT USING"**###";SCR(PRI):YPO=YPO+2:NEXT
1970 IF FIN=1 THEN PEN 2:LOCATE 10,14:PRINT"CONTINUE WITH BUTTON.":PEN 1:LOCATE 18,15:PRINT"      ":WHILE JOY(0)<>16:WEND:FIN=0:RETURN
1980 IF SEE=1 THEN FOR SCR=0 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
1990 IF SEE=1 THEN SEE=0:WHILE JOY(0)<>16:WEND:GOTO 290
2000 FOR SCR=0 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
2010 REX=4:REY=4:BSA=-1:NAM$="":LOCATE 18,14:PRINT"      ";
2020 RPX=REX*16-8:RPY=(25-REY)*16-8
2030 ROX=RPX:ROY=RPY:PLOT RPX,RPY,1:DRAW RPX+28,RPY:DRAW RPX+28,RPY-32:DRAW RPX,RPY-32:DRAW RPX,RPY
2040 FOR WAI=1 TO 100:NEXT
2050 IF JOY(0)=1 AND REY>5 THEN REY=REY-3:BEW=1
2060 IF JOY(0)=2 AND REY<10 THEN REY=REY+3:BEW=1
2070 IF JOY(0)=4 AND REX>4 THEN REX=REX-3:BEW=1
2080 IF JOY(0)=8 AND REX<34 THEN REX=REX+3:BEW=1
2090 IF JOY(0)=16 THEN BEW=2
2100 IF BEW>0 THEN 2110 ELSE 2050
2110 PLOT ROX,ROY,0:DRAW ROX+28,ROY:DRAW ROX+28,ROY-32:DRAW ROX,ROY-32:DRAW ROX,ROY
2120 IF BEW<2 THEN BEW=0:GOTO 2020 ELSE BEW=0:GOTO 2130
2130 IF LEN(NAM$)<6 THEN 2140 ELSE 2180
2140 IF REP$(REX,REY+1)<>"<"THEN 2150 ELSE 2180
2150 IF REP$(REX,REY+1)="#"THEN NAM$=NAM$+" ":BSA=BSA+1:GOTO 2180
2160 IF REP$(REX,REY+1)="="THEN NAM$(ZAL)=NAM$:FIN=1:GOTO 1930
2170 NAM$=NAM$+REP$(REX,REY+1):BSA=BSA+1
2180 IF REP$(REX,REY+1)="<"THEN NAM$=LEFT$(NAM$,BSA):BSA=BSA-1
2190 IF REP$(REX,REY+1)="="THEN NAM$(ZAL)=NAM$:FIN=1:GOTO 1930
2200 SOUND 1,300,20,15,1:FOR WAI=1 TO 250:NEXT
2210 LOCATE 18,14:PRINT"      ";:LOCATE 18,14:PRINT USING"&";NAM$:GOTO 2020
2220 '
2230 '  *** GAME OVER ***
2240 '  
2250 WINDOW#4,11,30,12,14:PAPER#4,0:CLS#4
2260 LOCATE 13,13:PEN 1:PAPER 0:PRINT"G A M E  O V E R"
2270 PLOT 160,222,1:DRAW 478,222:DRAW 478,176:DRAW 160,176:DRAW 160,222
2280 FOR WAI=1 TO 2000:NEXT
2290 IF GAO=2 THEN GAO=0:RETURN
2300 '
2310 '  *** END PICTURE ***
2320 ' 
2330 FOR SCR=25 TO 1 STEP-1:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
2340 MODE 1:CLS
2350 LOCATE 5,1:PRINT" HERE ARE THE FINAL STANDINGS :";
2360 LOCATE 4,25:PRINT"SHOOTING (C) 1987 BY MICHAEL KLEIN";
2370 PLOT 0,30,1:DRAW 640,30:PLOT 0,362:DRAW 640,362
2380 SC(1)=SCO(1):SC(2)=SCO(2):SC(3)=SCO(3):SC(4)=SCO(4)
2390 FOR AUS=1 TO PLA
2400 NUM(AUS)=MAX(SC(1),SC(2),SC(3),SC(4))
2410 ANZ=ANZ+1
2420 IF NUM(AUS)=SCO(1)THEN RAN(1)=ANZ:SC(1)=0
2430 IF NUM(AUS)=SCO(2)THEN RAN(2)=ANZ:SC(2)=0
2440 IF NUM(AUS)=SCO(3)THEN RAN(3)=ANZ:SC(3)=0
2450 IF NUM(AUS)=SCO(4)THEN RAN(4)=ANZ:SC(4)=0
2460 NEXT
2470 LOCATE 3,7:PRINT"1UP GETS ";:PRINT USING"**###";SCO(1);:PRINT" POINTS.THIS IS RANK ";:PRINT USING"#";RAN(1);
2480 LOCATE 3,10:PRINT"2UP GETS ";:PRINT USING"**###";SCO(2);:PRINT" POINTS.THIS IS RANK ";:PRINT USING"#";RAN(2);
2490 LOCATE 3,13:PRINT"3UP GETS ";:PRINT USING"**###";SCO(3);:PRINT" POINTS.THIS IS RANK ";:PRINT USING"#";RAN(3);
2500 LOCATE 3,16:PRINT"4UP GETS ";:PRINT USING"**###";SCO(4);:PRINT" POINTS.THIS IS RANK ";:PRINT USING"#";RAN(4);
2510 LOCATE 11,20:PRINT"CONTINUE WITH BUTTON"
2520 FOR SCR=1 TO 25:OUT &BC00,6:OUT &BD00,SCR:CALL &BD19:NEXT
2530 IF JOY(0)=16 THEN 250 ELSE CALL &BB06:GOTO 2530
2540 LOCATE 1,10:END
2550 '
2560 '  *** SCORING ***
2570 ' 
2580 PEN 1:PAPER 0:LOCATE 3,25:PRINT USING"#";NUM
2590 IF SCO>=99999 THEN SCO=99999 ELSE IF SCO<=0 THEN SCO=0
2600 IF SCO>TOP THEN TOP=SCO
2610 PEN 1:PAPER 0:LOCATE 13,25:PRINT USING"**###";SCO:IF SCO1=1 THEN SCO1=0:RETURN
2620 IF ROU>=99 THEN ROU=99 ELSE IF ROU<=0 THEN ROU=0
2630 PEN 1:PAPER 0:LOCATE 26,25:PRINT USING"**";ROU:IF ROU1=1 THEN ROU1=0:RETURN
2640 IF BUL>=99 THEN RUL=99 ELSE IF BUL<0 THEN BUL=0:GAO=1
2650 PEN 1:PAPER 0:LOCATE 37,25:PRINT USING"**";BUL:RETURN
2660 ' 
2670 ' *** MUSIC PART ***
2680 '
2690 CALL &BCA7*IN$=INKEY$:IF IN$<>""THEN 2690 ELSE SPE=8:RESTORE 2860
2700 ENV 5,3,5,1,15,-1,3:ENV 6,5,3,1,15,-1,5
2710 FOR BAC=1 TO 15:READ TON(BAC):NEXT
2720 STA=20:ADR3=42000:ADR2=42050:ADR1=42200:ADR4=42020
2730 IF(SQ(4)AND &X111)=4 THEN GOSUB 2780
2740 IF(SQ(1)AND &X111)=4 THEN GOSUB 2820
2750 IF(SQ(2)AND &X111)=4 THEN GOSUB 2840
2760 IF INKEY$<>""THEN RETURN
2770 GOTO 2730
2780 SOUND 4,TON(STA+PEEK(ADR3)),2*SPE,0,5,0,PEEK(ADR3+1):ADR3=ADR3+2:IF ADR3>42015 THEN ADR3=42000:GOSUB 2800
2790 RETURN
2800 STA=PEEK(ADR4):ADR4=ADR4+1:IF STA=255 THEN ADR4=42019:GOTO 2800
2810 RETURN
2820 SOUND 1,PEEK(ADR1)+256*PEEK(ADR1+1),PEEK(ADR1+2)*SPE,0,6:ADR1=ADR1+3:IF PEEK(ADR1)=255 THEN ADR1=42200
2830 RETURN
2840 SOUND 2,PEEK(ADR2)+256*PEEK(ADR2+1),PEEK(ADR2+2)*SPE,0,6:ADR2=ADR2+3:IF PEEK(ADR2)=255 THEN ADR2=42050
2850 RETURN
2860 DATA 965,851,758,716,638,568,506,478,426,379,358,319,284,253,239
2870 ' 
2880 '  *** DATAS FOR PICTURES ***
2890 '
2900 DATA 140,140,132,140,140,132,140,140,132,140,140,132,032,140,140,132,132
2910 DATA 032,132,140,140,132,140,140,132,140,140,132,140,140,132,140,140,132
2920 DATA 140,140,132,141,140,129,141,140,032,141,140,132,032,133,032,032,141
2930 DATA 140,132,141,140,133,133,032,133,133,032,133,032,133,032,141,140,032
2940 DATA 141,140,129,141,140,132,141,140,133,141,140,132,140,140,133,032,133
2950 DATA 032,032,140,140,133,133,032,133,141,140,133,141,140,133,032,133,032
2960 DATA 141,140,132,133,032,133,140,140,133,000,000,000,000,000,000,000,000
2970 DATA 4,5,A,7,5,B,10,5,C,13,5,D,16,5,E,19,5,F,22,5,G,25,5,H,28,5,I,31,5,J
2980 DATA 34,5,K,4,8,L,7,8,M,10,8,N,13,8,O,16,8,P,19,8,Q,22,8,R,25,8,S,28,8,T
2990 DATA 31,8,U,34,8,V,4,11,W,7,11,X,10,11,Y,13,11,Z,16,11,[,19,11,],22,11,-
3000 DATA 25,11,.,28,11,#,31,11,<,34,11,=,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
3010 DATA 4950,MCS...,4500,HEIKO.,3975,DUSSEL,3675,MICHI.,3425,DONALD
3020 DATA 2975,DETTO.,2375,MCS...,1825,KING-2,1325,FUNBOY,0850,SONNY.
3030 DATA 73,91,5,153,171,10,233,251,15,409,427,26,489,507,31,569,587,36