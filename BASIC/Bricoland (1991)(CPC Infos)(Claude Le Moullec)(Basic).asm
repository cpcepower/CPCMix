10 REM :::::::::::::::::::::::
20 REM :                     :
30 REM :    PRESENTATION     :
40 REM :    facultative      :
50 REM :                     : 
60 REM :::::::::::::::::::::::  
70 DIM JE(18,4):RESTORE 120:FOR H=1 TO 18:FOR G=1 TO 4:READ A:JE(H,G)=A:NEXT G,H
80 EVERY 10,2 GOSUB 620:REM la musique  
90 MODE 1:INK 0,0:INK 1,3:INK 2,24:INK 3,6:BORDER 0:B=2:GOSUB 450
100 ORIGIN 60,180:FOR H=1 TO 18:X=JE(H,1):Y=JE(H,2):L=JE(H,3):P=JE(H,4)
110 GOSUB 150:NEXT:GOTO 200
120 DATA 16,16,10,3,16,32,2,2,96,64,4,2,16,48,2,3,116,88,2,3,16,64,2,3,116,104
130 DATA 2,3,16,80,10,2,16,96,2,3,116,136,2,3,16,112,2,3,116,152,2,3,16,128,2
140 DATA 2,96,160,4,2,16,144,10,3,-16,-100,5,2,370,-50,9,1,250,-150,3,3,0,0,0,0
150 IF L<11 THEN GOSUB 170 ELSE W=L:L=10:X=X+(W-10)*10:Y=Y+(W-10)*4:GOSUB 170:L=W-10:X=X-(W-10)*10:Y=Y-(W-10)*4:IF L=0 THEN RETURN ELSE GOSUB 150
160 RETURN
170 FOR A=0 TO 16 STEP 2:PLOT X-B*8,Y+B*6+A,P:DRAWR 8*B,-6*B:DRAWR L*10,L*4:NEXT:FOR A=0 TO B STEP 0.1:PLOT X-A*8,Y+A*6+18:DRAWR 10*L,4*L:NEXT
180 FOR A=1 TO L:FOR C=1 TO B:PLOT X+A*10-4-8*C,Y+C*6+A*4+10,1+(P=1):DRAWR 4,0:DRAWR 0,2:DRAWR -6,0:DRAWR 0,2:DRAWR 2,2:DRAWR 4,0:DRAWR 0,-2:NEXT C,A
190 PLOT X,Y:DRAWR 10*L,4*L:DRAWR 0,18:DRAWR -8*B,6*B:DRAWR -10*L,-4*L:DRAWR 0,-18:DRAWR 8*B,-6*B:DRAWR 0,18:DRAWR -8*B,6*B:DRAWR 8*B,-6*B:DRAWR 10*L,4*L:RETURN
200 ORIGIN 0,0:OUT &BC00,6:OUT &BD00,24
210 a$="RICOLAND":X=220:y=324:GOSUB 220:GOTO 300
220 a=LEN(a$):a8=a*8   
230 LOCATE 1,25:PEN 1:PRINT a$;:PEN 1  
240 tx=x:y2=14   
250 FOR f=1 TO 8:x2=0:FOR g=1 TO a8
260 IF TEST( x2,y2)=1 THEN PLOT x+4,y,2:PLOT x+4,y-2:PLOT x,y-2:PLOT x+2,y-2:PLOT x+4,y-4:PLOT x+2,y-4:PLOT x,y-4:PLOT x-2,y+2,3:PLOT x,y+2:PLOT x+2,y+2:PLOT x+2,y:PLOT x-2,y:PLOT x,y
270 x=x+6:x2=x2+2  
280 NEXT g:y=y-6:tx=tx:y2=y2-2:x=tx:NEXT f   
290 LOCATE 1,25:PRINT SPACE$(LEN(A$)):RETURN   
300 PEN 2:LOCATE 19,12:PRINT "BY"
310 a$="LMC SOFTWARE":y=100:ENC=3:t=0:GOSUB 330  
320 y=96:ENC=2:t=1:GOSUB 330:GOTO 430
330 A=LEN(A$):P=A*8:IF t=0 THEN 350   
340 x=(646-A*32)/2:GOTO 360  
350 x=(639-A*32)/2  
360 PEN 2:LOCATE 1,25:PRINT A$;:PEN 1  
370 tx=x:y2=14 
380 FOR i=1 TO 8:x2=0:FOR j=1 TO P 
390 IF TEST(x2,y2)=2 THEN PLOT x,y,ENC:PLOT x,y-2:PLOT x+2,y:PLOT x+2,y-2 
400 x=x+4:x2=x2+2:NEXT j     
410 y=y-4:y2=y2-2:x=tx:NEXT i:RETURN  
420 NEXT i:RETURN 
430 LOCATE 1,25:PRINT SPACE$(A):OUT &BC00,6:OUT &BD00,25
440 WHILE INKEY$="":WEND:GOTO 520
450 FOR f=1 TO 20:Q=(RND*560)+40:W=(RND*350)+32
460 PLOT Q,W+30,9:DRAW Q,W-30:PLOT Q+52,W:DRAW Q-52,W:PLOT Q+8,W+4:PLOT Q-8,W+4:PLOT Q-8,W-4:PLOT Q+8,W-4 
470 PLOT Q+4,W+8:DRAW Q+4,W-8:PLOT Q-4,W+8:DRAW Q-4,W-8:PLOT Q+16,W+2:DRAW Q-16,W+2:PLOT Q+16,W-2:DRAW Q-16,W-2:PLOT Q,W+28,0:PLOT Q,W-28:PLOT Q+48,W:PLOT Q-48,W    
480 PLOT Q,W+6,10:DRAW Q+12,W:DRAW Q,W-6:DRAW Q-12,W:DRAW Q,W+6:PLOT Q,W+18:DRAW Q,W-18:PLOT Q+32,W:DRAW Q-32,W:PLOT Q,W+16,9:PLOT Q,W-16:PLOT Q+28,W:PLOT Q-28,W:PLOT Q,W+10,11:DRAW Q,W-10:PLOT Q+16,W:DRAW Q-16,W  
490 PLOT Q,W+4,12:DRAW Q,W-4:PLOT Q+4,W:DRAW Q-4,W:PLOT Q+4,W+2,11:PLOT Q-4,W+2:PLOT Q-4,W-2:PLOT Q+4,W-2       
500 NEXT:FOR F=1 TO 100:PLOT RND*640,RND*384+16,2:NEXT 
510 RETURN
520 BORDER 0:MODE 1:INK 0,0:INK 1,26:INK 2,13:INK 3,15 
530 LOCATE 16,2:PEN 1:PRINT "BRICOLAND":LOCATE 16,3:PEN 3:PRINT "========="
540 PEN 2:LOCATE 1,5:PRINT"  Apres bien des aventures BILLY THE KIDrentre chez lui."  
550 PEN 2:LOCATE 1,8:PRINT"    Malheureusement le chemin  du retourest parsem{ d'embuches et  BILLY qui esttres fatigu{s  ne  regarde pas  ou il metles pieds."
560 LOCATE 1,13:PRINT"    Votre tache sera  de lui aplanir lesdifficult{s rencontr{es sur son passage."   
570 LOCATE 1,16:PRINT"    Pour cela vous utiliserez le T{l{porteur et les {l{ments dud{cor mis @ votredisposition."  
580 LOCATE 1,20:PRINT"   La prise et la restitution des objetsse fait en appuyant sur la touche <FIRE>ou la touche <COPY>"    
590 LOCATE 11,24:PEN 3:PRINT "JOYSTICK ou CURSEUR"  
600 LOCATE 32,25:PEN 1:PRINT "<ENTER>" 
610 WHILE INKEY$="":WEND:RUN "BRICLAND"
620 READ d:IF d=-1 THEN RESTORE 650:GOTO 620 
630 IF d=0 THEN v=0:d1=0 ELSE v=1:d1=d 
640 SOUND 17,d,20,15*v:SOUND 10,d1,20,0:RETURN 
650 DATA 478,0,478,0,478,0,478,0,478,506,478,426,379,0,379,0,319,338,319,239
660 DATA 190,0,190,0,213,190,179,253,213,0,213,0,319,338,319,253,213,0,213,0
670 DATA 179,190,179,127,106,0,106,0,113,106,95,127,119,0,119,0,95,106,95,89 
680 DATA 80,0,80,0,71,80,89,95,106,0,106,0,106,113,106,95,89,0,89,0,80,89,95
690 DATA 106,119,0,119,0,119,127,119,106,95,0,95,0,89,95,106,119,127,0,0,0
700 DATA 142,0,0,0,159,0,0,0,179,0,0,0,190,0,0,0,213,0,0,0,-1 