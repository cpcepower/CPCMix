10 'DEF. VARIABLES,FONCTIONS..
20 ENT 1,4,2,1,8,-2,1,8,2,1,4,-2,1:ENT-2,6,-8,1,12,1,1,6,-1,1:ENV 3,10,110,100:ENV 4,5,3,1,1,0,90,5,-3,2:ENT 4,25,10,2,60,-10,1:GOSUB 640:DEFINT A-Y:DEF FNPOINT(XX,YY)=TEST((XX-1)*32,(25-YY)*16):DEF FNTEMPS=479-((TIME-ZTIME)/100)*2
30 DEF FNMULTI(N,M)=(INT(N/M)=N/M):NIV=1:DIM CB$(8,26),DN$(3):VC=1:EJ$=CHR$(24):EC$="":DN$(1)=CHR$(24):SYMBOL AFTER 200:INK 1,26:PEN 1:GOSUB 150:GOTO 780
40 GOSUB 1180:MODE 0:INK 0,0:INK 1,24:INK 2,7:INK 3,6:INK 4,26:INK 5,0:INK 6,2:INK 7,15:INK 8,18:INK 9,7:INK 10,14:INK 11,8:INK 12,26,0:PEN 4:PAPER 5:CLS
50 IF VC=1 THEN P1$=CHR$(&8):P2$=CHR$(&9):P3$=CHR$(&A):P4$=CHR$(&B)ELSE P1$=CHR$(&F2):P2$=CHR$(&F3):P3$=CHR$(&F1):P4$=CHR$(&F0)
60 VIE=3:SCORE=0:NUPO=0:IF NIV=1 THEN D=5 ELSE IF NIV=2 THEN D=3 ELSE IF NIV=3 THEN D=2
70 RESTORE 1170:SYMBOL 200,129,126,126,126,126,126,126,129:SYMBOL 221,12,30,55,127,55,30,12:SYMBOL 222,48,120,236,254,236,120,48:SYMBOL 223,8,8,28,62,107,127,62,28:SYMBOL 224,28,62,127,107,62,28,8,8:SYMBOL 250,126,219,255,189,195,219,126
80 GOSUB 1020:CA=1
90 FCA=0:FICA=1:DEJA=0:X=6:Y=19:X1=X:Y1=Y:XE=14:YE=6:BP=3:PEN 4:PAPER 5:IF CA-1=26 THEN GOTO 520
100 'CARRE CARACTERES
110 ABC$=STRING$(10,CHR$(143)):FOR A=1 TO 14:LOCATE 6,5+A:PRINT ABC$:NEXT:PEN 1:'CARACTERES
120 FOR A=1 TO 8:FOR I=1 TO 8:IF VAL(MID$(CB$(A,CA),I,1))=1 THEN LOCATE 6+I,8+A:PRINT" ":FCA=FCA+1
130 NEXT I:NEXT A:CA=CA+1:GOSUB 1130:IF CA=2 THEN GOSUB 470
140 PAPER 6:FCA=140-FCA:A$=INKEY$:GOTO 170
150 'TAB.CARACTERES
160 GOSUB 1180:MODE 2:FOR I=1 TO 26:LOCATE 1,1:PRINT CHR$(I+64):LOCATE 30,10:PRINT"PATIENTEZ S.V.P":FOR ZP=49152 TO 63491 STEP 2048:C=PEEK(ZP):CB$(ZP/2048-23,I)=(BIN$(C,8)):NEXT:NEXT:RETURN
170 'JEU 
180 ZTIME=TIME:MOVE 160,328:DRAW 478,328,7:MOVE 160,330:DRAW 478,330,7:MOVE 160,332:DRAW 478,332,7:LOCATE 2,3:PAPER 5:PRINT STRING$(VIE,CHR$(223)):PEN 11:LOCATE 18,3:PRINT STR$(NIV):PAPER 5:PEN 1:LOCATE 15,22:PAPER 5:PRINT USING"#####";SCORE+1
190 LOCATE 2+VIE,3:PAPER 5:PRINT"    ":PAPER 6' (4 ESPACES)
200 IF A$<>""THEN LOCATE X1,Y1:PRINT" ":IF DEJA=0 THEN FICA=FICA+1:LOCATE 15,22:PAPER 5:PRINT USING"#####";SCORE+FICA:PAPER 6 ELSE DEJA=0 ELSE GOTO 230
210 'IF X1=XE AND Y1=YE THEN PEN 9:PAPER FNPOINT (XE,YE):LOCATE XE,YE:PRINT CHR$(250):PAPER 6:PEN 1
220 IF FCA=FICA THEN GOTO 490
230 'IF X=XE AND Y=YE THEN ZTIME=ZTIME-500:SOUND 2,50,5,15:PEN 9:PAPER FNPOINT (XE,YE):LOCATE XE,YE:PRINT CHR$(250):PAPER 6:PEN 1:REN=8 ELSE REN=1
240 X1=X:Y1=Y:TEMPS=FNTEMPS:FOR I=REN TO 0 STEP-1:PLOT TEMPS+I,328,5:PLOT TEMPS+I,330:PLOT TEMPS+I,332:NEXT:IF TEMPS<160 THEN 410
250 A$=INKEY$:IF A$=P1$THEN X=X-1:BP=1
260 IF A$=P2$THEN X=X+1:BP=2
270 IF A$=P4$THEN Y=Y-1:BP=3
280 IF A$=P3$THEN IF X=6 AND Y=19 THEN 290 ELSE Y=Y+1:BP=4
290 IF FNMULTI(QP,D)=-1 THEN GOSUB 350
300 QP=QP+1:IF FNMULTI(QP,10)=-1 THEN GOSUB 1100:IF QP=20 THEN QP=0
310 IF A$=""THEN GOTO 340
320 IF FNPOINT(X,Y)=5 THEN GOTO 410 ELSE IF FNPOINT(X,Y)=6 OR FNPOINT(X,Y)=1 THEN DEJA=1
330 SOUND 5,560,15,0,4,4
340 LOCATE X,Y:PRINT CHR$(220+BP):GOTO 200
350 'BUG
360 RETURN:XE1=XE:YE1=YE:IF X<XE THEN XE=XE-1:GOTO 380
370 IF X>XE THEN XE=XE+1
380 IF Y>YE THEN YE=YE+1:GOTO 400
390 IF Y<YE THEN YE=YE-1
400 LOCATE XE1,YE1:PAPER FNPOINT(XE1,YE1):PEN 9:PRINT" ":PAPER FNPOINT(XE,YE):LOCATE XE,YE:PRINT CHR$(250):PAPER 6:PEN 1:RETURN:END
410 'MORTS
420 LOCATE X,Y:PRINT CHR$(220+BP):PAPER 6:LOCATE X1,Y1:PRINT" ":SOUND 1,2000,10,15,,1:IF TEMPS>=160 THEN PAPER 5
430 LOCATE X,Y:PRINT" ":IF VIE=1 THEN GOTO 580
440 X=6:Y=19:X1=X:Y1=Y:DEJA=1:PAPER FNPOINT(XE,YE):LOCATE XE,YE:PRINT" ":XE=15:YE=6:XE1=XE:YE1=YE:VIE=VIE-1:LOCATE 2,2:PAPER 5:PRINT"     ":IF TEMPS<160 THEN ZTIME=TIME
450 TEMPS=FNTEMPS:MOVE 160,328:DRAW TEMPS,328,7:MOVE 160,330:DRAW TEMPS,330,7:MOVE 160,332:DRAW TEMPS,332,7'IF Y>=6 THEN TEMPS=FNTEMPS:MOVE 160,328:DRAW TEMPS,328,7:MOVE 160,330:DRAW TEMPS,330,7:MOVE 160,332:DRAW TEMPS,332,7
460 GOTO 190
470 'MICROPROCESSEUR
480 PEN 8:LOCATE 3,21:PRINT STRING$(3,CHR$(143)):LOCATE 3,22:PRINT STRING$(3,CHR$(143)):PLOT 62,78,8:DRAW 62,48:PLOT 160,78:DRAW 160,48:FOR I=64 TO 162 STEP 12:PLOT I,80,10:PLOT I,46:NEXT:PEN 1:RETURN
490 'LETTRE SUIVANTE
500 PLOT 62+((CA-2)*4),78,10:DRAW 62+((CA-2)*4),48:TEMPS=FNTEMPS:PAPER 5:SCORE=SCORE+FICA:FOR I=TEMPS TO 150 STEP-10:FOR II=10 TO 0 STEP-1:PLOT I+II,328,5:PLOT I+II,330:PLOT I+II,332:NEXT II:SCORE=SCORE+2:SOUND 2,10,12,15,,1:LOCATE 15,22
510 PRINT USING"#####";SCORE:NEXT:MOVE 160,328:DRAW 478,328,5:MOVE 160,330:DRAW 478,330:MOVE 160,332:DRAW 478,332:PAPER 6:GOTO 90
520 'VICTOIRE TOTALE
530 SPEED KEY 30,2:FOR I=1 TO 250 STEP 5:INK 4,INT(RND*5)+10:SOUND 1,I,5,15:FOR A=1 TO 10:INK 5,INT(RND*10):NEXT A:SOUND 4,250-I,5,15:NEXT I:SPEED INK 20,20:INK 5,0:LOCATE 7,21:PAPER 5:PEN 12:PRINT" BRAVO ":FOR I=1 TO 5000:NEXT:GOSUB 1180:MODE 2:INK 1,21
540 INK 0,0:PEN 1:PAPER 0:BORDER 0:SCORE=SCORE+FICA:LOCATE 34,2:PRINT"ALPHA BUG":LOCATE 15,8:PRINT"En faisant un score de ";SCORE;"points,(RIEN QUE CA !!!)":LOCATE 15,10:PRINT"vous avez reussi toutes les lettres.":LOCATE 15,12
550 PRINT"MERCI,je dispose a nouveau de toutes mes majuscules.":LOCATE 15,15:PRINT"Maintenant,vous pouvez enfin vous reposer ...":FOR I=1 TO 5:SOUND 5,978,700,15,,2,0:NEXT
560 IF INKEY$=""THEN 560 ELSE SOUND 165,0,0:GOTO 780
570 'MORT DEFINITIVE
580 SPEED KEY 30,2:LOCATE 2,3:PAPER 5:PRINT"    ":PAPER 6:FOR I=1 TO 200:NEXT:FOR I=1 TO 40 STEP 2:SOUND 1,10*I,10,15,,3:FOR J=1 TO 10:INK 5,INT(RND*26)+1:NEXT:SOUND 2,10*I,10,15,,3:NEXT:SPEED INK 20,20:INK 5,0:LOCATE 7,21:PAPER 5:PEN 12:PRINT"GAME OVER"
590 FOR I=1 TO 5000:NEXT:FOR I=1 TO 10:A$=INKEY$:NEXT:GOSUB 1180:MODE 2:INK 1,21:INK 0,0:PEN 1:PAPER 0:BORDER 0:SCORE=SCORE+FICA:LOCATE 34,2:PRINT"ALPHA BUG":LOCATE 22,10:PRINT"Vous avez fait un score de ";SCORE;"points"
600 IF CA-2=0 THEN LOCATE 22,13:PRINT"Vous n'avez reussi aucune lettre.":GOTO 620
610 LOCATE 22,13:PRINT"vous avez reussi ";CA-2;" lettre(s)."
620 IF INKEY$=""THEN GOTO 620 ELSE GOTO 780
630 'PRESENTATION
640 MODE 1:INK 0,0:BORDER 0:PAPER 0:INK 1,24:INK 3,6:INK 2,20:FOR TT=1 TO 16 STEP 2:MOVE 312,0:DRAW 632-TT,200,1:MOVE 312,399:DRAW 632-TT,200,1:MOVE 312,0:DRAW 7+TT,200,1:MOVE 312,399:DRAW 7+TT,200,1:NEXT:FOR TT=1 TO 26 STEP 2:MOVE 312,0
650 DRAW 616-TT,200,3:MOVE 312,399:DRAW 616-TT,200,3:MOVE 312,0:DRAW 23+TT,200,3:MOVE 312,399:DRAW 23+TT,200,3:NEXT:FOR TT=1 TO 16 STEP 2:MOVE 312,0:DRAW 590-TT,200,1:MOVE 312,399:DRAW 590-TT,200,1:MOVE 312,0:DRAW 49+TT,200,1:MOVE 312,399
660 DRAW 49+TT,200,1:NEXT:PEN 2:LOCATE 16,9:PRINT"A L P H A":LOCATE 18,17:PRINT"B U G":PEN 0:PAPER 2:LOCATE 11,13:PRINT CHR$(164)+" 1986 Fabrice DUBACH":A$=INKEY$:INK 3,6,24:INK 1,24,6:SPEED INK 4,4:'MUSIQUE
670 FOR J=1 TO 2:RESTORE 700:IF J=1 THEN V=4.6 ELSE V=4.4
680 FOR I=1 TO 274:READ A,B:IF INKEY$<>""THEN RETURN
690 SOUND 1,A,B*V,12:SOUND 2,A-1,B*V,12:SOUND 4,A+1,B*V,12:NEXT I,J:RETURN
700 DATA 142,4,179,4,284,4,142,4,179,4,284,4,142,4,179,4,284,4,142,4,179,4,284,4,159,4,179,4,284,4,179,4,284,4,358,4,179,4,284,4,358,4,190,4,284,4,358,4,213,4,284,4,358,4,213,4,284,4,358,4,179,4,213,4,284,4,142,4,179,4
710 DATA 213,4,106,4,142,4,179,4,106,4,142,4,179,4,106,4,142,4,179,4,106,4,142,4,179,4,119,4,142,4,179,4,134,4,179,4,213,4,134,4,213,4,268,4,142,4,213,4,268,4,159,4,213,4,268,4,159,4,213,4,268,4,142,4,213,4,268,4,134,4
720 DATA 213,4,268,4,142,4,190,4,225,4,134,4,190,4,225,4,142,4,190,4,225,4,113,4,190,4,225,4,134,4,190,4,225,4,142,4,190,4,225,4,142,4,179,4,284,4,159,4,179,4,284,4,179,4,284,4,358,4,179,4,284,4,358,4,190,4,284,4,358,4
730 DATA 213,4,284,4,358,4,190,4,284,4,319,4,190,4,284,4,319,4,190,4,284,4,319,4,190,4,284,4,319,4,179,4,284,4,319,4,190,4,284,4,319,4,213,4,284,4,358,4,213,4,284,4,358,4,213,4,284,4,358,4,213,16,1,8
740 DATA 169,4,284,4,338,4,169,4,284,4,338,4,169,4,284,4,338,4,169,4,284,4,338,4,190,4,284,4,338,4,213,4,284,4,338,4,213,4,319,4,379,4,225,4,319,4,379,4,225,4,319,4,379,4,225,4,319,4,379,4,239,4,319,4,379,4,225,4,319,4
750 DATA 379,4,127,4,190,4,225,4,127,4,190,4,225,4,127,4,190,4,225,4,127,4,190,4,225,4,113,4,190,4,225,4,127,4,190,4,225,4,127,4,169,4,213,4,142,4,169,4,213,4,142,4,169,4,213,4,142,4,169,4,213,4,127,4,169,4,213,4,113,4
760 DATA 169,4,213,4,106,4,169,4,213,4,106,4,169,4,213,4,106,4,169,4,213,4,106,4,169,4,213,4,113,4,169,4,213,4,119,4,169,4,213,4,127,4,213,4,253,4,127,4,213,4,253,4,127,4,213,4,253,4,127,4,213,4,253,4,142,3,213,4,253,4
770 DATA 159,4,213,4,253,4,169,4,213,4,253,4,169,4,213,4,253,4,169,4,213,4,253,4,169,4,225,4,319,4,159,4,225,4,319,4,190,4,225,4,319,4,213,4,284,4,338,4,213,4,284,4,338,4,213,4,284,4,338,4,213,16,1,8
780 'MENU
790 GOSUB 1180:INK 1,21:INK 0,0:BORDER 0:LOCATE 34,2:PRINT"ALPHA BUG":LOCATE 22,7:PRINT"1-Regles":LOCATE 22,9:PRINT"2-Choix du niveau de difficulte":LOCATE 22,11:PRINT"3-Jeu"
800 LOCATE 22,13:PRINT EJ$+"4-Joystick"+EJ$:LOCATE 22,15:PRINT EC$+"5-Touches curseur"+EC$
810 LOCATE 15,19:INPUT"Votre choix ";A$:IF A$<>"1"AND A$<>"2"AND A$<>"3"AND A$<>"4"AND A$<>"5"THEN LOCATE 15,18:PRINT SPACE$(80);CHR$(7):GOTO 810
820 IF A$="3"THEN SPEED KEY 10,10:GOTO 40
830 IF A$="4"THEN VC=1:EJ$=CHR$(24):EC$="":LOCATE 15,18:PRINT SPACE$(80)
840 IF A$="5"THEN VC=2:EC$=CHR$(24):EJ$="":LOCATE 15,18:PRINT SPACE$(80)
850 IF A$="1"THEN 910
860 IF A$<>"2"THEN GOTO 800
870 'NIVEAU DIFFICULTE
880 GOSUB 1180:LOCATE 34,2:PRINT"ALPHA BUG":LOCATE 22,7:PRINT DN$(1);"1-Difficile";DN$(1):LOCATE 22,9:PRINT DN$(2);"2-Tres difficile";DN$(2):LOCATE 22,11:PRINT DN$(3);"3-Impossible";DN$(3)
890 LOCATE 15,15:LINE INPUT"Votre choix :";A$:IF A$<>"1"AND A$<>"2"AND A$<>"3"THEN PRINT CHR$(7):GOTO 890
900 NIV=VAL(A$):FOR I=1 TO 3:DN$(I)="":NEXT:DN$(NIV)=CHR$(24):GOTO 780
910 'REGLES
920 GOSUB 1180:LOCATE 17,1:PRINT CHR$(24)+"Ceci est un S.O.S. de votre microprocesseur Z80":LOCATE 12,3:PRINT CHR$(24)+"Mes majuscules sont attaquees par un bug,sauvez-les !!!!":LOCATE 5,5:PRINT"Ce bug a peint toute mes majuscules en blanc !!!"
930 LOCATE 5,6:PRINT"Il faut absolument que vous les repeigniez en bleu.(leur couleur naturelle)":LOCATE 5,7:PRINT"Et il faut vous depecher car vous perdez regulierement de l'energie.":LOCATE 5,8
940 PRINT"De plus si vous avez le malheur de rencontrer le bug (qui vous poursuit),":LOCATE 5,9:PRINT"La perte d'energie est multipliee par cinq !":LOCATE 5,10:PRINT"Et bien entendu lorsque vous n'avez plus d'energie...":LOCATE 5,11
950 PRINT"vous mourrez ......"'LOCATE 5,11:PRINT"vous mourrez "+CHR$(24)+"definitivement." 
960 LOCATE 5,12:PRINT"Heureusement vous possedez trois vies !!!":LOCATE 5,13:PRINT"Mais attention car il vous suffit de depasser la limite des lettres pour":LOCATE 5,14:PRINT"en perdre une de plus ....!":LOCATE 5,16
970 PRINT"Un indicateur (en forme de microprocesseur) place en bas a gauche":LOCATE 5,17:PRINT"vous indique:":LOCATE 7,18:PRINT"-la partie verte:les majuscules encore non-repeintes":LOCATE 7,19:PRINT"-la partie bleu :les majuscules repeintes":LOCATE 5,21
980 PRINT"Votre score est indique en bas a droite et augmente de 1 point":LOCATE 5,22:PRINT"par case peinte.A la fin de chaque lettre un bonus proportionnel au":LOCATE 5,23:PRINT"temps restant s'ajoute au score.Le niveau de difficulte est affiche"
990 LOCATE 5,24:PRINT"en haut a droite.":LOCATE 34,25:PRINT"BONNE CHANCE"
1000 IF INKEY$=""THEN GOTO 1000
1010 GOTO 780:'CADRE ECRAN
1020 LOCATE 6,25:PRINT"ALPHA BUG":ICO=1:FOR I=8 TO 620 STEP 4:CO=CO+ICO:IF CO=-6 THEN ICO=1
1030 IF CO=6 THEN ICO=-1
1040 IF I>150 AND I<450 THEN PLOT I,0,1:PLOT I,16 ELSE PLOT I,5+CO,3
1050 PLOT I,195*2+CO,3:NEXT:FOR I=0 TO 195*2 STEP 2:CO=CO+ICO
1060 IF I<6 THEN PLOT 152,I,1:PLOT 452,I,1:I=I+2:GOTO 1060 ELSE IF I<=16 THEN PLOT 152,I,1:PLOT 452,I,1
1070 IF CO=-6 THEN ICO=1
1080 IF CO=6 THEN ICO=-1
1090 PLOT 8+CO,I,3:PLOT 624+CO,I,3:NEXT I:RETURN:'GESTION PONTS
1100 PAPER 5:FOR I=1 TO NPO:LOCATE POV(I),POY(I):PRINT" ":IF POV(I)=PO(I,1)THEN PPO(I)=1
1110 IF POV(I)=PO(I,2)THEN PPO(I)=-1
1120 POV(I)=POV(I)+PPO(I):LOCATE POV(I),POY(I):PRINT CHR$(200):NEXT I:PAPER 6:RETURN:'DATA PONTS
1130 IF NUPO=0 OR CA-1>NUPO AND CA-1<19 THEN READ NUPO
1140 IF CA-1=NUPO THEN READ NPO ELSE NPO=0:RETURN
1150 PPO=0:FOR I=1 TO NPO:READ POY(I),PO(I,1),PO(I,2):IF I=2 THEN POV(I)=PO(I,2)ELSE POV(I)=PO(I,1)
1160 LOCATE POV(I),POY(I):PRINT CHR$(200):NEXT:RETURN
1170 DATA 1,1,13,10,11,2,2,12,10,11,15,10,11,4,1,15,9,11,15,1,15,9,11,16,1,12,10,12,17,3,15,8,10,14,11,12,15,12,13,18,1,12,10,11
1180 'SCROLING
1190 SCR=INT(RND*2)+1:PAPER 0:ON SCR GOTO 1200,1220
1200 'SCROLING 
1210 FOR I=1 TO 15:LOCATE 1,27:PRINT CHR$(10):NEXT:LOCATE 1,1:RETURN
1220 'SCROLING 

1230 FOR I=1 TO 25:LOCATE 1,1:PRINT CHR$(11):NEXT:RETURN